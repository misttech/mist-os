# Copyright 2024 Mist Tecnologia LTDA
# Copyright 2019 The Fuchsia Authors
#
# Use of this source code is governed by a MIT-style
# license that can be found in the LICENSE file or at
# https://opensource.org/licenses/MIT

import("//build/toolchain/ifs_extract.gni")
import("//build/toolchain/toolchain_environment.gni")
import("//build/toolchain/zircon/user_basic_redirect.gni")
import("//build/zircon/c_utils.gni")
import("//build/zircon/zircon_cpu.gni")

# This is how the kernel depends on the built vDSO to embed the image, and
# userboot depends on it to extract addresses, via the link_output_rspfile()
# metadata protocol.
user_basic_redirect("vdso") {
  public_deps = [
    ":liblinux",

    # This is just an independent testing step that doesn't produce any
    # output. But making it a dependency here ensures that a kernel can't be
    # built with a vDSO that doesn't have a verified ABI.
    ":verify-abi",
  ]
}

if (toolchain_environment == "user.basic") {
  # The vDSO is built as a loadable_module() with an explicit -soname switch
  # rather than as a shared_library() so that it gets independent variant
  # selection.
  loadable_module("liblinux") {
    visibility = [ ":*" ]

    ldflags = [ "-Wl,-soname=liblinux.so" ]

    configs += [ "//build/config/zircon:rodso" ]

    exclude_toolchain_tags = [
      # The vDSO can use only the basic machine ABI.
      "needs-compiler-abi",

      # The vDSO cannot have a writable segment.
      "needs-writable-globals",
    ]

    # FIXME (Herrera) Temporary disable file compilation 
    # (need to fix zither legacy_syscall_cdecl and c backends)
    # to work as in the mistos backend (c.go).
    #sources = [ "syscall-wrappers.cc" ]

    public_deps = [ "//zircon/kernel/lib/mistos/vdso:public" ]
    deps = [
      ":syscall-asm",
      "//zircon/kernel/lib/mistos/userabi:headers",
      "//zircon/kernel/lib/mistos/vdso:private",
      "//zircon/system/ulib/affine",
    ]

    # This ensures that the vDSO is never installed into filesystem images
    # since it's materialized at runtime by the kernel.
    metadata = {
      distribution_entries_barrier = []
      distribution_entries = []
    }
  }

  source_set("syscall-asm") {
    visibility = [ ":*" ]

    sources = [ "syscalls-$zircon_cpu.S" ]
    deps = [ "//zircon/kernel/lib/mistos/vdso:private" ]
  }

  # Make sure the vDSO that goes into the kernel matches the ABI
  # that userland links against.
  abi_ifs = "//zircon/kernel/lib/mistos/vdso/liblinux.ifs"
  vdso_ifs = "$target_gen_dir/liblinux.ifs"

  ifs_extract("vdso.ifs") {
    visibility = [ ":*" ]

    outputs = [ vdso_ifs ]
    deps = [ ":liblinux" ]
    args = [ "--strip-ifs-target" ]
  }

  action("verify-abi") {
    visibility = [ ":*" ]

    deps = [ ":vdso.ifs" ]

    sources = [
      abi_ifs,
      vdso_ifs,
    ]
    outputs = [ "$target_out_dir/$target_name.ok" ]

    script = "verify-abi.sh"

    # This uses absolute paths so the "cp IMPL ABI" message has easily and
    # universally cut&pastable paths.
    args = rebase_path(sources + outputs, root_build_dir)

    metadata = {
      link_output_barrier = []
    }
  }
}

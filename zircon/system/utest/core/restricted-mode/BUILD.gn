# Copyright 2021 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

restricted_blob_name = "\"test/restricted-blob.$current_cpu.so\""

# Encodes the default ELF machine for the blob and indicates that there
# is no memory allocation upper limit.
restricted_blob_info =
    "{$restricted_blob_name, elfldltl::ElfMachine::kNative, 0}"

source_set("restricted-mode") {
  testonly = true

  sources = [
    "$current_cpu.S",
    "arch-register-state.$current_cpu.cc",
    "restricted-mode.cc",
  ]

  defines = []

  # https://fxbug.dev/382971270 enable lower mem allocations when under asan
  if (current_cpu != "arm64" || is_asan) {
    defines += [
      "RESTRICTED_BLOB_COUNT=1",
      "RESTRICTED_BLOB_INFO={ $restricted_blob_info }",
    ]
  }

  deps = [
    "..:needs-next",
    "//src/lib/elfldltl",
    "//src/lib/elfldltl/testing:elfldltl-zxtest",
    "//src/lib/symbolizer-markup",
    "//zircon/kernel/lib/arch",
    "//zircon/system/ulib/pretty",
    "//zircon/system/ulib/runtime",
    "//zircon/system/ulib/zircon-internal",
    "//zircon/system/ulib/zx",
    "//zircon/system/ulib/zxtest",
  ]

  # The restricted blob is built in the linux environment for two reasons:
  # 1. Most of the different ELF build environments for a given CPU are
  #    equivalent for pure assembly code, and that's all that's used in the
  #    restricted-blob.
  # 2. The restricted-blob is also built in the ARM32 build environment
  #    when building for ARM64 (see below), and the Fuchsia build
  #    environments do not support ARM32.
  data_deps = [ "restricted-blob(//build/toolchain:linux_$current_cpu)" ]

  # Presently, only arm64 offers a 32-bit restricted mode.
  # https://fxbug.dev/382971270 enable lower mem allocations when under asan
  if (current_cpu == "arm64" && !is_asan) {
    restricted_blob_arm_name = "\"test/restricted-blob.arm.so\""
    restricted_blob_arm_info =
        "{$restricted_blob_arm_name, elfldltl::ElfMachine::kArm, 0x100000000UL}"
    defines += [
      "RESTRICTED_BLOB_COUNT=2",
      "RESTRICTED_BLOB_INFO={ $restricted_blob_info, $restricted_blob_arm_info }",
    ]
    data_deps += [ "restricted-blob(//build/toolchain:linux_arm)" ]
  }
}

# Copyright 2021 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//zircon/system/ulib/acpica/acpica.gni")

config("acpica-config") {
  include_dirs = [ "include" ]
}

if (is_fuchsia) {
  source_set("acpica-fuchsia") {
    deps = [
      "//src/devices/pci/lib/pci",
      "//src/lib/ddk",
      "//third_party/acpica:headers",
      "//zircon/system/ulib/bitmap",
      "//zircon/system/ulib/fbl",
      "//zircon/system/ulib/zircon-internal",
    ]

    public_configs = [ ":acpica-config" ]

    sources = [
      "ioport.cc",
      "irq.cc",
      "mmio.cc",
      "mutex.cc",
      "osfuchsia.cc",
      "pci.cc",
      "semaphore.cc",
      "task.cc",
    ]

    defines = [ "_ALL_SOURCE" ]

    if (acpica_debug_output) {
      defines += [ "ACPI_DEBUG_OUTPUT" ]
    }
  }
} else if (is_mistos) {
  source_set("acpica-zircon") {
    deps = [
      "//third_party/acpica:headers",
      "//vendor/misttech/zircon/kernel/lib/mistos/util:sync-headers",
      "//zircon/kernel/lib/kpci",
      "//zircon/kernel/object",
      "//zircon/kernel/vm:headers",
      "//zircon/system/ulib/bitmap",
      "//zircon/system/ulib/fbl",
      "//zircon/system/ulib/zircon-internal",
    ]

    public_configs = [ ":acpica-config" ]

    sources = [
      "ioport.cc",
      "irq.cc",
      "mmio.cc",
      "mutex.cc",
      "oszircon.cc",
      "pci.cc",
      "semaphore.cc",
      "task.cc",
    ]

    defines = [ "_ALL_SOURCE" ]

    if (acpica_debug_output) {
      defines += [ "ACPI_DEBUG_OUTPUT" ]
    }

    configs += [
      "//build/config:default_include_dirs",
      "//vendor/misttech/src/lib/ddk:include",
    ]
  }
} else {
  group("acpica-fuchsia") {
  }
  group("acpica-zircon") {
  }
}

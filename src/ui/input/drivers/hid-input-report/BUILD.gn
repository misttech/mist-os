# Copyright 2019 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/bind/bind.gni")
import("//build/components.gni")
import("//build/drivers.gni")
import("//build/test.gni")

driver_bind_rules("hid_input_report_bind") {
  rules = "meta/hid_input_report.bind"
  bind_output = "hid-input-report.bindbc"
  tests = "meta/bind_tests.json"
  deps = [
    "//sdk/fidl/fuchsia.hardware.input:fuchsia.hardware.input_bindlib",
    "//src/devices/bind/fuchsia.hid",
  ]
}

group("tests") {
  testonly = true
  deps = [
    ":hid-input-report-test",
    ":hid_input_report_bind_test",
  ]
}

source_set("driver-internals") {
  sources = [
    "input-report.cc",
    "input-reports-reader.cc",
  ]
  public_deps = [
    ":hid_input_report_bind",
    "//sdk/fidl/fuchsia.hardware.input:fuchsia.hardware.input_cpp",
    "//sdk/fidl/fuchsia.input.report:fuchsia.input.report_cpp",
    "//sdk/lib/component/outgoing/cpp",
    "//sdk/lib/driver/component/cpp",
    "//sdk/lib/driver/devfs/cpp",
    "//sdk/lib/driver/logging/cpp",
    "//sdk/lib/inspect/component/cpp",
    "//src/ui/input/lib/hid",
    "//src/ui/input/lib/hid-input-report",
    "//src/ui/input/lib/hid-parser",
    "//zircon/system/ulib/fbl",
    "//zircon/system/ulib/trace",
    "//zircon/system/ulib/zx",
  ]

  # TODO(https://fxbug.dev/42176699): This target uses mutable tables which are deprecated,
  # rather than builders.
  configs += [ "//build/cpp:fidl-wire-deprecated-mutable-tables" ]

  # TODO(https://fxbug.dev/42085293): delete the below and fix compiler warnings
  configs += [ "//build/config:Wno-vla-cxx-extension" ]
}

fuchsia_driver_package("package") {
  package_name = "hid-input-report"
  driver_components = [ ":hid-input-report" ]
}

fuchsia_cc_driver("hid-input-report-driver") {
  output_name = "hid-input-report"
  sources = [ "driver.cc" ]
  deps = [
    ":driver-internals",
    ":hid_input_report_bind",
    "//src/devices/lib/driver:driver_runtime",
  ]
}

fuchsia_driver_component("hid-input-report") {
  component_name = "hid-input-report"
  manifest = "meta/hid-input-report.cml"
  deps = [ ":hid-input-report-driver" ]
  info = "meta/hid-input-report-info.json"
}

test("hid-input-report-test-bin") {
  sources = [
    "driver.cc",
    "test.cc",
  ]
  deps = [
    ":driver-internals",
    ":hid_input_report_bind",
    "//sdk/lib/driver/testing/cpp",
    "//sdk/lib/inspect/testing/cpp",
    "//src/lib/ddk:ddk-metadata-headers",
    "//src/lib/fxl/test:gtest_main",
    "//src/lib/testing/predicates",
    "//zircon/system/ulib/sync",
  ]

  # TODO(https://fxbug.dev/42176699): This target uses mutable tables which are deprecated,
  # rather than builders.
  configs += [ "//build/cpp:fidl-wire-deprecated-mutable-tables" ]
}

fuchsia_unittest_package("hid-input-report-test") {
  deps = [ ":hid-input-report-test-bin" ]
  test_specs = {
    log_settings = {
      max_severity = "ERROR"
    }
  }
}

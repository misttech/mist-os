# Copyright 2019 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/bind/bind.gni")
import("//build/components.gni")
import("//build/drivers.gni")
import("//build/test.gni")

driver_bind_rules("hid_bind") {
  rules = "meta/hid.bind"
  bind_output = "hid.bindbc"
  tests = "meta/tests.json"
  deps = [ "//src/devices/bind/fuchsia.hid" ]
}

source_set("driver-internals") {
  sources = [
    "device-report-reader.cc",
    "hid-fifo.c",
    "hid-instance.cc",
    "hid.cc",
  ]
  public_deps = [
    ":hid_bind",
    "//sdk/fidl/fuchsia.hardware.hidbus:fuchsia.hardware.hidbus_cpp",
    "//sdk/fidl/fuchsia.hardware.input:fuchsia.hardware.input_cpp",
    "//sdk/lib/driver/compat/cpp",
    "//sdk/lib/driver/component/cpp",
    "//src/devices/bind/fuchsia.hid:fuchsia.hid_cpp",
    "//src/ui/input/lib/hid",
    "//src/ui/input/lib/hid-parser",
    "//zircon/system/ulib/fbl",
    "//zircon/system/ulib/trace",
  ]
}

fuchsia_cc_driver("hid-driver") {
  output_name = "hid"
  configs += [
    "//build/config:all_source",
    "//build/config/fuchsia:enable_zircon_asserts",
  ]

  # TODO(https://fxbug.dev/42085293): delete the below and fix compiler warnings
  configs += [ "//build/config:Wno-vla-cxx-extension" ]

  deps = [
    ":driver-internals",
    "//src/devices/lib/driver:driver_runtime",
  ]
}

fuchsia_driver_component("hid") {
  visibility = [
    ":*",
    "//src/ui/input:drivers",
    "//src/ui/input/drivers/usb-hid:*",
    "//src/ui/input/tests/hid:*",
  ]
  component_name = "hid"
  deps = [ ":hid-driver" ]
  info = "meta/hid-info.json"
  manifest = "meta/hid.cml"
}

fuchsia_driver_package("package") {
  package_name = "hid"
  driver_components = [ ":hid" ]
}

test("hid-test") {
  sources = [ "hid-test.cc" ]
  deps = [
    ":driver-internals",
    ":hid_bind",
    "//sdk/fidl/fuchsia.hardware.hidbus:fuchsia.hardware.hidbus_cpp_testing",
    "//sdk/lib/driver/testing/cpp",
    "//src/lib/fxl/test:gtest_main",
    "//src/lib/testing/predicates",
  ]
}

fuchsia_unittest_package("hid-test-package") {
  package_name = "hid-test"
  deps = [ ":hid-test" ]
  test_specs = {
    log_settings = {
      max_severity = "ERROR"
    }
  }
}

group("tests") {
  testonly = true
  deps = [
    ":hid-test-package",
    ":hid_bind_test",
  ]
}

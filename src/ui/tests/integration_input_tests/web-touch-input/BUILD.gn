# Copyright 2020 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/components.gni")
import("//build/fidl/fidl.gni")
import("//build/testing/cc_test_executable.gni")

cc_test_executable("web-touch-input-test-bin") {
  testonly = true
  sources = [ "web-touch-input-test.cc" ]
  output_name = "web-touch-input-test"
  deps = [
    "//sdk/fidl/fuchsia.accessibility.semantics:fuchsia.accessibility.semantics_cpp",
    "//sdk/fidl/fuchsia.buildinfo:fuchsia.buildinfo_cpp",
    "//sdk/fidl/fuchsia.component:fuchsia.component_cpp",
    "//sdk/fidl/fuchsia.element:fuchsia.element_cpp",
    "//sdk/fidl/fuchsia.fonts:fuchsia.fonts_cpp",
    "//sdk/fidl/fuchsia.hardware.display:fuchsia.hardware.display_cpp",
    "//sdk/fidl/fuchsia.input.injection:fuchsia.input.injection_cpp",
    "//sdk/fidl/fuchsia.intl:fuchsia.intl_cpp",
    "//sdk/fidl/fuchsia.kernel:fuchsia.kernel_cpp",
    "//sdk/fidl/fuchsia.memorypressure:fuchsia.memorypressure_cpp",
    "//sdk/fidl/fuchsia.metrics:fuchsia.metrics_cpp",
    "//sdk/fidl/fuchsia.net.interfaces:fuchsia.net.interfaces_cpp",
    "//sdk/fidl/fuchsia.posix.socket:fuchsia.posix.socket_cpp",
    "//sdk/fidl/fuchsia.process:fuchsia.process_cpp",
    "//sdk/fidl/fuchsia.scheduler:fuchsia.scheduler_cpp",
    "//sdk/fidl/fuchsia.tracing.provider:fuchsia.tracing.provider_cpp",
    "//sdk/fidl/fuchsia.ui.app:fuchsia.ui.app_cpp",
    "//sdk/fidl/fuchsia.ui.display.singleton:fuchsia.ui.display.singleton_cpp",
    "//sdk/fidl/fuchsia.ui.input:fuchsia.ui.input_cpp",
    "//sdk/fidl/fuchsia.ui.pointer:fuchsia.ui.pointer_cpp",
    "//sdk/fidl/fuchsia.ui.scenic:fuchsia.ui.scenic_cpp",
    "//sdk/fidl/fuchsia.ui.test.input:fuchsia.ui.test.input_cpp",
    "//sdk/fidl/fuchsia.vulkan.loader:fuchsia.vulkan.loader_cpp",
    "//sdk/fidl/fuchsia.web:fuchsia.web_cpp",
    "//sdk/lib/async-loop:async-loop-cpp",
    "//sdk/lib/async-loop:async-loop-default",
    "//sdk/lib/sys/component/cpp/testing:cpp",
    "//sdk/lib/sys/cpp",
    "//sdk/lib/ui/scenic/cpp",
    "//src/lib/fostr/fidl/fuchsia.ui.input",
    "//src/lib/fxl/test:gtest_main",
    "//src/lib/testing/loop_fixture",
    "//src/ui/testing/util",
    "//third_party/googletest:gtest",
    "//zircon/system/ulib/zx",

    # TODO(https://fxbug.dev/296294151): Fix after realm builder support new C++ binding.
    "//sdk/fidl/fuchsia.component.decl:fuchsia.component.decl_cpp_hlcpp_conversion",
    "//sdk/lib/fidl/cpp:hlcpp_conversion",
  ]
}

fuchsia_test_component("web-touch-input-test-component") {
  restricted_features = [ "allow_non_hermetic_packages" ]
  testonly = true
  manifest = "meta/web-touch-input-test.cml"
  deps = [ ":web-touch-input-test-bin" ]
  test_type = "system"
}

fuchsia_test_package("web-touch-input-test") {
  test_components = [ ":web-touch-input-test-component" ]
  deps = [
    "//src/cobalt/bin/testing/fake_cobalt",
    "//src/connectivity/network:netstack-for-tests",
    "//src/developer/build_info/testing:fake-build-info-component",
    "//src/developer/memory/pressure_signaler:component",
    "//src/fonts:font_provider_hermetic_for_test",
    "//src/testing/fidl/intl_property_manager:intl_property_manager_component",
    "//src/ui/bin/text:text_manager_comp",

    # Include some default config (it doesn't matter which, since we're going to override it).
    "//src/ui/testing/test_ui_stack:flatland_scene_manager_config",
    "//src/ui/testing/test_ui_stack:test-ui-stack-component",
    "//src/ui/tests/integration_input_tests/web-touch-input/web-touch-input-chromium:component",
  ]
  test_specs = {
    log_settings = {
      # TODO(https://fxbug.dev/42174827): Investigate flakes and remove allowing ERROR severity.
      max_severity = "ERROR"
    }
    environments = [
      {
        dimensions = {
          # Ensure the device has Vulkan.
          device_type = "AEMU"
        }
      },
    ]
  }
}

group("tests") {
  testonly = true
  deps = [
    ":web-touch-input-test",

    # The deps below are necessary on minimal product configs.
    # TODO(https://fxbug.dev/42070261): These should be provided by the Product
    # configurations on which these tests run rather than as deps.
    # TODO(https://fxbug.dev/42070261): Should these be deps of the individual test
    # targets above?
    "//src/chromium:web_context_provider_for_test",
    "//src/cobalt/bin/testing/fake_cobalt",
    "//src/graphics/bin/vulkan_loader",
    "//src/graphics/display/testing/fake-coordinator-connector",
    "//src/testing/fidl/intl_property_manager",
    "//src/ui/bin/text:text_manager",
    "//src/ui/tests/integration_input_tests/web-touch-input/web-touch-input-chromium",
  ]
}

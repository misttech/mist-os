# Copyright 2018 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/components.gni")
import("//build/rust/rustc_binary.gni")
import("//build/rust/rustc_library.gni")

group("tests") {
  testonly = true

  deps = [ ":vsock_service-tests" ]
}

fuchsia_component_manifest("manifest") {
  component_name = "vsock_service"
  manifest = "meta/vsock_service.cml"
}

fuchsia_structured_config_rust_lib("vsock_service_config") {
  cm_label = ":manifest"
}

rustc_binary("bin") {
  name = "vsock_service"
  edition = "2021"

  deps = [
    ":lib",
    ":vsock_service_config",
    "//sdk/fidl/fuchsia.hardware.vsock:fuchsia.hardware.vsock_rust",
    "//sdk/fidl/fuchsia.vsock:fuchsia.vsock_rust",
    "//sdk/lib/device-watcher/rust",
    "//src/lib/fuchsia",
    "//src/lib/fuchsia-component",
    "//src/lib/fuchsia-fs",
    "//third_party/rust_crates:anyhow",
    "//third_party/rust_crates:futures",
    "//third_party/rust_crates:tracing",
  ]

  sources = [ "src/main.rs" ]
}

rustc_library("lib") {
  name = "vsock_service_lib"
  edition = "2021"
  with_unit_tests = true
  deps = [
    "//sdk/fidl/fuchsia.hardware.vsock:fuchsia.hardware.vsock_rust",
    "//sdk/fidl/fuchsia.vsock:fuchsia.vsock_rust",
    "//sdk/rust/zx",
    "//src/lib/fidl/rust/fidl",
    "//src/lib/fuchsia-async",
    "//third_party/rust_crates:anyhow",
    "//third_party/rust_crates:futures",
    "//third_party/rust_crates:thiserror",
    "//third_party/rust_crates:tracing",
  ]

  sources = [
    "src/addr.rs",
    "src/lib.rs",
    "src/port.rs",
    "src/service.rs",
  ]
}

fuchsia_package_with_single_component("vsock_service") {
  cm_label = ":manifest"
  deps = [ ":bin" ]
}

fuchsia_unittest_package("vsock_service-tests") {
  deps = [ ":lib_test" ]
}

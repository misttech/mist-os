# Copyright 2024 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/components.gni")
import("//build/rust/rustc_binary.gni")

group("vsock-loopback") {
  testonly = true
  deps = [
    ":package",
    ":tests",
  ]
}

group("tests") {
  testonly = true
  deps = [ ":vsock-loopback-unittests" ]
}

rustc_binary("bin") {
  output_name = "vsock-loopback"
  edition = "2021"

  with_unit_tests = true

  deps = [
    "//sdk/fidl/fuchsia.hardware.vsock:fuchsia.hardware.vsock_rust",
    "//sdk/rust/zx",
    "//src/lib/diagnostics/inspect/runtime/rust",
    "//src/lib/diagnostics/inspect/rust",
    "//src/lib/fidl/rust/fidl",
    "//src/lib/fuchsia",
    "//src/lib/fuchsia-async",
    "//src/lib/fuchsia-component",
    "//third_party/rust_crates:anyhow",
    "//third_party/rust_crates:futures",
    "//third_party/rust_crates:log",
    "//third_party/rust_crates:thiserror",
  ]

  sources = [ "src/main.rs" ]
}

fuchsia_component("component") {
  component_name = "vsock-loopback"
  manifest = "meta/vsock_loopback.cml"
  deps = [ ":bin" ]
}

fuchsia_package("package") {
  package_name = "vsock-loopback"
  deps = [ ":component" ]
}

fuchsia_unittest_package("vsock-loopback-unittests") {
  deps = [ ":bin_test" ]
}

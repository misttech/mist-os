# Copyright 2019 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/components.gni")
import("//build/fidl/fidl.gni")
import("//build/test.gni")

# These build args are not the correct place to configure sysmem (outside of
# local experiments). Instead, use BoardSysmemConfig and/or PlatformSysmemConfig
# via the assembly tool. The settings in PlatformSysmemConfig can override
# settings in the BoardSysmemConfig. See also
# BoardProvidedConfig.sysmem_format_costs which can also be specified for the
# board.
#
# TODO(b/347090887): Remove these when developer overrides are wired up for
# bazel-based builds.
declare_args() {
  sysmem_contiguous_memory_size = -1
  sysmem_contiguous_memory_size_percent = 5
  sysmem_protected_memory_size = 0
  sysmem_protected_memory_size_percent = -1
  sysmem_contiguous_guard_pages_fatal = false
  sysmem_contiguous_guard_pages_internal = false
  sysmem_contiguous_guard_page_count = -1
  sysmem_contiguous_guard_pages_unused = false
  sysmem_contiguous_guard_pages_unused_fraction_denominator = 128
  sysmem_contiguous_guard_pages_unused_cycle_seconds = 600
  sysmem_protected_ranges_disable_dynamic = false
}

fuchsia_component_manifest("manifest") {
  component_name = "sysmem"
  manifest = "meta/sysmem.cml"
}

fuchsia_structured_config_cpp_elf_lib("config_cpp_elf_lib") {
  cm_label = ":manifest"
  namespace = "sysmem_config"
}

executable("sysmem_executable") {
  output_name = "sysmem"

  configs += [
    "//build/config:all_source",
    "//build/config/fuchsia:enable_zircon_asserts",
  ]

  sources = [
    "app.cc",
    "main.cc",
  ]

  deps = [
    ":sysmem_common_srcs",
    "//sdk/fidl/fuchsia.feedback:fuchsia.feedback_cpp",
    "//sdk/fidl/fuchsia.hardware.sysmem:fuchsia.hardware.sysmem_cpp",
    "//sdk/fidl/fuchsia.metrics:fuchsia.metrics_cpp",
    "//sdk/fidl/fuchsia.sysmem:fuchsia.sysmem_cpp",
    "//sdk/fidl/fuchsia.sysmem2:fuchsia.sysmem2_cpp",
    "//sdk/lib/async-loop:async-loop-cpp",
    "//sdk/lib/async-loop:async-loop-default",
    "//sdk/lib/sys/cpp",
    "//sdk/lib/vfs/cpp",
    "//zircon/system/ulib/trace-provider",
  ]
}

fuchsia_component("sysmem_cmp") {
  component_name = "sysmem"
  cm_label = ":manifest"
  deps = [ ":sysmem_executable" ]
}

fuchsia_structured_config_values("config_values") {
  cm_label = ":manifest"
  values = {
    contiguous_memory_size = sysmem_contiguous_memory_size
    contiguous_memory_size_percent = sysmem_contiguous_memory_size_percent
    protected_memory_size = sysmem_protected_memory_size
    protected_memory_size_percent = sysmem_protected_memory_size_percent
    contiguous_guard_pages_fatal = sysmem_contiguous_guard_pages_fatal
    contiguous_guard_pages_internal = sysmem_contiguous_guard_pages_internal
    contiguous_guard_page_count = sysmem_contiguous_guard_page_count
    contiguous_guard_pages_unused = sysmem_contiguous_guard_pages_unused
    contiguous_guard_pages_unused_fraction_denominator =
        sysmem_contiguous_guard_pages_unused_fraction_denominator
    contiguous_guard_pages_unused_cycle_seconds =
        sysmem_contiguous_guard_pages_unused_cycle_seconds
    protected_ranges_disable_dynamic = sysmem_protected_ranges_disable_dynamic
  }
}

fuchsia_package("pkg") {
  package_name = "sysmem"
  deps = [
    ":config_values",
    ":sysmem_cmp",
  ]
}

source_set("sysmem_common_srcs") {
  sources = [
    "allocator.cc",
    "buffer_collection.cc",
    "buffer_collection_token.cc",
    "buffer_collection_token_group.cc",
    "contiguous_pooled_memory_allocator.cc",
    "external_memory_allocator.cc",
    "indent.cc",
    "koid_util.cc",
    "logging.cc",
    "logical_buffer_collection.cc",
    "memory_allocator.cc",
    "node.cc",
    "node_properties.cc",
    "orphaned_node.cc",
    "protected_ranges.cc",
    "snapshot_annotation_register.cc",
    "sysmem.cc",
    "sysmem_metrics.cc",
    "usage_pixel_format_cost.cc",
    "utils.cc",
  ]
  public_deps = [
    ":config_cpp_elf_lib",
    ":fuchsia.sysmem2.internal_cpp",
    "//sdk/fidl/fuchsia.feedback:fuchsia.feedback_cpp",
    "//sdk/fidl/fuchsia.hardware.platform.device:fuchsia.hardware.platform.device_cpp",
    "//sdk/fidl/fuchsia.hardware.sysmem:fuchsia.hardware.sysmem_bindlib_cpp",
    "//sdk/fidl/fuchsia.hardware.sysmem:fuchsia.hardware.sysmem_cpp",
    "//sdk/fidl/fuchsia.inspect:fuchsia.inspect_cpp",
    "//sdk/fidl/fuchsia.kernel:fuchsia.kernel_cpp",
    "//sdk/fidl/fuchsia.logger:fuchsia.logger_cpp",
    "//sdk/fidl/fuchsia.sysmem:fuchsia.sysmem_cpp",
    "//sdk/fidl/fuchsia.sysmem2:fuchsia.sysmem2_cpp",
    "//sdk/lib/async:async-cpp",
    "//sdk/lib/async-loop:async-loop-cpp",
    "//sdk/lib/async_patterns/cpp",
    "//sdk/lib/component/outgoing/cpp",
    "//sdk/lib/fidl",
    "//sdk/lib/fit",
    "//sdk/lib/inspect/component/cpp",
    "//sdk/lib/syslog/cpp",
    "//src/devices/bind/fuchsia.sysmem.heap:fuchsia.sysmem.heap_cpp",
    "//src/lib/memory_barriers",
    "//src/lib/metrics_buffer",
    "//src/sysmem/metrics",
    "//zircon/system/ulib/closure-queue",
    "//zircon/system/ulib/fbl",
    "//zircon/system/ulib/image-format",
    "//zircon/system/ulib/inspect",
    "//zircon/system/ulib/region-alloc",
    "//zircon/system/ulib/sync",
    "//zircon/system/ulib/sysmem-version",
    "//zircon/system/ulib/trace",
    "//zircon/system/ulib/zx",
    "//zircon/third_party/ulib/safemath",
  ]

  deps = [
    "//zircon/system/ulib/image-format",
    "//zircon/system/ulib/region-alloc",
  ]
}

test("sysmem-unittest-bin") {
  if (is_fuchsia) {
    fdio_config = [ "//build/config/fuchsia:fdio_config" ]
    if (configs + fdio_config - fdio_config != configs) {
      configs -= fdio_config
    }
    configs += [ "//build/config/fuchsia:enable_zircon_asserts" ]
  }
  output_name = "sysmem-unittest"
  sources = [
    "test/contiguous_pooled_memory_allocator_test.cc",
    "test/device_test.cc",
    "test/pixel_format_cost_test.cc",
    "test/protected_ranges_test.cc",
  ]
  include_dirs = [ "." ]
  deps = [
    ":config_cpp_elf_lib",
    ":sysmem_common_srcs",
    "//sdk/fidl/fuchsia.sysmem:fuchsia.sysmem_cpp",
    "//sdk/lib/async:async-cpp",
    "//sdk/lib/async-default",
    "//sdk/lib/async-loop:async-loop-cpp",
    "//sdk/lib/async-loop:async-loop-default",
    "//sdk/lib/async-testing",
    "//sdk/lib/async_patterns/testing/cpp",
    "//sdk/lib/fidl",
    "//sdk/lib/fit",
    "//src/devices/testing/fake-bti",
    "//src/lib/fxl/test:gtest_main",
    "//src/lib/testing/predicates",
    "//third_party/googletest:gtest",
    "//zircon/system/ulib/closure-queue",
    "//zircon/system/ulib/fbl",
    "//zircon/system/ulib/image-format",
    "//zircon/system/ulib/region-alloc",
    "//zircon/system/ulib/sync",
    "//zircon/system/ulib/sysmem-version",
    "//zircon/system/ulib/zx",
    "//zircon/third_party/ulib/safemath",
  ]
}

template("test_format_costs_file") {
  resource(target_name) {
    _input = invoker.format_costs
    deps = [ _input ]
    sources =
        [ get_label_info(_input, "target_out_dir") + "/" +
          get_label_info(_input, "name") + ".format_costs_persistent_fidl" ]
    outputs = "data/format_costs/" + get_label_info(_input, "name") +
              ".format_costs_persistent_fidl"
  }
}

test_format_costs_file("test_format_costs_arm_mali") {
  format_costs = "//src/sysmem/format_costs/arm_mali"
}

test_format_costs_file("test_format_costs_intel") {
  format_costs = "//src/sysmem/format_costs/intel"
}

test_format_costs_file("test_format_costs_video_decoder_nv12") {
  format_costs = "//src/sysmem/format_costs/video_decoder_nv12"
}

fuchsia_unittest_package("sysmem-unittest") {
  deps = [
    ":sysmem-unittest-bin",
    ":test_format_costs_arm_mali",
    ":test_format_costs_intel",
    ":test_format_costs_video_decoder_nv12",
  ]
  test_specs = {
    log_settings = {
      max_severity = "ERROR"
    }
  }
}

group("tests") {
  testonly = true
  deps = [ ":sysmem-unittest" ]
}

fidl("fuchsia.sysmem2.internal") {
  sources = [ "combined_token.fidl" ]
  public_deps = [
    "//sdk/fidl/fuchsia.sysmem",
    "//sdk/fidl/fuchsia.sysmem2",
  ]
}

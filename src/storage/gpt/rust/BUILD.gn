# Copyright 2024 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/components.gni")
import("//build/components/fuchsia_unittest_package.gni")
import("//build/rust/rustc_library.gni")

group("rust") {
  public_deps = [ ":gpt" ]
}

rustc_library("gpt") {
  edition = "2021"
  deps = [
    "//src/storage/lib/block_client/rust",
    "//third_party/rust_crates:anyhow",
    "//third_party/rust_crates:crc",
    "//third_party/rust_crates:tracing",
    "//third_party/rust_crates:uuid",
    "//third_party/rust_crates:zerocopy_0_0_0_alpha_0",
  ]
  test_deps = [
    ":gpt_testing",
    "//sdk/fidl/fuchsia.hardware.block.volume:fuchsia.hardware.block.volume_rust",
    "//src/lib/fidl/rust/fidl",
    "//src/lib/fuchsia",
    "//src/lib/zircon/rust:fuchsia-zircon",
    "//src/storage/gpt:golden_images",
    "//src/storage/lib/block_server:fake_block_server",
    "//third_party/rust_crates:futures",
  ]
  sources = [
    "src/format.rs",
    "src/lib.rs",
  ]
  with_unit_tests = true
}

fuchsia_unittest_package("gpt-rust-test") {
  deps = [ ":gpt_test" ]
}

rustc_library("gpt_testing") {
  edition = "2021"
  deps = [
    ":gpt",
    "//src/lib/zircon/rust:fuchsia-zircon",
    "//third_party/rust_crates:crc",
    "//third_party/rust_crates:uuid",
    "//third_party/rust_crates:zerocopy_0_0_0_alpha_0",
  ]
  sources = [ "src/testing.rs" ]
  source_root = "src/testing.rs"
}

group("tests") {
  testonly = true
  deps = [ ":gpt-rust-test" ]
}

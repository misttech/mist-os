# Copyright 2019 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/components.gni")
import("//build/fidl/fidl.gni")

declare_args() {
  # Set this to true when configuring gn args to enable blobfs page-in metrics recording. This will
  # also increase the inspect VMO size for blobfs to 2 MiB, to accommodate the large number of
  # metrics entries.
  blobfs_page_in_metrics_recording = false
}

static_library("blobfs") {
  friend = [
    ":decompressor_impl",
    "test:*",
    "test/integration:*",
  ]
  visibility = [
    "//src/security/lib/scrutiny/utils/*",
    "//src/storage/*",
  ]
  public = [
    "blob_layout.h",
    "cache_policy.h",
    "common.h",
    "compression_settings.h",
    "delivery_blob.h",
    "delivery_blob_private.h",
    "format.h",
    "iterator/node_populator.h",
    "node_finder.h",
  ]
  sources = [
    "allocator/base_allocator.cc",
    "allocator/base_allocator.h",
    "allocator/extent_reserver.cc",
    "allocator/extent_reserver.h",
    "allocator/node_reserver.cc",
    "allocator/node_reserver.h",
    "blob_layout.cc",
    "blobfs_checker.cc",
    "common.cc",
    "compression/chunked.cc",
    "compression/chunked.h",
    "compression/compressor.h",
    "compression/decompressor.cc",
    "compression/decompressor.h",
    "compression/seekable_decompressor.h",
    "compression_settings.cc",
    "delivery_blob.cc",
    "format.cc",
    "iterator/allocated_extent_iterator.cc",
    "iterator/allocated_extent_iterator.h",
    "iterator/allocated_node_iterator.cc",
    "iterator/allocated_node_iterator.h",
    "iterator/block_iterator.cc",
    "iterator/block_iterator.h",
    "iterator/block_iterator_provider.h",
    "iterator/extent_iterator.h",
    "iterator/node_populator.cc",
    "iterator/vector_extent_iterator.cc",
    "iterator/vector_extent_iterator.h",
  ]
  public_deps = [
    "//sdk/lib/fit",
    "//sdk/lib/fit-promise",
    "//src/lib/chunked-compression",
    "//src/lib/digest",
    "//src/storage/lib/vfs/cpp/journal",
    "//zircon/system/ulib/bitmap",
    "//zircon/system/ulib/fbl",
    "//zircon/system/ulib/id_allocator",
    "//zircon/system/ulib/zx",
  ]
  deps = [
    "//sdk/lib/fit-promise",
    "//sdk/lib/syslog/cpp",
    "//src/lib/chunked-compression",
    "//src/lib/digest",
    "//src/storage/blobfs/compression/configs",
    "//src/storage/lib/trace:cpp",
    "//src/storage/lib/vfs/cpp/journal",
    "//src/storage/lib/vfs/cpp/transaction",
    "//zircon/system/ulib/bitmap",
    "//zircon/system/ulib/fbl",
    "//zircon/system/ulib/id_allocator",
    "//zircon/system/ulib/zx",
    "//zircon/third_party/ulib/cksum",
    "//zircon/third_party/ulib/safemath",
  ]
  if (is_fuchsia) {
    public += [
      "mkfs.h",
      "mount.h",
    ]
    sources += [
      "allocator/allocator.cc",
      "allocator/allocator.h",
      "blob.cc",
      "blob.h",
      "blob_cache.cc",
      "blob_cache.h",
      "blob_creator.cc",
      "blob_creator.h",
      "blob_data_producer.cc",
      "blob_data_producer.h",
      "blob_loader.cc",
      "blob_loader.h",
      "blob_reader.cc",
      "blob_reader.h",
      "blob_verifier.cc",
      "blob_verifier.h",
      "blob_writer.cc",
      "blob_writer.h",
      "blobfs.cc",
      "blobfs.h",
      "blobfs_checker.h",
      "blobfs_inspect_tree.cc",
      "blobfs_inspect_tree.h",
      "blobfs_metrics.cc",
      "blobfs_metrics.h",
      "cache_node.cc",
      "cache_node.h",
      "component_runner.cc",
      "component_runner.h",
      "compression/blob_compressor.cc",
      "compression/blob_compressor.h",
      "compression/external_decompressor.cc",
      "compression/external_decompressor.h",
      "compression/streaming_chunked_decompressor.cc",
      "compression/streaming_chunked_decompressor.h",
      "directory.cc",
      "directory.h",
      "fsck.cc",
      "fsck.h",
      "loader_info.h",
      "metrics/compression_metrics.cc",
      "metrics/compression_metrics.h",
      "metrics/fragmentation_metrics.cc",
      "metrics/fragmentation_metrics.h",
      "metrics/read_metrics.cc",
      "metrics/read_metrics.h",
      "metrics/verification_metrics.cc",
      "metrics/verification_metrics.h",
      "mkfs.cc",
      "mount.cc",
      "page_loader.cc",
      "page_loader.h",
      "service/admin.cc",
      "service/admin.h",
      "service/health_check.cc",
      "service/health_check.h",
      "service/lifecycle.cc",
      "service/lifecycle.h",
      "service/ota_health_check.cc",
      "service/ota_health_check.h",
      "service/startup.cc",
      "service/startup.h",
      "transaction.cc",
      "transaction.h",
      "transaction_manager.h",
      "transfer_buffer.cc",
      "transfer_buffer.h",
    ]
    public_deps += [
      ":fuchsia.blobfs.internal_cpp",
      "//sdk/fidl/fuchsia.fs:fuchsia.fs_cpp",
      "//sdk/fidl/fuchsia.hardware.block:fuchsia.hardware.block_cpp",
      "//sdk/fidl/fuchsia.io:fuchsia.io_cpp",
      "//sdk/fidl/fuchsia.process.lifecycle:fuchsia.process.lifecycle_cpp",
      "//sdk/lib/async",
      "//sdk/lib/async-loop:async-loop-cpp",
      "//sdk/lib/fidl/cpp/wire",
      "//sdk/lib/inspect/component/cpp",
      "//src/storage/fidl/fuchsia.fs.startup:fuchsia.fs.startup_cpp",
      "//src/storage/lib/block_client/cpp:interface",
      "//src/storage/lib/vfs/cpp",
      "//src/storage/lib/vfs/cpp/inspect",
      "//src/storage/lib/vfs/cpp/transaction",
      "//src/storage/lib/watchdog",
      "//src/sys/pkg/fidl/fuchsia.update.verify:fuchsia.update.verify_cpp",
      "//zircon/system/ulib/fzl",
      "//zircon/system/ulib/inspect",
      "//zircon/system/ulib/storage/buffer",
      "//zircon/system/ulib/storage/operation",
      "//zircon/third_party/ulib/safemath",
    ]
    deps += [
      "//sdk/fidl/fuchsia.fs:fuchsia.fs_cpp",
      "//sdk/fidl/fuchsia.hardware.block:fuchsia.hardware.block_cpp",
      "//sdk/fidl/fuchsia.hardware.block.driver:fuchsia.hardware.block.driver_banjo_c",
      "//sdk/fidl/fuchsia.hardware.block.volume:fuchsia.hardware.block.volume_cpp",
      "//sdk/fidl/fuchsia.io:fuchsia.io_cpp",
      "//sdk/fidl/fuchsia.process.lifecycle:fuchsia.process.lifecycle_cpp",
      "//sdk/lib/async",
      "//sdk/lib/async:async-cpp",
      "//sdk/lib/async-loop:async-loop-cpp",
      "//sdk/lib/async-loop:async-loop-default",
      "//sdk/lib/component/incoming/cpp",
      "//sdk/lib/fdio",
      "//sdk/lib/fdio:constants",
      "//sdk/lib/fidl/cpp/wire",
      "//sdk/lib/fit",
      "//sdk/lib/inspect/component/cpp",
      "//sdk/lib/scheduler/cpp",
      "//src/storage/blobfs:fuchsia.blobfs.internal_cpp",
      "//src/storage/fidl/fuchsia.fs.startup:fuchsia.fs.startup_cpp",
      "//src/storage/fvm",
      "//src/storage/fxfs/fidl/fuchsia.fxfs:fuchsia.fxfs_cpp",
      "//src/storage/lib/block_client/cpp",
      "//src/storage/lib/block_client/cpp:interface",
      "//src/storage/lib/vfs/cpp",
      "//src/storage/lib/vfs/cpp/inspect",
      "//src/storage/lib/watchdog",
      "//src/sys/pkg/fidl/fuchsia.update.verify:fuchsia.update.verify_cpp",
      "//zircon/system/ulib/fzl",
      "//zircon/system/ulib/inspect",
      "//zircon/system/ulib/storage/buffer",
      "//zircon/system/ulib/storage/operation",
      "//zircon/system/ulib/sync",
      "//zircon/system/ulib/trace-provider",
    ]
  } else {
    public += [
      "fsck_host.h",
      "host.h",
    ]
    sources += [
      "allocator/host_allocator.cc",
      "allocator/host_allocator.h",
      "fsck_host.cc",
      "host.cc",
    ]
  }

  defines = []

  if (blobfs_page_in_metrics_recording) {
    defines += [
      # Enable blobfs page-in metrics.
      "BLOBFS_ENABLE_PAGE_IN_METRICS",

      # The page-in metrics feature requires a large Inspect VMO (>512KB) for blobfs.
      "BLOBFS_ENABLE_LARGE_INSPECT_VMO",
    ]
  }
}

fidl("fuchsia.blobfs.internal") {
  sources = [ "compression/decompressor_sandbox/decompressor.fidl" ]
  public_deps = [ "//zircon/vdso/zx" ]
}

source_set("decompressor_impl") {
  visibility = [ "./*" ]
  sources = [
    "compression/decompressor_sandbox/decompressor_impl.cc",
    "compression/decompressor_sandbox/decompressor_impl.h",
  ]
  public_deps = [
    # Used by decompressor_impl.h
    ":fuchsia.blobfs.internal_cpp",
  ]
  deps = [
    ":blobfs",
    ":fuchsia.blobfs.internal_cpp",
    "//sdk/lib/scheduler/cpp",
    "//sdk/lib/syslog/cpp",
    "//src/lib/chunked-compression",
    "//src/storage/lib/trace:cpp",
    "//zircon/system/ulib/fzl",
    "//zircon/system/ulib/zx",
    "//zircon/third_party/ulib/safemath",
  ]
}

executable("decompressor") {
  visibility = [ ":*" ]
  sources = [ "compression/decompressor_sandbox/main.cc" ]
  deps = [
    ":decompressor_impl",
    ":fuchsia.blobfs.internal_cpp",
    "//sdk/lib/async-loop:async-loop-cpp",
    "//sdk/lib/async-loop:async-loop-default",
    "//sdk/lib/component/outgoing/cpp",
    "//sdk/lib/fidl/hlcpp",
    "//sdk/lib/syslog/cpp",
    "//zircon/system/ulib/trace-provider",
    "//zircon/system/ulib/zx",
  ]
}

fuchsia_component("decompressor-sandbox") {
  visibility = [
    ":*",
    "//src/storage/blobfs/bin",
    "//src/storage/fshost:*",
  ]
  manifest = "meta/decompressor_sandbox.cml"
  deps = [ ":decompressor" ]
}

group("tests") {
  testonly = true
  deps = [
    "bin:tests",
    "test",
  ]
}

# Copyright 2024 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/components.gni")
import("//build/components/fuchsia_unittest_component.gni")
import("//build/rust/rustc_binary.gni")
import("//build/rust/rustc_library.gni")

rustc_library("lib") {
  edition = "2021"
  deps = [
    "//sdk/fidl/fuchsia.hardware.block:fuchsia.hardware.block_rust",
    "//sdk/fidl/fuchsia.io:fuchsia.io_rust",
    "//sdk/fidl/fuchsia.process.lifecycle:fuchsia.process.lifecycle_rust",
    "//sdk/rust/zx",
    "//src/lib/fidl/rust/fidl",
    "//src/lib/fuchsia-async",
    "//src/storage/fidl/fuchsia.fs.startup:fuchsia.fs.startup_rust",
    "//src/storage/gpt/rust",
    "//src/storage/lib/block_client/rust",
    "//src/storage/lib/block_server",
    "//src/storage/lib/block_server",
    "//src/storage/lib/vfs/rust:vfs",
    "//src/storage/storage-host/fidl/fuchsia.storagehost:fuchsia.storagehost_rust",
    "//third_party/rust_crates:anyhow",
    "//third_party/rust_crates:futures",
    "//third_party/rust_crates:tracing",
  ]
  test_deps = [
    "//sdk/fidl/fuchsia.hardware.block.partition:fuchsia.hardware.block.partition_rust",
    "//sdk/fidl/fuchsia.hardware.block.volume:fuchsia.hardware.block.volume_rust",
    "//src/lib/fuchsia",
    "//src/lib/fuchsia-component",
    "//src/storage/lib/block_server:fake_block_server",
  ]
  sources = [
    "src/gpt.rs",
    "src/lib.rs",
    "src/partition.rs",
    "src/partitions_directory.rs",
    "src/service.rs",
  ]
  name = "storage_host"
  with_unit_tests = true
}

fuchsia_unittest_component("storage-host-test") {
  deps = [ ":lib_test" ]
}

fuchsia_test_package("storage-host-tests") {
  test_components = [ ":storage-host-test" ]
}

rustc_binary("storage_host") {
  edition = "2021"
  deps = [
    ":lib",
    "//src/lib/diagnostics/inspect/runtime/rust",
    "//src/lib/diagnostics/inspect/rust",
    "//src/lib/diagnostics/log/rust",
    "//src/lib/fuchsia-async",
    "//src/lib/fuchsia-component",
    "//src/lib/fuchsia-runtime",
    "//src/lib/trace/rust:trace-provider",
    "//third_party/rust_crates:anyhow",
    "//third_party/rust_crates:tracing",
  ]
  sources = [ "src/main.rs" ]

  # Optimize for size
  configs += [ "//build/config/rust:bootfs" ]
}

fuchsia_component("storage-host") {
  manifest = "meta/storage_host.cml"
  deps = [ ":storage_host" ]
}

fuchsia_package("package") {
  package_name = "storage-host"
  deps = [ ":storage-host" ]
}

group("tests") {
  testonly = true
  deps = [ ":storage-host-tests" ]
}

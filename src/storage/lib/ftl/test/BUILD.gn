# Copyright 2020 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/components.gni")
import("//build/test.gni")
import("//src/storage/testing/driver_test_realm.gni")

group("tests") {
  testonly = true
  deps = [
    ":ftl-library-test",
    ":ftl-wear-simulator-package",
  ]
}

test("ftl-test") {
  output_name = "ftl-library-test"
  if (is_fuchsia) {
    fdio_config = [ "//build/config/fuchsia:fdio_config" ]
    if (configs + fdio_config - fdio_config != configs) {
      configs -= fdio_config
    }
  }
  sources = [
    "ftl_test.cc",
    "ndm_driver_test.cc",
    "ndm_test.cc",
  ]
  deps = [
    "//src/devices/block/drivers/ftl/tests:shared",
    "//src/lib/fxl/test:gtest_main",
    "//src/storage/lib/ftl",
    "//third_party/googletest:gtest",
  ]
  defines = [ "NDM_DEBUG" ]
}

fuchsia_unittest_package("ftl-library-test") {
  deps = [ ":ftl-test" ]
}

source_set("ftl-workload-fuzzer") {
  sources = [ "ftl_workload_fuzzer.cc" ]
  deps = [
    "//src/devices/block/drivers/ftl/tests:shared",
    "//src/storage/lib/ftl",
  ]
}

test("ftl-wear-simulator-test") {
  sources = [ "wear_simulator.cc" ]
  deps = [
    "//src/lib/fxl/test:gtest_main",
    "//src/storage/blobfs/bin:blobfs-component",
    "//src/storage/blobfs/test:test_utils",
    "//src/storage/fs_test",
    "//src/storage/fvm:fvm-component",
    "//src/storage/minfs/bin:minfs-test-component",
    "//zircon/system/ulib/fbl",
  ]
}

storage_driver_test_realm_v2_component("ftl-wear-simulator") {
  deps = [ ":ftl-wear-simulator-test" ]
}

fuchsia_test_package("ftl-wear-simulator-package") {
  test_specs = {
    log_settings = {
      max_severity = "ERROR"
    }
  }
  test_components = [ ":ftl-wear-simulator" ]
}

# Copyright 2021 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/components.gni")
import("//build/config.gni")
import("//build/rust/rustc_binary.gni")

rustc_binary("bin") {
  edition = "2021"
  output_name = "bt_device_id"
  with_unit_tests = true

  deps = [
    ":bt_device_id_profile_config",
    "//sdk/fidl/fuchsia.bluetooth.bredr:fuchsia.bluetooth.bredr_rust",
    "//sdk/fidl/fuchsia.bluetooth.deviceid:fuchsia.bluetooth.deviceid_rust",
    "//sdk/rust/zx",
    "//src/connectivity/bluetooth/lib/fuchsia-bluetooth",
    "//src/lib/fidl/rust/fidl",
    "//src/lib/fuchsia",
    "//src/lib/fuchsia-component",
    "//third_party/rust_crates:anyhow",
    "//third_party/rust_crates:bitfield",
    "//third_party/rust_crates:futures",
    "//third_party/rust_crates:log",
    "//third_party/rust_crates:thiserror",
  ]

  test_deps = [
    "//src/connectivity/bluetooth/lib/async-helpers:async-test-helpers",
    "//src/lib/async-utils",
    "//src/lib/fuchsia",
    "//src/lib/fuchsia-async",
    "//third_party/rust_crates:assert_matches",
  ]

  sources = [
    "src/device_id/mod.rs",
    "src/device_id/server.rs",
    "src/device_id/service_record.rs",
    "src/device_id/token.rs",
    "src/error.rs",
    "src/fidl_service.rs",
    "src/main.rs",
  ]

  configs -= [ "//build/config/rust/lints:allow_unused_results" ]
}

fuchsia_component_manifest("manifest") {
  component_name = "bt-device-id"
  manifest = "meta/bt-device-id.cml"
}

fuchsia_structured_config_rust_lib("bt_device_id_profile_config") {
  cm_label = ":manifest"
}

# The default structured config for this component disables the default DI advertisement.
fuchsia_structured_config_values("default_config_values") {
  cm_label = ":manifest"
  values = {
    vendor_id = 65535  # Vendor ID = 0xFFFF (disabled)
    product_id = 0  # Product ID not specified
    version = 0  # Product version not specified
    primary = true
    service_description = ""
  }
}

fuchsia_component("component") {
  cm_label = ":manifest"
  deps = [ ":bin" ]
}

fuchsia_package("without-config") {
  package_name = "bt-device-id"
  validate_structured_config = false
  deps = [ ":component" ]
}

fuchsia_package("bt-device-id") {
  deps = [
    ":component",
    ":default_config_values",
  ]
}

fuchsia_unittest_package("bt-device-id-tests") {
  deps = [ ":bin_test" ]
}

group("tests") {
  testonly = true
  deps = [ ":bt-device-id-tests" ]
}

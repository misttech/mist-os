# Copyright 2017 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/bind/bind.gni")
import("//build/components.gni")
import("//build/drivers.gni")
import("//build/testing/cc_test_executable.gni")

driver_bind_rules("bt_hci_passthrough_bind") {
  rules = "meta/bt_hci_passthrough.bind"
  tests = "meta/bind-tests.json"
  deps = [ "//src/devices/bind/fuchsia.bluetooth" ]
}

source_set("passthrough_sources") {
  sources = [
    "passthrough.cc",
    "passthrough.h",
  ]

  deps = [
    ":bt_hci_passthrough_bind",
    "//sdk/fidl/fuchsia.hardware.bluetooth:fuchsia.hardware.bluetooth_cpp",
    "//sdk/lib/driver/component/cpp",
    "//sdk/lib/driver/devfs/cpp:cpp",
    "//sdk/lib/fidl",
    "//src/devices/lib/driver",
    "//src/devices/lib/driver:driver_runtime",
    "//zircon/system/ulib/zx",
  ]

  configs += [ "//build/config/fuchsia:enable_zircon_asserts" ]
}

fuchsia_cc_driver("fuchsia_driver") {
  output_name = "bt-hci-passthrough"
  deps = [ ":passthrough_sources" ]
}

fuchsia_driver_component("driver") {
  component_name = "bt-hci-passthrough"
  deps = [ ":fuchsia_driver" ]
  info = "meta/driver-info.json"
  manifest = "meta/driver.cml"
}

fuchsia_driver_package("passthrough") {
  driver_components = [ ":driver" ]
}

cc_test_executable("tests_executable") {
  testonly = true
  output_name = "bt-hci-passthrough-tests"
  sources = [ "passthrough_test.cc" ]

  deps = [
    ":passthrough_sources",
    "//sdk/fidl/fuchsia.hardware.bluetooth:fuchsia.hardware.bluetooth_cpp",
    "//sdk/lib/async-loop:async-loop-cpp",
    "//sdk/lib/async-loop:async-loop-default",
    "//sdk/lib/async_patterns/testing/cpp",
    "//sdk/lib/component/outgoing/cpp",
    "//sdk/lib/driver/compat/cpp",
    "//sdk/lib/driver/component/cpp",
    "//sdk/lib/driver/runtime:driver_runtime_cpp",
    "//sdk/lib/driver/testing/cpp",
    "//sdk/lib/fidl/hlcpp",
    "//src/devices/lib/driver:driver_runtime",
    "//src/lib/fxl/test:gtest_main",
    "//src/lib/testing/loop_fixture",
    "//zircon/system/ulib/zx",
  ]
}

fuchsia_unittest_package("bt-hci-passthrough-tests") {
  deps = [ ":tests_executable" ]
  test_specs = {
    log_settings = {
      max_severity = "ERROR"
    }
  }
}

group("tests") {
  testonly = true
  deps = [
    ":bt-hci-passthrough-tests",
    ":bt_hci_passthrough_bind_test",
  ]
}

# Copyright 2021 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/components.gni")
import("//build/fidl/fidl.gni")
import("//build/rust/rustc_binary.gni")

fidl("fidl") {
  testonly = true
  name = "fuchsia.netemul.test"
  sources = [ "counter.test.fidl" ]
  public_deps = [
    "//sdk/fidl/fuchsia.component",
    "//sdk/fidl/fuchsia.io",
    "//zircon/vdso/zx",
  ]
}

rustc_binary("bin") {
  testonly = true
  name = "counter"
  edition = "2021"
  deps = [
    ":config_lib",
    ":fidl_rust",
    "//sdk/rust/zx",
    "//src/lib/diagnostics/inspect/runtime/rust",
    "//src/lib/diagnostics/inspect/rust",
    "//src/lib/fdio/rust:fdio",
    "//src/lib/fidl/rust/fidl",
    "//src/lib/fuchsia",
    "//src/lib/fuchsia-component",
    "//third_party/rust_crates:anyhow",
    "//third_party/rust_crates:argh",
    "//third_party/rust_crates:futures",
    "//third_party/rust_crates:log",
  ]
  sources = [ "src/main.rs" ]
  configs -= [ "//build/config/rust/lints:allow_unused_results" ]
}

fuchsia_component_manifest("counter_manifest") {
  component_name = "counter"
  manifest = "meta/counter.cml"
}

fuchsia_component("counter") {
  testonly = true
  cm_label = ":counter_manifest"
  deps = [
    ":bin",
    ":config_values",
  ]
}

fuchsia_component("counter-alternative") {
  testonly = true
  manifest = "meta/counter-alternative.cml"
  deps = [ ":bin" ]
}

fuchsia_component("devfs-subdir-user") {
  testonly = true
  manifest = "meta/devfs-subdir-user.cml"
  deps = [ ":bin" ]
}

fuchsia_component("counter-without-program") {
  testonly = true
  manifest = "meta/counter-without-program.cml"
  deps = [ ":bin" ]
}

fuchsia_structured_config_values("config_values") {
  cm_label = ":counter_manifest"
  values = {
    routed_config = false
    starting_value = 0
  }
  visibility = [ ":*" ]
}

fuchsia_structured_config_rust_lib("config_lib") {
  name = "counter_config"
  cm_label = ":counter_manifest"
}

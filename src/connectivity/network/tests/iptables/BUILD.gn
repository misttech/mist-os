# Copyright 2024 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/components.gni")
import("//build/rust/rustc_test.gni")

rustc_test("iptables_test") {
  edition = "2021"
  sources = [ "src/lib.rs" ]
  deps = [
    "//sdk/fidl/fuchsia.component:fuchsia.component_rust",
    "//sdk/fidl/fuchsia.component.decl:fuchsia.component.decl_rust",
    "//sdk/fidl/fuchsia.net.filter:fuchsia.net.filter_rust",
    "//sdk/fidl/fuchsia.process:fuchsia.process_rust",
    "//sdk/rust/zx",
    "//src/lib/fuchsia",
    "//src/lib/fuchsia-component-test",
    "//src/lib/fuchsia-runtime",
    "//src/lib/network/fidl_fuchsia_net_filter_ext",
    "//src/sys/lib/component-events",
    "//third_party/rust_crates:libc",
    "//third_party/rust_crates:log",
    "//third_party/rust_crates:test-case",
  ]
  configs -= [ "//build/config/rust/lints:allow_unused_results" ]
}

fuchsia_test_component("iptables-restore") {
  manifest = "meta/iptables-restore.cml"
  test_type = "starnix"
}

fuchsia_test_component("ip6tables-restore") {
  manifest = "meta/ip6tables-restore.cml"
  test_type = "starnix"
}

group("tests") {
  testonly = true
  deps = [
    # Note the following targets aren't actually tests, but we add them to this
    # group to ensure they are hooked into the build graph. Otherwise they'd be
    # floating targets that never get built.
    #
    # We don't include the `iptables-test` component as it is a realm builder
    # that is intended to be included as part of a test package that pulls in
    # additional dependencies it needs at runtime.
    ":ip6tables-restore",
    ":iptables-restore",
    ":iptables_test",
  ]
}

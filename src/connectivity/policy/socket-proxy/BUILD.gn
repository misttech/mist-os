# Copyright 2024 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/components.gni")
import("//build/rust/rustc_binary.gni")
import("//src/connectivity/network/unified_binary/network_unified_binary.gni")

group("tests") {
  testonly = true

  deps = [
    ":socket-proxy-test",
    "integration:tests",
  ]
}

network_unified_binary("socket_proxy") {
  edition = "2021"
  with_unit_tests = true

  deps = [
    "//sdk/fidl/fuchsia.net:fuchsia.net_rust",
    "//sdk/fidl/fuchsia.netpol.socketproxy:fuchsia.netpol.socketproxy_rust",
    "//sdk/fidl/fuchsia.posix:fuchsia.posix_rust",
    "//sdk/fidl/fuchsia.posix.socket:fuchsia.posix.socket_rust",
    "//sdk/fidl/fuchsia.posix.socket.raw:fuchsia.posix.socket.raw_rust",
    "//src/lib/diagnostics/inspect/derive",
    "//src/lib/diagnostics/inspect/runtime/rust",
    "//src/lib/diagnostics/inspect/rust",
    "//src/lib/fidl/rust/fidl",
    "//src/lib/fuchsia",
    "//src/lib/fuchsia-component",
    "//third_party/rust_crates:anyhow",
    "//third_party/rust_crates:futures",
    "//third_party/rust_crates:log",
  ]

  test_deps = [
    "testing",
    "//src/connectivity/lib/net-declare",
    "//src/lib/diagnostics/testing/diagnostics-assertions/rust:diagnostics-assertions",
    "//src/lib/fuchsia-component-test",
    "//third_party/rust_crates:assert_matches",
    "//third_party/rust_crates:pretty_assertions",
    "//third_party/rust_crates:test-case",
  ]

  sources = [
    "src/dns_watcher.rs",
    "src/main.rs",
    "src/registry.rs",
    "src/socket_provider.rs",
  ]
}

fuchsia_component_manifest("manifest") {
  component_name = "network-socket-proxy"
  manifest = "meta/network_socket_proxy.cml"
}

network_unified_binary_regular_and_gub_components("component") {
  component_name = "network-socket-proxy"
  cm_label = ":manifest"
  gub_deps = [ ":socket_proxy" ]
}

network_unified_binary_regular_and_gub_packages("package") {
  package_name = "network-socket-proxy"
  gub_deps = [ ":component" ]
  visibility = [ "//bundles/assembly/*" ]
}

fuchsia_unittest_package("socket-proxy-test") {
  deps = [ ":socket_proxy_test" ]
}

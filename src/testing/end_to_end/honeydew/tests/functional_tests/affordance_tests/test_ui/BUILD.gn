# Copyright 2023 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be found
# in the LICENSE file.

import("//build/python/python_mobly_test.gni")
import("//build/testing/environments.gni")

if (is_host) {
  python_mobly_test("screenshot_test_ffx") {
    main_source = "test_screenshot.py"
    libraries = [
      "//src/testing/end_to_end/honeydew",
      "//src/testing/end_to_end/mobly_base_tests:fuchsia_base_test",
    ]
    transport = "fuchsia-controller"
    environments = [
      # Only enable for AEMU-x64 as host-tests aren't supported on QEMU-arm64.
      aemu_env,
      vim3_env,
      nuc11_env,
      nelson_env,
    ]
  }

  python_mobly_test("user_input_test_fc") {
    main_source = "test_user_input.py"
    libraries = [
      "//src/testing/end_to_end/honeydew",
      "//src/testing/end_to_end/mobly_base_tests:fuchsia_base_test",
    ]
    transport = "fuchsia-controller"
    environments = [
      # Only enable for AEMU-x64 as host-tests aren't supported on QEMU-arm64.
      aemu_env,
      vim3_env,
      nuc11_env,
    ]
  }
}

group("packages") {
  testonly = true
  public_deps = [ "//src/ui/examples:flatland-examples" ]
}

group("test_screenshot") {
  testonly = true
  public_deps = [
    ":packages",
    ":screenshot_test_ffx($host_toolchain)",
  ]
}

group("test_user_input") {
  testonly = true
  public_deps = [
    ":packages",
    ":user_input_test_fc($host_toolchain)",
  ]
}

# Group containing all of the UI affordance functional test cases and their dependencies.
group("tests") {
  testonly = true
  public_deps = [
    ":test_screenshot",
    ":test_user_input",
  ]
}

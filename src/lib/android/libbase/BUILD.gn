# Copyright 2022 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//src/lib/android/android.gni")

if (use_src_lib_android_rules) {
  config("libbase_config") {
    include_dirs = [ "//third_party/android/platform/system/libbase/include" ]
    configs = [
      "//src/lib/android:android_config",

      # TODO(https://fxbug.dev/42085293): delete the below and fix compiler warnings
      "//build/config:Wno-vla-cxx-extension",
    ]
  }

  source_set("libbase") {
    public_configs = [ ":libbase_config" ]
    sources = [
      "//third_party/android/platform/system/libbase/abi_compatibility.cpp",
      "//third_party/android/platform/system/libbase/file.cpp",
      "//third_party/android/platform/system/libbase/hex.cpp",
      "//third_party/android/platform/system/libbase/logging.cpp",
      "//third_party/android/platform/system/libbase/stringprintf.cpp",
      "//third_party/android/platform/system/libbase/strings.cpp",
      "override/properties.cc",
    ]
    public_deps = [ "//third_party/fmtlib" ]
    deps = [ "//src/lib/android/logging" ]
    if (is_host) {
      sources += [ "//third_party/android/platform/system/libbase/threads.cpp" ]
    } else {
      sources += [ "override/threads.cc" ]
      public_deps += [ "//src/lib/android/bionic_compat" ]
      deps += [ "//zircon/system/ulib/zx" ]
    }
  }
}  # use_src_lib_android_rules

# Copyright 2023 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/cpp/library_headers.gni")

group("testing") {
  testonly = true
  public_deps = [ ":elfldltl-testing" ]
}

group("headers") {
  testonly = true

  public_deps = [
    ":headers.common",
    "//third_party/googletest:gmock",
    "//third_party/googletest:gtest",
  ]
}

library_headers("headers.common") {
  visibility = [ ":*" ]
  testonly = true

  headers = [
    "lib/elfldltl/testing/diagnostics.h",
    "lib/elfldltl/testing/fuzzer.h",
    "lib/elfldltl/testing/get-test-data.h",
    "lib/elfldltl/testing/loader.h",
    "lib/elfldltl/testing/test-pipe-reader.h",
    "lib/elfldltl/testing/typed-test.h",
  ]

  public_deps = [
    "..",
    "//zircon/system/ulib/fbl",
  ]
}

static_library("elfldltl-testing") {
  testonly = true

  public_deps = [ ":headers" ]

  sources = [
    "get-test-data-path.cc",
    "test-pipe-reader.cc",
  ]
  deps = [ "//third_party/googletest:gtest" ]
  if (is_fuchsia) {
    sources += [
      "get-test-lib-vmo.cc",
      "get-test-lib.cc",
    ]
    deps += [
      "//sdk/fidl/fuchsia.ldsvc:fuchsia.ldsvc_cpp",
      "//sdk/lib/fdio",
      "//zircon/system/ulib/zx",
    ]
  }
}

# This is a subset built for use with zxtest in a standalone test.
static_library("elfldltl-zxtest") {
  testonly = true

  public_deps = [ ":headers.zxtest" ]

  if (is_fuchsia) {
    sources = [ "get-test-lib-vmo.cc" ]
    deps = [
      "//sdk/fidl/fuchsia.ldsvc:fuchsia.ldsvc_cpp",
      "//zircon/system/ulib/zx",
    ]
  }
}

group("headers.zxtest") {
  visibility = [ ":*" ]
  testonly = true

  public_deps = [
    ":headers.common",
    "//zircon/system/ulib/zxtest:zxtest-streams",
  ]
  public_configs = [ ":use-zxtest.config" ]
}

config("use-zxtest.config") {
  visibility = [ ":*" ]

  defines = [ "USE_ZXTEST" ]
}

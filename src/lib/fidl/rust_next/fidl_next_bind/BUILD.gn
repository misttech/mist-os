# Copyright 2024 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/components.gni")
import("//build/fidl/fidl.gni")
import("//build/rust/rustc_library.gni")
import("//build/rust/rustc_test.gni")

rustc_library("fidl_next_bind") {
  edition = "2021"
  sources = [
    "src/buffer.rs",
    "src/client.rs",
    "src/endpoint.rs",
    "src/error.rs",
    "src/lib.rs",
    "src/protocol.rs",
    "src/server.rs",
    "src/service.rs",
  ]
  deps = [
    "//src/lib/fidl/rust_next/fidl_next_codec",
    "//src/lib/fidl/rust_next/fidl_next_protocol",
  ]

  if (is_fuchsia) {
    sources += [ "src/fuchsia.rs" ]
    deps += [
      "//sdk/rust/zx",
      "//src/lib/fuchsia-async",
    ]
  }

  with_unit_tests = true

  # NOTE: this library is still experimental
  visibility = [
    "//:developer_universe_packages",
    "//src/lib/fidl/rust_next/*",
    "//tools/fidl/fidlc/testdata/*",
    "//tools/fidl/fidlgen_rust_next/*",
  ]

  # Enables compatibility impls with the existing Rust bindings.
  # This can be removed once we no longer require backwards compatibility.
  features = [ "compat" ]
  deps += [ "//src/lib/fidl/rust/fidl" ]
}

fuchsia_unittest_package("fidl_next_bind_test_package") {
  deps = [ ":fidl_next_bind_test" ]
}

group("tests") {
  testonly = true
  deps = [
    ":fidl_next_bind_test($host_toolchain)",
    # TODO: fix and re-enable device-side tests.
    # ":fidl_next_bind_test_package",
  ]
}

# Copyright 2024 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/components.gni")
import("//build/fidl/fidl.gni")
import("//build/rust/rustc_library.gni")
import("//build/rust/rustc_test.gni")

rustc_library("fidl_next_protocol") {
  edition = "2021"
  sources = [
    "src/buffer.rs",
    "src/client.rs",
    "src/error.rs",
    "src/framework_error.rs",
    "src/lib.rs",
    "src/lockers.rs",
    "src/mpsc.rs",
    "src/server.rs",
    "src/testing.rs",
    "src/transport.rs",
    "src/wire.rs",
  ]
  deps = [
    "//src/lib/fidl/rust_next/fidl_next_codec",
    "//third_party/rust_crates:thiserror",
    "//third_party/rust_crates:zerocopy",

    # Used for lock-free mpsc channel, but not for any APIs
    "//third_party/rust_crates:futures",
  ]

  if (is_fuchsia) {
    sources += [
      "src/fuchsia/channel.rs",
      "src/fuchsia/mod.rs",
    ]
    deps += [
      "//sdk/rust/zx",
      "//src/lib/fuchsia-async",
    ]
  }

  with_unit_tests = true
  test_deps = [ "//src/lib/fuchsia-async" ]

  # NOTE: this library is still experimental
  visibility = [
    "//:developer_universe_packages",
    "//src/lib/fidl/rust_next/*",
    "//tools/fidl/fidlc/testdata/*",
    "//tools/fidl/fidlgen_rust_next/*",
  ]
}

fuchsia_unittest_package("fidl_next_protocol_test_package") {
  deps = [ ":fidl_next_protocol_test" ]
}

group("tests") {
  testonly = true
  deps = [
    ":fidl_next_protocol_test($host_toolchain)",
    # TODO: fix and re-enable device-side tests.
    # ":fidl_next_protocol_test_package",
  ]
}

# Copyright 2024 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/components.gni")
import("//build/fidl/fidl.gni")

fidl("test.suspendcontrol") {
  sources = [ "fidl/suspendcontrol.test.fidl" ]
  public_deps = [
    "//sdk/fidl/fuchsia.hardware.suspend",
    "//zircon/vdso/zx",
  ]
}

source_set("cpp") {
  sources = [
    "device_server.cc",
    "device_server.h",
  ]
  public_deps = [
    ":test.suspendcontrol_cpp",
    "//sdk/fidl/fuchsia.hardware.suspend:fuchsia.hardware.suspend_cpp",
    "//sdk/lib/fit",
    "//sdk/lib/syslog/cpp",
  ]
}

executable("fake-suspend-bin") {
  sources = [ "fake-suspend.cc" ]
  deps = [
    ":cpp",
    "//sdk/fidl/fuchsia.device:fuchsia.device_cpp",
    "//sdk/lib/component/outgoing/cpp",
    "//zircon/system/ulib/async-default",
    "//zircon/system/ulib/async-loop:async-loop-cpp",
    "//zircon/system/ulib/async-loop:async-loop-default",
  ]
}

fuchsia_component("fake-suspend") {
  manifest = "meta/fake-suspend.cml"
  deps = [ ":fake-suspend-bin" ]
}

fuchsia_package("fake-suspend-pkg") {
  deps = [ ":fake-suspend" ]
}

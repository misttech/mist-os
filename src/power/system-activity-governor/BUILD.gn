# Copyright 2023 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/components.gni")
import("//build/rust/rustc_binary.gni")

rustc_binary("bin") {
  name = "system_activity_governor"
  edition = "2021"

  configs += [ "//build/config/rust:bootfs" ]

  sources = [
    "src/cpu_element_manager.rs",
    "src/cpu_manager.rs",
    "src/main.rs",
    "src/system_activity_governor.rs",
  ]
  deps = [
    ":sag_config",
    "//sdk/fidl/fuchsia.hardware.suspend:fuchsia.hardware.suspend_rust",
    "//sdk/fidl/fuchsia.power.broker:fuchsia.power.broker_rust",
    "//sdk/fidl/fuchsia.power.observability:fuchsia.power.observability_rust",
    "//sdk/fidl/fuchsia.power.suspend:fuchsia.power.suspend_rust",
    "//sdk/fidl/fuchsia.power.system:fuchsia.power.system_rust",
    "//sdk/rust/zx",
    "//src/lib/async-utils",
    "//src/lib/diagnostics/inspect/contrib/rust",
    "//src/lib/diagnostics/inspect/runtime/rust",
    "//src/lib/diagnostics/inspect/rust",
    "//src/lib/fidl/rust/fidl",
    "//src/lib/fuchsia",
    "//src/lib/fuchsia-async",
    "//src/lib/fuchsia-component",
    "//src/lib/fuchsia-fs",
    "//src/lib/trace/rust:trace",
    "//src/lib/trace/rust:trace-provider",
    "//src/power/broker/client",
    "//third_party/rust_crates:anyhow",
    "//third_party/rust_crates:async-lock",
    "//third_party/rust_crates:async-trait",
    "//third_party/rust_crates:futures",
    "//third_party/rust_crates:tracing",
  ]
}

fuchsia_component_manifest("manifest") {
  manifest = "meta/system-activity-governor.cml"
  component_name = "system-activity-governor"
}

fuchsia_structured_config_rust_lib("sag_config") {
  cm_label = ":manifest"
}

group("system-activity-governor") {
  testonly = true
  deps = [
    ":bin",
    ":tests",
  ]
}

fuchsia_package_with_single_component("config-no-suspender") {
  manifest = "meta/config-no-suspender.cml"
}

fuchsia_package_with_single_component("config-use-suspender") {
  manifest = "meta/config-use-suspender.cml"
}

fuchsia_component("component") {
  cm_label = ":manifest"
  deps = [ ":bin" ]
}

fuchsia_package("package") {
  package_name = "system-activity-governor"
  deps = [ ":component" ]
}

group("tests") {
  testonly = true
  deps = [
    "fake/tests",
    "integration/tests",
  ]
}

# Copyright 2021 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//src/developer/ffx/build/ffx_plugin.gni")
import("//src/developer/ffx/build/ffx_tool.gni")

ffx_plugin("ffx_session") {
  version = "0.1.0"
  edition = "2021"
  with_unit_tests = true
  plugin_deps = [
    "add:ffx_session_add",
    "drop-power-lease:ffx_session_drop_power_lease",
    "launch:ffx_session_launch",
    "remove:ffx_session_remove",
    "restart:ffx_session_restart",
    "show:ffx_session_show",
    "start:ffx_session_start",
    "stop:ffx_session_stop",
  ]
  args_sources = [ "src/args.rs" ]
  args_deps = [
    "//src/developer/ffx/core:lib",
    "//third_party/rust_crates:argh",
  ]
}

group("tests") {
  testonly = true

  deps = [
    ":ffx_session_tests",
    "add:ffx_session_add_tests",
    "drop-power-lease:ffx_session_drop_power_lease_tests",
    "launch:ffx_session_launch_tests",
    "remove:ffx_session_remove_tests",
    "restart:ffx_session_restart_tests",
    "show:ffx_session_show_tests",
    "start:ffx_session_start_tests",
    "stop:ffx_session_stop_tests",
  ]
}

ffx_tool("ffx_session_tool") {
  edition = "2021"
  output_name = "ffx-session"

  # Needed for Honeydew's SDK inclusion
  #
  # TODO(b/330934833): Remove when IDK allows for `testonly` targets in dep
  # tree or when we can separate the concept of data_dep from host_test_data.
  testonly = false

  deps = [
    ":ffx_session_suite",
    "//src/lib/fuchsia-async",
  ]
  sources = [ "src/main.rs" ]

  sdk_target_name = "sdk"
  sdk_category = "partner"
}

group("session") {
  public_deps = [
    ":ffx_session_tool",
    ":ffx_session_tool_host_tool",
  ]
}

group("bin") {
  public_deps = [ ":ffx_session_tool_versioned" ]
}

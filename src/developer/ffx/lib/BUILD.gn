# Copyright 2021 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

if (is_host) {
  libs = [
    # keep-sorted start
    "addr",
    "buildid",
    "compat_info",
    "daemonize",
    "discovery",
    "doctor_utils",
    "emulator_instance",
    "errors",
    "fastboot",
    "fastboot_file_discovery",
    "fho",
    "fidl-fuchsia-developer-ffx-ext",
    "fuchsia-controller",
    "isolate",
    "manual_targets",
    "mdns_discovery",
    "metrics",
    "netext",
    "pbms",
    "pkg",
    "port_picker",
    "protocols",
    "rcs",
    "sdk",
    "ssh",
    "stream_util",
    "symbol-index",
    "symbolize",
    "target",
    "target/connector",
    "target/holders",
    "tasks",
    "testing",
    "timeout",
    "usb_fastboot_discovery",
    "version",
    "writer",

    # keep-sorted end
  ]

  group("tests") {
    testonly = true
    deps = []
    foreach(lib, libs) {
      deps += [ "${lib}:tests" ]
    }
  }

  group("lib") {
    testonly = true

    deps = []
    foreach(lib, libs) {
      deps += [ lib ]
    }
  }
}

group("host_tests") {
  testonly = true
  deps = [
    "e2e_emu:host_tests($host_toolchain)",
    "fuchsia-controller:host_tests($host_toolchain)",
    "symbolize:host_tests($host_toolchain)",
  ]
}

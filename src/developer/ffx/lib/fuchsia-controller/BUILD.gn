# Copyright 2023 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/python/python_binary.gni")
import("//build/python/python_host_test.gni")
import("//build/python/python_library.gni")
import("//build/rust/rustc_staticlib.gni")
import("//src/tests/fidl/conformance_suite/gidl-conformance-suite.gni")
import("//tools/fidl/gidl/gidl.gni")

assert(is_host)

visibility = [
  ":*",
  "cpp:*",
]

conformance_test_source = "$target_gen_dir/conformance_test.py"

gidl("conformance_tests_py") {
  type = "conformance"
  language = "fuchsia_controller"
  inputs = conformance_suite_gidl_files
  fidl = conformance_suite_fidl_target
  output = conformance_test_source
}

python_host_test("fuchsia_controller_conformance_tests") {
  libraries = [
    ":fidl_bindings",
    ":fuchsia_controller_py",
  ]
  deps = [ ":conformance_tests_py" ]
  test_data_deps = [
    "tests:fuchsia_controller_host_test_data",
    "//src/tests/fidl/conformance_suite:conformance_fidl_host_test_data(${toolchain_variant.base})",
  ]
  main_source = conformance_test_source
}

rustc_staticlib("lib") {
  visibility += [ "tests:*" ]
  output_name = "fuchsia_controller"
  edition = "2021"
  with_unit_tests = true

  deps = [
    "//sdk/rust/zx-status",
    "//sdk/rust/zx-types",
    "//src/developer/ffx/config:lib",
    "//src/developer/ffx/lib/errors:lib",
    "//src/developer/ffx/lib/rcs:lib",
    "//src/developer/ffx/lib/target:lib",
    "//src/developer/ffx/plugins/target/wait:ffx_wait",
    "//src/developer/ffx/plugins/target/wait:ffx_wait_args",
    "//src/lib/fidl/rust/fidl",
    "//src/lib/fuchsia-async",
    "//third_party/rust_crates:anyhow",
    "//third_party/rust_crates:async-channel",
    "//third_party/rust_crates:async-lock",
    "//third_party/rust_crates:camino",
    "//third_party/rust_crates:tokio",
    "//third_party/rust_crates:tracing",
  ]

  test_deps = [
    "//third_party/rust_crates:byteorder",
    "//third_party/rust_crates:futures-test",
  ]

  sources = [
    "src/commands.rs",
    "src/env_context.rs",
    "src/ext_buffer.rs",
    "src/lib.rs",
    "src/lib_context.rs",
    "src/waker.rs",
  ]
}

python_library("fuchsia_controller_py") {
  visibility += [ "tests:*" ]
  library_name = "fuchsia_controller_py"
  source_root = "python/fuchsia_controller_py"
  library_deps = [
    "cpp:fidl_codec",
    "cpp:fuchsia_controller_internal",
  ]
  sources = [
    "__init__.py",
    "wrappers.py",
  ]

  # Ensure all FIDL dependencies are available and stable. Use the
  # "partner_internal" SDK category and greater as an approximation of this.
  # However, note that not everything in those categories is stable.
  # TODO(https://fxbug.dev/314822328): Reframe the markers in terms of stability.
  assert_no_deps = [
    "//sdk:marker-cts",
    "//sdk:marker-excluded",
    "//sdk:marker-experimental",
    "//sdk:marker-internal",
    "//sdk:marker-unknown",
  ]
}

python_library("fidl_bindings") {
  visibility += [
    "./*",
    "tests:*",
    "//src/testing/end_to_end/honeydew:*",
  ]

  library_name = "fidl"
  source_root = "python/fidl"
  library_deps = [
    ":fuchsia_controller_py",
    "cpp:fidl_codec",
  ]
  sources = [
    "__init__.py",
    "_async_socket.py",
    "_client.py",
    "_fidl_common.py",
    "_import.py",
    "_ipc.py",
    "_library.py",
    "_server.py",
  ]
}

python_binary("example") {
  testonly = true
  main_source = "python/example.py"
  deps = [
    ":fidl_bindings",
    ":fuchsia_controller_py",
    "cpp:fidl_codec",
    "//src/developer/ffx:ffx_tool",
  ]
}

group("fuchsia-controller") {
  testonly = true
  visibility += [ "//src/developer/ffx/lib" ]
  deps = [
    ":example",
    ":fuchsia_controller_py",
    "cpp:fidl_codec",
  ]
}

group("tests") {
  testonly = true
  visibility += [ "*" ]
  deps = [
    ":lib_test",
    "tests:fuchsia_controller_encode_test",
    "tests:fuchsia_controller_ir_test",
  ]

  # TODO(https://fxbug.dev/42078138): These tests fail on mac builders. This is most likely
  # because of the infra machines running OSX 10.15
  if (host_os != "mac") {
    deps += [
      ":fuchsia_controller_conformance_tests",
      "tests:fuchsia_controller_errors_test",
      "tests:fuchsia_controller_fidl_channel_test",
      "tests:fuchsia_controller_fidl_client_test",
      "tests:fuchsia_controller_fidl_common_test",
      "tests:fuchsia_controller_fidl_server_test",
      "tests:fuchsia_controller_importing_test",
      "tests:fuchsia_controller_isolate_directory_test",
      "tests:fuchsia_controller_socket_test",
    ]
  }
}

group("host_tests") {
  testonly = true
  visibility += [ "//src/developer/ffx/lib:host_tests" ]
  deps = []

  # TODO(https://fxbug.dev/42078138): These tests fail on mac builders. This is most likely
  # because of the infra machines running OSX 10.15
  if (host_os != "mac") {
    # TODO(https://fxbug.dev/42080013): Re-enable when the corrupt profile header
    # issue is fixed.
    if (!is_coverage) {
      deps += [ "tests:fuchsia_controller_e2e_test" ]
    }
  }
}

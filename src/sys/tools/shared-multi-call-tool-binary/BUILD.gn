# Copyright 2024 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/components.gni")
import("//build/rust/rustc_binary.gni")
import("//build/toolchain/concurrent_jobs.gni")

rustc_binary("shared-multi-call-tool-binary") {
  visibility = [
    ":*",
    "../component:*",
    "../package:*",
  ]
  edition = "2021"

  deps = [
    "//src/lib/fuchsia",
    "//src/sys/tools/component",
    "//src/sys/tools/package",
    "//third_party/rust_crates:anyhow",
    "//third_party/rust_crates:futures",
  ]

  # This is only used in eng products, not production.
  deps += [ "//build/validate:non_production_tag" ]

  sources = [ "src/main.rs" ]
  configs += [
    "//build/config/rust/lints:clippy_warn_all",

    # This binary is a part of the bootfs image, so attempt to minimize its size impact.
    "//build/config/rust:bootfs",
  ]

  # As this is a large binary that uses many resources to compile (and takes minutes when LTO is
  # enabled, shift it to the large RBE worker pool)
  _concurrent_jobs = concurrent_jobs.link_highmem
  configs += _concurrent_jobs.configs
  forward_variables_from(_concurrent_jobs.vars, "*")
}

# Copyright 2020 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/bazel/export_fuchsia_package_to_bazel.gni")
import("//build/components.gni")
import("//build/fidl/fidl.gni")
import("//build/rust/rustc_binary.gni")

rustc_binary("element_manager_bin") {
  name = "element_manager"
  with_unit_tests = true
  edition = "2021"

  deps = [
    ":element_config",
    ":fuchsia.element.manager.persistence_rust",
    "//sdk/fidl/fuchsia.component:fuchsia.component_rust",
    "//sdk/fidl/fuchsia.component.decl:fuchsia.component.decl_rust",
    "//sdk/fidl/fuchsia.element:fuchsia.element_rust",
    "//sdk/fidl/fuchsia.io:fuchsia.io_rust",
    "//sdk/fidl/fuchsia.mem:fuchsia.mem_rust",
    "//sdk/fidl/fuchsia.ui.app:fuchsia.ui.app_rust",
    "//sdk/rust/zx",
    "//src/lib/async-utils",
    "//src/lib/diagnostics/inspect/rust",
    "//src/lib/fdio/rust:fdio",
    "//src/lib/fidl/rust/fidl",
    "//src/lib/fuchsia",
    "//src/lib/fuchsia-async",
    "//src/lib/fuchsia-component",
    "//src/lib/fuchsia-fs",
    "//src/lib/fuchsia_nix",
    "//src/lib/ui/fuchsia-scenic",
    "//src/session/lib/realm_management",
    "//src/sys/lib/fidl-connector",
    "//third_party/rust_crates:anyhow",
    "//third_party/rust_crates:derivative",
    "//third_party/rust_crates:futures",
    "//third_party/rust_crates:log",
    "//third_party/rust_crates:rand",
    "//third_party/rust_crates:thiserror",
  ]

  test_deps = [
    "//sdk/fidl/fuchsia.io:fuchsia.io_rust",
    "//src/lib/test_util",
    "//src/session/testing",
    "//third_party/rust_crates:assert_matches",
    "//third_party/rust_crates:lazy_static",
    "//third_party/rust_crates:maplit",
  ]

  sources = [
    "src/annotation.rs",
    "src/element.rs",
    "src/element_manager.rs",
    "src/main.rs",
  ]
}

fuchsia_component_manifest("manifest") {
  component_name = "element_manager"
  manifest = "meta/element_manager.cml"
}

fuchsia_structured_config_rust_lib("element_config") {
  cm_label = ":manifest"
}

fuchsia_structured_config_values("sc_values") {
  cm_label = ":manifest"
  values = {
    url_to_collection = []
    default_collection = "elements"
  }
}

fuchsia_component("element_manager_comp") {
  cm_label = ":manifest"
  deps = [ ":element_manager_bin" ]
}

fuchsia_package("element_manager") {
  deps = [
    ":element_manager_comp",
    ":sc_values",
  ]
}

# Make `element_manager` available to products assembled with Bazel.
export_fuchsia_package_to_bazel("element_manager_bazel_inputs") {
  package = ":element_manager"
}

fuchsia_unittest_package("element_manager_tests") {
  manifest = "meta/element_manager_bin_test.cml"
  deps = [ ":element_manager_bin_test" ]
  test_specs = {
    log_settings = {
      max_severity = "ERROR"  # element_manager::tests::propose_persistent_element_with_bad_storage
    }
  }
}

group("tests") {
  testonly = true
  deps = [ ":element_manager_tests" ]
}

fidl("fuchsia.element.manager.persistence") {
  sources = [ "fidl/persistence.fidl" ]

  public_deps = [ "//sdk/fidl/fuchsia.element" ]
}

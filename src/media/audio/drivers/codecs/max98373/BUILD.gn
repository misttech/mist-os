# Copyright 2019 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/bind/bind.gni")
import("//build/components.gni")
import("//build/drivers.gni")
import("//build/test.gni")

driver_bind_rules("max98373-bind") {
  rules = "meta/max98373.bind"
  tests = "meta/tests.json"
  deps = [
    "//sdk/fidl/fuchsia.hardware.interrupt:fuchsia.hardware.interrupt_bindlib",
    "//src/devices/bind/fuchsia.acpi",
    "//src/devices/bind/fuchsia.maxim.platform",
  ]
}

fuchsia_cc_driver("max98373-driver") {
  output_name = "max98373"
  configs += [
    "//build/config:all_source",
    "//build/config/fuchsia:enable_zircon_asserts",
  ]
  sources = [ "max98373.cc" ]
  deps = [
    ":max98373-bind",
    "//sdk/fidl/fuchsia.hardware.acpi:fuchsia.hardware.acpi_cpp",
    "//sdk/fidl/fuchsia.hardware.gpio:fuchsia.hardware.gpio_cpp",
    "//src/devices/i2c/lib/device-protocol-i2c-channel",
    "//src/devices/lib/acpi",
    "//src/devices/lib/driver",
    "//src/lib/ddk",
    "//src/lib/ddktl",
    "//src/media/audio/lib/simple-codec",
    "//zircon/system/ulib/fbl",
    "//zircon/system/ulib/zircon-internal",
    "//zircon/system/ulib/zx",
  ]
}

fuchsia_driver_component("max98373") {
  component_name = "max98373"
  deps = [ ":max98373-driver" ]
  info = "meta/max98373-info.json"
  manifest = "meta/max98373.cml"
}

fuchsia_driver_package("package") {
  package_name = "max98373"
  export_to_bazel = true
  driver_components = [ ":max98373" ]
}

test("max98373-test") {
  output_name = "max98373-test"
  configs += [ "//build/config:all_source" ]
  sources = [
    "max98373-test.cc",
    "max98373.cc",
  ]
  deps = [
    ":max98373-bind",
    "//sdk/fidl/fuchsia.hardware.acpi:fuchsia.hardware.acpi_cpp",
    "//sdk/fidl/fuchsia.hardware.gpio:fuchsia.hardware.gpio_cpp",
    "//sdk/fidl/fuchsia.hardware.i2c:fuchsia.hardware.i2c_cpp",
    "//sdk/lib/async-loop:async-loop-cpp",
    "//sdk/lib/async-loop:async-loop-default",
    "//sdk/lib/async_patterns/testing/cpp",
    "//src/devices/gpio/testing/fake-gpio",
    "//src/devices/i2c/lib/device-protocol-i2c-channel",
    "//src/devices/i2c/testing/mock-i2c:mock-i2c-gtest",
    "//src/devices/lib/acpi",
    "//src/devices/testing/mock-ddk",
    "//src/lib/ddk",
    "//src/lib/ddktl",
    "//src/lib/fxl/test:gtest_main",
    "//src/lib/testing/predicates",
    "//src/media/audio/lib/simple-codec",
    "//third_party/googletest:gtest",
    "//zircon/system/ulib/fbl",
    "//zircon/system/ulib/sync",
    "//zircon/system/ulib/zircon-internal",
    "//zircon/system/ulib/zx",
  ]
}

fuchsia_unittest_package("max98373-test-package") {
  package_name = "max98373-test"
  deps = [ ":max98373-test" ]

  test_specs = {
    environments = basic_envs
    log_settings = {
      max_severity = "ERROR"
    }
  }
}

group("tests") {
  testonly = true
  deps = [
    ":max98373-bind_test",
    ":max98373-test-package",
  ]
}

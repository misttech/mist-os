# Copyright 2017 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/testing/cc_test_executable.gni")
import("//src/graphics/lib/magma/gnbuild/magma.gni")

group("integration") {
  testonly = true

  public_deps = [
    ":magma-conformance-vendor-helper-arm",
    ":msd_arm_mali_integration_tests",
  ]
}

source_set("mali_utils") {
  testonly = true
  sources = [
    "mali_utils.cc",
    "mali_utils.h",
  ]
  public_deps = [
    "../../include",
    "//sdk/lib/magma_client:magma_headers",
    "//src/graphics/lib/magma/src/libmagma",
    "//src/graphics/magma/lib/magma/util:macros",
    "//src/graphics/magma/lib/magma/util:short_macros",
    "//src/graphics/magma/lib/magma_client/test_util:magma_map_cpu",
    "//src/graphics/magma/lib/magma_client/test_util:test_device_helper",
  ]
}

cc_test_executable("msd_arm_mali_integration_tests") {
  testonly = true

  defines = [
    "MALI_TEST_DRIVER_PACKAGE=\"fuchsia.com/msd-arm-mali-integration-tests\"",
    "MALI_PRODUCTION_DRIVER_PACKAGE=\"fuchsia.com/msd-arm-mali\"",
  ]

  sources = [
    "driver_registry.h",
    "run_unit_tests.cc",
    "test_fault_recovery.cc",
    "test_icd_list.cc",
    "test_perf_counters.cc",
    "test_power_management.cc",
    "test_shutdown.cc",
  ]

  public_deps = [
    "//sdk/fidl/fuchsia.driver.development:fuchsia.driver.development_cpp",
    "//sdk/fidl/fuchsia.driver.registrar:fuchsia.driver.registrar_cpp",
    "//sdk/fidl/fuchsia.gpu.magma:fuchsia.gpu.magma_cpp",
    "//sdk/fidl/fuchsia.hardware.gpu.mali:fuchsia.hardware.gpu.mali_cpp",
    "//sdk/lib/component/incoming/cpp",
    "//sdk/lib/magma_client:magma_headers",
    "//src/graphics/lib/magma/src/libmagma",
    "//src/graphics/magma/lib/magma/test_util:logger_init_helper",
    "//src/graphics/magma/lib/magma/util:macros",
    "//src/graphics/magma/lib/magma/util:short_macros",
    "//src/graphics/magma/lib/magma_client/test_util:magma_map_cpu",
    "//src/graphics/magma/lib/magma_client/test_util:test_device_helper",
    "//src/lib/fxl/test:gtest_main",
    "//third_party/googletest:gmock",
    "//third_party/googletest:gtest",
    "//zircon/system/ulib/trace",
    "//zircon/system/ulib/trace-vthread",
    "//zircon/system/ulib/zx",
  ]

  deps = [
    ":mali_utils",
    "../../include",
  ]
}

executable("magma-conformance-vendor-helper-arm") {
  testonly = true
  sources = [ "vendor_helper_arm.cc" ]
  deps = [
    "//sdk/lib/async-loop:async-loop-cpp",
    "//sdk/lib/component/outgoing/cpp",
    "//sdk/lib/syslog/cpp",
    "//src/graphics/drivers/msd-arm-mali/include:include",
    "//src/graphics/magma/tests/integration:vendor_helper_interface",
  ]
}

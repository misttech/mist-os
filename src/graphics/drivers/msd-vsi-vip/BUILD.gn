# Copyright 2018 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/bind/bind.gni")
import("//build/components.gni")
import("//build/drivers.gni")
import("//src/graphics/lib/magma/gnbuild/magma.gni")

declare_args() {
  # Enable suspend.
  # This will stop the ring buffer and suspend the clks when there are no
  # submitted commands.
  msd_vsi_vip_enable_suspend = true
}

config("msd_vsi_vip_config") {
  defines = []
  if (msd_vsi_vip_enable_suspend) {
    defines += [ "MSD_VSI_VIP_ENABLE_SUSPEND" ]
  }
}

fuchsia_driver_package("msd-vsi-vip") {
  export_to_bazel = true
  driver_components = [ ":msd_vsi" ]
}

fuchsia_driver_package("msd-vsi-vip-test") {
  testonly = true
  driver_components = [
    ":msd_vsi_test",
    "//src/graphics/magma/tests/unit_tests:test_firmware",
  ]
}

fuchsia_cc_driver("msd_vsi-driver") {
  output_name = "libmsd_vsi"
  deps = [
    ":msd_vsi_entry",
    "src",
  ]
  assert_no_deps = [ "//zircon/system/ulib/trace-engine:trace-engine-static" ]
  assert_no_deps += [
    "//src/devices/lib/driver",
    "//src/lib/ddk",
  ]
}

fuchsia_driver_component("msd_vsi") {
  info = "meta/msd_vsi-info.json"
  component_name = "libmsd_vsi"
  deps = [ ":msd_vsi-driver" ]
  manifest = "meta/msd_vsi.cml"
}

fuchsia_cc_driver("msd_vsi_test-driver") {
  testonly = true
  output_name = "libmsd_vsi_test"
  deps = [
    ":msd_vsi_test_entry",
    "src",
  ]
  assert_no_deps = [ "//zircon/system/ulib/trace-engine:trace-engine-static" ]
}

fuchsia_driver_component("msd_vsi_test") {
  info = "meta/msd_vsi_test-info.json"
  testonly = true
  component_name = "libmsd_vsi_test"
  deps = [ ":msd_vsi_test-driver" ]
  manifest = "meta/msd_vsi_test.cml"
}

driver_bind_rules("msd_vsi_bind") {
  rules = "meta/msd_vsi.bind"
  tests = "tests/bind_tests.json"
  deps = [
    "//src/devices/bind/fuchsia.platform",
    "//src/devices/bind/fuchsia.verisilicon.platform",
  ]
}

entry_deps = [
  "//sdk/fidl/fuchsia.gpu.magma:fuchsia.gpu.magma_cpp",
  "//sdk/lib/driver/component/cpp",
  "//sdk/lib/driver/incoming/cpp",
  "//sdk/lib/driver/runtime:driver_runtime",
  "//sdk/lib/fidl_driver:fidl_driver_transport",
  "//src/devices/bin/driver_runtime",
  "//src/graphics/magma/lib/magma/platform:trace",
  "//src/graphics/magma/lib/magma/platform/zircon:device_dfv2",
  "//src/graphics/magma/lib/magma/platform/zircon:logger_for_dfv2",
  "//src/graphics/magma/lib/magma/platform/zircon:status",
  "//src/graphics/magma/lib/magma/platform/zircon:thread_profile_dfv2",
  "//src/graphics/magma/lib/magma/util:macros",
  "//src/graphics/magma/lib/magma/util:short_macros",
  "//src/graphics/magma/lib/magma_service/sys_driver",
  "//zircon/system/ulib/trace",
  "//zircon/system/ulib/trace-vthread",
  "//zircon/system/ulib/zx",
]

source_set("msd_vsi_entry") {
  sources = [ "src/driver_entry.cc" ]
  deps =
      entry_deps + [
        ":msd_vsi_bind",
        "//src/graphics/magma/lib/magma_service/sys_driver:magma_driver_base",
      ]
}

source_set("msd_vsi_test_entry") {
  testonly = true
  sources = [
    "src/driver_entry.cc",
    "src/driver_test_gtest.cc",
  ]

  defines = [ "MAGMA_TEST_DRIVER=1" ]

  deps = entry_deps + [
           ":msd_vsi_bind",
           "src",
           "tests/unit_tests:hardware_tests",
           "//src/graphics/magma/lib/magma_service/sys_driver:magma_driver_base",
           "//src/graphics/magma/lib/magma_service/test_util:gtest_printer",
           "//src/graphics/magma/lib/magma_service/test_util:platform_device_helper_dfv2",
           "//src/graphics/magma/lib/magma_service/test_util:platform_msd_device_helper",
           "//third_party/googletest:gtest",
         ]
  assert_no_deps = [
    "//src/devices/lib/driver",
    "//src/lib/ddk",
  ]
}

group("tests") {
  testonly = true
  deps = [ ":msd_vsi_bind_test" ]
}

group("test-driver") {
  testonly = true
  public_deps = [ ":msd-vsi-vip-test" ]
}

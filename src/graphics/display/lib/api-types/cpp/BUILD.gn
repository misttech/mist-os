# Copyright 2023 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/components.gni")
import("//build/test.gni")

source_set("cpp") {
  sources = [
    "alpha-mode.cc",
    "alpha-mode.h",
    "buffer-collection-id.h",
    "buffer-id.h",
    "color.cc",
    "color.h",
    "config-check-result.cc",
    "config-check-result.h",
    "config-stamp.cc",
    "config-stamp.h",
    "coordinate-transformation.cc",
    "coordinate-transformation.h",
    "dimensions.h",
    "display-timing.cc",
    "display-timing.h",
    "driver-buffer-collection-id.h",
    "driver-buffer-id.h",
    "driver-capture-image-id.h",
    "driver-image-id.h",
    "driver-layer-id.h",
    "driver-layer.h",
    "event-id.h",
    "image-buffer-usage.h",
    "image-id.h",
    "image-metadata.h",
    "image-tiling-type.cc",
    "image-tiling-type.h",
    "layer-composition-operations.cc",
    "layer-composition-operations.h",
    "layer-id.h",
    "mode-and-id.h",
    "mode.h",
    "pixel-format.h",
    "rectangle.h",
    "vsync-ack-cookie.h",
  ]
  public_deps = [
    "//sdk/banjo/fuchsia.hardware.display.controller:fuchsia.hardware.display.controller_banjo_cpp",
    "//sdk/fidl/fuchsia.hardware.display:fuchsia.hardware.display_cpp",
    "//sdk/fidl/fuchsia.hardware.display.engine:fuchsia.hardware.display.engine_cpp",
    "//sdk/fidl/fuchsia.hardware.display.types:fuchsia.hardware.display.types_cpp",
    "//sdk/fidl/fuchsia.images2:fuchsia.images2_cpp",
    "//sdk/fidl/fuchsia.math:fuchsia.math_cpp",
    "//sdk/lib/stdcompat",
    "//zircon/system/ulib/fbl",
  ]
}

test_sources = [
  "alpha-mode-test.cc",
  "buffer-collection-id-test.cc",
  "buffer-id-test.cc",
  "color-test.cc",
  "config-check-result-test.cc",
  "config-stamp-test.cc",
  "coordinate-transformation-test.cc",
  "dimensions-test.cc",
  "display-id-test.cc",
  "display-timing-test.cc",
  "driver-buffer-collection-id-test.cc",
  "driver-buffer-id-test.cc",
  "driver-capture-image-id-test.cc",
  "driver-image-id-test.cc",
  "driver-layer-id-test.cc",
  "driver-layer-test.cc",
  "event-id-test.cc",
  "image-buffer-usage-test.cc",
  "image-id-test.cc",
  "image-metadata-test.cc",
  "image-tiling-type-test.cc",
  "layer-composition-operations-test.cc",
  "layer-id-test.cc",
  "mode-and-id-test.cc",
  "mode-test.cc",
  "pixel-format-test.cc",
  "rectangle-test.cc",
  "vsync-ack-cookie-test.cc",
]

group("test-deps") {
  testonly = true
  visibility = [ ":*" ]
  public_deps = [
    ":cpp",
    "//src/lib/fxl/test:gtest_main",
    "//third_party/googletest:gmock",
    "//third_party/googletest:gtest",
    "//zircon/system/ulib/image-format",
  ]
}

test("unittests") {
  sources = test_sources
  output_name = "display-api-types-cpp-tests"
  deps = [ ":test-deps" ]
  visibility = [ ":*" ]
}

# TODO(https://fxbug.dev/378965477): Remove once C++17 support is dropped.
test("unittests-cxx17") {
  sources = test_sources
  output_name = "display-api-types-cpp17-tests"
  deps = [ ":test-deps" ]
  visibility = [ ":*" ]

  # Remove the C++ language specifier.
  configs -= [ "//build/config:language" ]
  configs += [ ":cxx17" ]
}

# TODO(https://fxbug.dev/378965477): Remove once C++17 support is dropped.
config("cxx17") {
  visibility = [ ":*" ]
  cflags_cc = [ "-std=c++17" ]
}

fuchsia_unittest_package("unittests-package") {
  package_name = "display-api-types-cpp-tests"
  deps = [ ":unittests" ]
}

# TODO(https://fxbug.dev/378965477): Remove once C++17 support is dropped.
fuchsia_unittest_package("unittests-cxx17-package") {
  package_name = "display-api-types-cpp17-tests"
  deps = [ ":unittests-cxx17" ]
}

group("tests") {
  testonly = true
  deps = [
    ":unittests-cxx17-package",
    ":unittests-package",
  ]
}

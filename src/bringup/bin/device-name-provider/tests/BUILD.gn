# Copyright 2020 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/components.gni")
import("//build/test.gni")

test("device-name-provider-test") {
  sources = [ "args-test.cc" ]
  deps = [
    "//sdk/fidl/fuchsia.boot:fuchsia.boot_cpp",
    "//sdk/lib/async-loop:async-loop-cpp",
    "//sdk/lib/async-loop:async-loop-default",
    "//src/bringup/bin/device-name-provider:args",
    "//src/bringup/bin/device-name-provider:device_name_provider_config",
    "//src/storage/lib/vfs/cpp",
    "//zircon/system/ulib/mock-boot-arguments",
    "//zircon/system/ulib/zxtest",
  ]
}

fuchsia_unittest_component("device-name-provider-test-component") {
  component_name = "device-name-provider-test"
  deps = [ ":device-name-provider-test" ]
}

test("integration-test") {
  sources = [ "integration-test.cc" ]

  deps = [
    ":integration-test-config",
    "//sdk/fidl/fuchsia.device:fuchsia.device_cpp",
    "//sdk/lib/component/incoming/cpp",
    "//src/lib/fxl/test:gtest_main",
    "//src/lib/testing/predicates",
  ]
}

fuchsia_component_manifest("integration-test-component-manifest") {
  testonly = true
  manifest = "../meta/integration-test.cml"
  component_name = "integration-test-component"
}

fuchsia_structured_config_cpp_elf_lib("integration-test-config") {
  testonly = true
  cm_label = ":integration-test-component-manifest"
}

fuchsia_component("integration-test-component") {
  testonly = true
  cm_label = ":integration-test-component-manifest"
  deps = [ ":integration-test" ]
}

fuchsia_component("integration-test-realm") {
  testonly = true
  manifest = "../meta/integration-test-realm.cml"
}

fuchsia_component("integration-test-realm-no-zbi") {
  testonly = true
  manifest = "../meta/integration-test-realm-no-zbi.cml"
}

fuchsia_test_package("tests") {
  package_name = "device-name-provider-tests"
  subpackages = [ "fake_zbi" ]
  validate_structured_config = false
  deps = [
    ":integration-test-component",
    "//src/bringup/bin/device-name-provider",
    "//src/bringup/bin/device-name-provider:component",
    "//src/devices/block/drivers/core",
    "//src/devices/block/drivers/gpt",
    "//src/devices/block/drivers/ramdisk",
    "//src/devices/board/drivers/integration-test",
    "//src/devices/bus/drivers/platform",
  ]
  test_components = [
    ":device-name-provider-test-component",
    ":integration-test-realm",
    ":integration-test-realm-no-zbi",
  ]
}

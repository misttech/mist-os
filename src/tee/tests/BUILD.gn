# Copyright 2024 The Fuchsia Authors
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/components/fuchsia_test.gni")
import("//build/components/fuchsia_test_component.gni")
import("//build/components/fuchsia_test_package.gni")
import("//build/rust/rustc_test.gni")

group("tests") {
  testonly = true
  deps = [
    ":ta-lifecycle",
    ":tee-internal-api-entry-points-test",
    "rpc:tests",
  ]
}

rustc_test("ta_lifecycle_test") {
  edition = "2021"

  source_root = "src/ta_lifecycle.rs"
  sources = [ source_root ]
  configs -= [ "//build/config/rust/lints:allow_unused_results" ]
  deps = [
    "//sdk/fidl/fuchsia.tee:fuchsia.tee_rust",
    "//src/lib/fuchsia",
    "//src/lib/fuchsia-component",
    "//third_party/rust_crates:anyhow",
  ]
}

fuchsia_test_component("ta-lifecycle-test-root") {
  manifest = "meta/ta-lifecycle-root.cml"
}

fuchsia_test_component("ta-lifecycle-test") {
  manifest = "meta/ta-lifecycle-test.cml"
  deps = [ ":ta_lifecycle_test" ]
}

fuchsia_package("ta-lifecycle-test-package") {
  testonly = true
  subpackages = [ "//src/tee/ta/noop:ta-noop" ]
  deps = [
    ":ta-lifecycle-test",
    ":ta-lifecycle-test-root",
    "//src/tee/manager:test-tee-system-properties",
  ]
}

fuchsia_test("ta-lifecycle") {
  package = ":ta-lifecycle-test-package"
  component = ":ta-lifecycle-test-root"
}

rustc_test("tee_internal_api_entry_points_test") {
  edition = "2021"

  source_root = "src/entry_points.rs"
  sources = [ source_root ]
  configs -= [ "//build/config/rust/lints:allow_unused_results" ]
  deps = [
    "//sdk/fidl/fuchsia.tee:fuchsia.tee_rust",
    "//src/lib/fuchsia",
    "//src/lib/fuchsia-component",
    "//third_party/rust_crates:anyhow",
  ]
}

fuchsia_test_component("tee-internal-api-entry-points-test-root") {
  manifest = "meta/tee-internal-api-entry-points-root.cml"
}

fuchsia_test_component("tee-internal-api-entry-points-component") {
  component_name = "tee-internal-api-entry-points-test"
  manifest = "meta/tee-internal-api-entry-points-test.cml"
  deps = [ ":tee_internal_api_entry_points_test" ]
}

fuchsia_test_package("tee-internal-api-entry-points-test") {
  subpackages = [ "//src/tee/ta/entry_points:tee-internal-api-entry-points" ]
  test_components = [ ":tee-internal-api-entry-points-test-root" ]
  deps = [
    ":tee-internal-api-entry-points-component",
    "//src/tee/manager:test-tee-system-properties",
  ]
}

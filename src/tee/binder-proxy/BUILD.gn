# Copyright 2024 The Fuchsia Authors
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/components.gni")
import("//build/rust/rustc_binary.gni")
import("//src/lib/android/aidl/aidl.gni")

group("tests") {
  testonly = true
  deps = [ "tests" ]
}

rustc_binary("bin") {
  output_name = "binder_proxy"
  edition = "2021"

  sources = [
    "src/binder_proxy.rs",
    "src/main.rs",
    "src/microfuchsia_control.rs",
  ]

  deps = [
    ":android_system_microfuchsia_vm_service",
    ":binder_proxy_config",
    "//sdk/rust/zx",
    "//src/lib/android/binder/rust:binder",
    "//src/lib/android/binder/rust/rpcbinder",
    "//src/lib/fuchsia",
    "//src/lib/fuchsia-sync",
    "//src/paravirtualization/lib/vsock/vsock_sys",
    "//src/tee/lib/dev_urandom_compat",
    "//third_party/rust_crates:anyhow",
    "//third_party/rust_crates:libc",
    "//third_party/rust_crates:tracing",
  ]
}

android_system_microfuchsia_vm_service_aidl_interfaces = [
  "aidl/android/system/microfuchsia/vm_service/IMicrofuchsia.aidl",
  "aidl/android/system/microfuchsia/vm_service/IHostProxy.aidl",
]

aidl_rust("android_system_microfuchsia_vm_service") {
  base = "aidl"
  interfaces = android_system_microfuchsia_vm_service_aidl_interfaces
}

aidl_cpp("android_system_microfuchsia_vm_service_cpp") {
  base = "aidl"
  interfaces = android_system_microfuchsia_vm_service_aidl_interfaces
}

fuchsia_component_manifest("manifest") {
  component_name = "binder-proxy"
  manifest = "meta/binder_proxy.cml"
}

fuchsia_component("component") {
  cm_label = ":manifest"
  deps = [ ":bin" ]
}

fuchsia_structured_config_rust_lib("binder_proxy_config") {
  cm_label = ":manifest"
}

fuchsia_package("binder-proxy") {
  deps = [ ":component" ]
}

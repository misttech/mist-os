# Copyright 2025 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/components.gni")
import("//build/rust/rustc_library.gni")
import("//build/rust/rustc_test.gni")

group("tests") {
  testonly = true

  deps = [
    ":starnix_remotevol_key_file_integration_test",
    ":starnix_remotevol_shutdown_integration_test",
    "//src/starnix/tests/remotevol/symlinks:tests",
  ]
}

test_deps = [
  "//src/lib/diagnostics/reader/rust",
  "//src/lib/fuchsia",
  "//src/lib/fuchsia-component-test",
  "//third_party/rust_crates:futures",
  "//third_party/rust_crates:log",
]

rustc_library("remotevol_fuchsia_test_util") {
  testonly = true
  name = "remotevol_fuchsia_test_util"
  edition = "2021"
  source_root = "lib/fuchsia.rs"
  sources = [ source_root ]
  deps = [
    "//sdk/fidl/fuchsia.io:fuchsia.io_rust",
    "//sdk/fidl/fuchsia.sys2:fuchsia.sys2_rust",
    "//src/lib/diagnostics/reader/rust",
    "//src/lib/fidl/rust/fidl",
    "//src/lib/fuchsia-async",
    "//src/lib/fuchsia-component-test",
    "//src/lib/fuchsia-fs",
    "//third_party/rust_crates:log",
  ]
}

rustc_library("remotevol_linux_test_util") {
  testonly = true
  name = "remotevol_linux_test_util"
  edition = "2021"
  source_root = "lib/linux.rs"
  sources = [ source_root ]
  deps = [
    "//src/starnix/lib/linux_uapi",
    "//third_party/rust_crates:libc",
    "//third_party/rust_crates:zerocopy",
  ]
}

rustc_test("remotevol_shutdown_integration_test_bin") {
  edition = "2021"
  source_root = "src/shutdown.rs"
  sources = [ source_root ]
  deps = test_deps
  deps += [
    "//sdk/fidl/fuchsia.io:fuchsia.io_rust",
    "//src/lib/fuchsia-fs",
    "//src/sys/lib/component-events",
    "//third_party/rust_crates:assert_matches",
  ]
}

rustc_test("remotevol_key_file_integration_test_bin") {
  edition = "2021"
  source_root = "src/key_file.rs"
  sources = [ source_root ]
  deps = test_deps
  deps += [
    ":remotevol_fuchsia_test_util",
    "//sdk/fidl/fuchsia.sys2:fuchsia.sys2_rust",
    "//src/lib/fuchsia-component",
    "//src/lib/fuchsia-fs",
    "//src/storage/fxfs/test-fxfs/fidl/fuchsia.test.fxfs:fuchsia.test.fxfs_rust",
  ]
}

fuchsia_test_component("remotevol_shutdown_test") {
  manifest = "meta/shutdown_integration_test.cml"

  # fuchsia-component-test has an expect_includes on the realm_builder.shard.cml
  # but this test requires a persistent storage realm builder shard instead.
  check_includes = false
  deps = [ ":remotevol_shutdown_integration_test_bin" ]
  test_type = "starnix"
}

fuchsia_test_component("remotevol_key_file_test") {
  manifest = "meta/key_file_integration_test.cml"

  # fuchsia-component-test has an expect_includes on the realm_builder.shard.cml
  # but this test requires a persistent storage realm builder shard instead.
  check_includes = false
  deps = [ ":remotevol_key_file_integration_test_bin" ]
  test_type = "starnix"
}

fuchsia_component("kernel_with_container") {
  testonly = true
  manifest = "meta/kernel_with_container.cml"
}

fuchsia_test_package("starnix_remotevol_shutdown_integration_test") {
  test_components = [ ":remotevol_shutdown_test" ]
  deps = [ ":kernel_with_container" ]
  subpackages = [
    "//src/starnix/kernel:starnix_kernel_package",
    "//src/storage/fxfs/test-fxfs:test-fxfs-package",
    "//src/starnix/containers/debian:debian_package",
  ]
}

fuchsia_test_package("starnix_remotevol_key_file_integration_test") {
  test_components = [ ":remotevol_key_file_test" ]
  deps = [ ":kernel_with_container" ]
  subpackages = [
    "//src/starnix/kernel:starnix_kernel_package",
    "//src/storage/fxfs/test-fxfs:test-fxfs-package",
    "//src/starnix/containers/debian:debian_package",
  ]
}

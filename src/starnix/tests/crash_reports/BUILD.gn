# Copyright 2024 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/components.gni")
import("//build/rust/rustc_binary.gni")
import("//src/starnix/build/starnix_linux_executable.gni")
import("//src/starnix/kernel/starnix.gni")

group("tests") {
  testonly = true
  deps = [ ":starnix_crash_report_test" ]
}

rustc_binary("bin") {
  testonly = true
  edition = "2021"
  output_name = "starnix_crash_report_test"
  sources = [ "src/main.rs" ]
  deps = [
    "//sdk/fidl/fuchsia.feedback:fuchsia.feedback_rust",
    "//src/lib/fuchsia",
    "//src/lib/fuchsia-component",
    "//src/lib/fuchsia-component-test",
    "//src/sys/lib/component-events",
    "//third_party/rust_crates:futures",
    "//third_party/rust_crates:log",
  ]
}

fuchsia_test_component("integration_test") {
  manifest = "meta/integration_test.cml"
  deps = [ ":bin" ]
  test_type = "starnix"
}

starnix_linux_executable("generate_linux_crash_report") {
  testonly = true
  sources = [ "src/crasher.cc" ]
}

fuchsia_component("container_with_crasher") {
  testonly = true
  manifest = "meta/container_with_crasher.cml"
}

fuchsia_component("crasher") {
  testonly = true
  manifest = "meta/crasher.cml"
  deps = [ ":generate_linux_crash_report" ]
}

fuchsia_test_package("starnix_crash_report_test") {
  test_components = [ ":integration_test" ]
  deps = [
    ":container_with_crasher",
    ":crasher",
  ]
  subpackages = [
    "//src/starnix/containers/debian:debian_package",
    "//src/starnix/kernel:starnix_kernel_package",
  ]
}

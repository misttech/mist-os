# Copyright 2023 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

declare_args() {
  # Whether to use CowMap instead of BTreeMap to back RangeMap.
  starnix_use_cowmap = false
}

import("//build/components.gni")
import("//build/rust/rustc_library.gni")

rustc_library("range_map") {
  edition = "2021"
  sources = [ "src/lib.rs" ]
  configs += [ "//src/starnix/config:starnix_clippy_lints" ]

  if (starnix_use_cowmap) {
    features = [ "use_cowmap" ]

    deps = [ "//src/starnix/lib/cowmap" ]
  }
}

# Include starnix in the name for easier dashboarding of flakes.
rustc_test("starnix_range_map_test") {
  edition = "2021"
  sources = [ "src/lib.rs" ]
  deps = [ "//src/lib/fuchsia" ]
  configs += [ "//src/starnix/config:starnix_clippy_lints" ]
}

fuchsia_unittest_component("unittests_component") {
  component_name = "range_map_unittests"
  deps = [ ":starnix_range_map_test" ]
}

fuchsia_test_package("starnix_range_map_unittests") {
  test_components = [ ":unittests_component" ]
}

group("tests") {
  testonly = true
  deps = [
    ":starnix_range_map_test($host_toolchain)",
    ":starnix_range_map_unittests",
  ]
}

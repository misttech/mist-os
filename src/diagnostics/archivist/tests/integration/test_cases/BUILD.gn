# Copyrigh 2024 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/components.gni")
import("//build/rust/rustc_binary.gni")
import("//build/rust/rustc_test.gni")
import("//sdk/ctf/build/ctf.gni")
import("//src/diagnostics/archivist/build/pipeline.gni")

rustc_test("bin") {
  output_name = "archivist_integration_test_cases"
  edition = "2021"
  deps = [
    "../realm_factory/fidl:fuchsia.archivist.test_rust",
    "//sdk/fidl/fuchsia.diagnostics:fuchsia.diagnostics_rust",
    "//sdk/fidl/fuchsia.diagnostics.host:fuchsia.diagnostics.host_rust",
    "//sdk/fidl/fuchsia.logger:fuchsia.logger_rust",
    "//sdk/fidl/fuchsia.testing.harness:fuchsia.testing.harness_rust",
    "//sdk/rust/zx",
    "//src/lib/diagnostics/data/rust",
    "//src/lib/diagnostics/reader/rust",
    "//src/lib/diagnostics/selectors",
    "//src/lib/diagnostics/testing/diagnostics-assertions/rust:diagnostics-assertions",
    "//src/lib/fidl/rust/fidl",
    "//src/lib/fuchsia",
    "//src/lib/fuchsia-async",
    "//src/lib/fuchsia-component",
    "//src/lib/syslog/rust:syslog-listener",
    "//src/testing/realm_proxy/client",
    "//third_party/rust_crates:anyhow",
    "//third_party/rust_crates:async-trait",
    "//third_party/rust_crates:difference",
    "//third_party/rust_crates:futures",
    "//third_party/rust_crates:itertools",
    "//third_party/rust_crates:log",
    "//third_party/rust_crates:rand",
    "//third_party/rust_crates:serde_json",
  ]

  sources = [
    "src/assert.rs",
    "src/inspect/mod.rs",
    "src/inspect/reader.rs",
    "src/inspect/recursive_glob.rs",
    "src/inspect/truncation.rs",
    "src/lib.rs",
    "src/logs/attribution.rs",
    "src/logs/basic.rs",
    "src/logs/budget.rs",
    "src/logs/crash.rs",
    "src/logs/host.rs",
    "src/logs/interest.rs",
    "src/logs/lifecycle.rs",
    "src/logs/lifecycle_stop.rs",
    "src/logs/log_stream.rs",
    "src/logs/mod.rs",
    "src/logs/selectors.rs",
    "src/logs/sorting.rs",
    "src/logs/utils.rs",
    "src/puppet.rs",
    "src/test_topology.rs",
    "src/utils.rs",
  ]

  if (current_build_target_api_level == "PLATFORM" ||
      current_build_target_api_level == "HEAD") {
    sources += [ "src/inspect/escrow.rs" ]

    deps += [ "//src/lib/diagnostics/log/encoding/rust" ]
  }

  inputs = [
    "test_data/unified_reader_all_golden.json",
    "test_data/pipeline_reader_all_golden.json",
    "test_data/unified_reader_full_filter_golden.json",
    "test_data/pipeline_reader_nonoverlapping_selectors_golden.json",
    "test_data/unified_reader_single_value_golden.json",
    "test_data/pipeline_reader_single_value_golden.json",
  ]
  configs += [ "//build/config/rust/lints:clippy_warn_all" ]
}

fuchsia_component("archivist_integration_test_cases_component") {
  testonly = true
  component_name = "test_cases"
  deps = [ ":bin" ]
  manifest = "meta/test_cases.cml"
}

ctf_fuchsia_package("pkg") {
  testonly = true
  package_name = "archivist_integration_test_cases"
  deps = [ ":archivist_integration_test_cases_component" ]
}

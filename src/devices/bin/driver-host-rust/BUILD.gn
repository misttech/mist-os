# Copyright 2025 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/components.gni")
import("//build/rust/rustc_binary.gni")

group("driver-host-rust") {
  testonly = true
  deps = [
    ":package",
    ":tests",
  ]
}

group("tests") {
  testonly = true
  deps = [ ":driver-host-rust-unittests" ]
}

rustc_binary("bin") {
  output_name = "driver-host-rust"
  edition = "2021"

  with_unit_tests = true

  deps = [
    "//sdk/fidl/fuchsia.data:fuchsia.data_rust",
    "//sdk/fidl/fuchsia.driver.framework:fuchsia.driver.framework_rust",
    "//sdk/fidl/fuchsia.driver.host:fuchsia.driver.host_rust",
    "//sdk/fidl/fuchsia.io:fuchsia.io_rust",
    "//sdk/fidl/fuchsia.ldsvc:fuchsia.ldsvc_rust",
    "//sdk/lib/driver/component/rust",
    "//sdk/lib/driver/runtime/rust",
    "//sdk/lib/driver/runtime/rust/env",
    "//sdk/lib/driver/runtime/rust/fdf_sys",
    "//sdk/rust/zx",
    "//src/lib/diagnostics/inspect/runtime/rust",
    "//src/lib/diagnostics/inspect/rust",
    "//src/lib/driver_symbols:driver_symbols_rust",
    "//src/lib/fidl/rust/fidl",
    "//src/lib/fuchsia",
    "//src/lib/fuchsia-async",
    "//src/lib/fuchsia-component",
    "//src/lib/fuchsia-runtime",
    "//src/lib/fuchsia-sync",
    "//src/lib/trace/rust:trace-provider",
    "//src/sys/lib/namespace",
    "//src/sys/lib/stdout-to-debuglog/rust",
    "//third_party/rust_crates:anyhow",
    "//third_party/rust_crates:futures",
    "//third_party/rust_crates:libc",
    "//third_party/rust_crates:log",
  ]

  sources = [
    "src/driver.rs",
    "src/driver_host.rs",
    "src/loader.rs",
    "src/main.rs",
    "src/modules.rs",
    "src/utils.rs",
  ]
}

fuchsia_component("component") {
  component_name = "driver_host"
  manifest = "meta/driver_host.cml"
  deps = [ ":bin" ]
}

fuchsia_component("next_component") {
  component_name = "driver_host_next"
  manifest = "meta/driver_host_next.cml"
  deps = [ ":bin" ]
}

fuchsia_package("package") {
  package_name = "driver_host"
  deps = [
    ":component",
    ":next_component",
  ]
}

# Run with `fx test driver-host-rust-unittests`.
fuchsia_unittest_package("driver-host-rust-unittests") {
  deps = [ ":bin_test" ]
  test_specs = {
    log_settings = {
      max_severity = "ERROR"
    }
  }
}

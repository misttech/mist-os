# Copyright 2024 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/components.gni")
import("//build/test.gni")

source_set("test-libs") {
  testonly = true
  public_deps = [
    "..:common",
    "//sdk/lib/driver/testing/cpp",
    "//src/devices/bus/testing/fake-pdev",
    "//src/devices/testing/fake-dma-buffer",
    "//src/devices/testing/fake-mmio-reg",
    "//src/lib/fxl/test:gtest_main",
    "//src/lib/testing/predicates",
  ]
}

test("xhci-unittest-bin") {
  if (is_fuchsia) {
    fdio_config = [ "//build/config/fuchsia:fdio_config" ]
    if (configs + fdio_config - fdio_config != configs) {
      configs -= fdio_config
    }
  }
  output_name = "xhci-unittest"
  sources = [
    "../xhci-enumeration.cc",
    "test-endpoint.cc",
    "test-xhci.cc",
    "xhci-enumeration-test.cc",
  ]
  deps = [
    ":test-libs",
    "//zircon/system/ulib/mmio-ptr",
  ]
}

test("xhci-core-unittest-bin") {
  if (is_fuchsia) {
    configs += [ "//build/config:all_source" ]
  }
  if (is_fuchsia) {
    fdio_config = [ "//build/config/fuchsia:fdio_config" ]
    if (configs + fdio_config - fdio_config != configs) {
      configs -= fdio_config
    }
  }
  output_name = "xhci-core-unittest"
  sources = [
    "../usb-xhci.cc",
    "../xhci-device-state.cc",
    "../xhci-endpoint.cc",
    "usb-xhci-test.cc",
  ]
  deps = [
    ":test-libs",
    "//src/devices/lib/driver:driver_runtime",
  ]
  defines = [ "TEST_SKIP_INIT" ]
}

test("xhci-event-ring-unittest-bin") {
  if (is_fuchsia) {
    fdio_config = [ "//build/config/fuchsia:fdio_config" ]
    if (configs + fdio_config - fdio_config != configs) {
      configs -= fdio_config
    }
  }
  output_name = "xhci-event-ring-unittest"
  sources = [
    "../xhci-event-ring.cc",
    "../xhci-interrupter.cc",
    "test-endpoint.cc",
    "test-xhci.cc",
    "xhci-event-ring-test.cc",
  ]
  deps = [
    ":test-libs",
    "//sdk/lib/async:async-cpp",
    "//sdk/lib/async-loop:async-loop-cpp",
  ]
}

test("xhci-transfer-ring-unittest-bin") {
  if (is_fuchsia) {
    fdio_config = [ "//build/config/fuchsia:fdio_config" ]
    if (configs + fdio_config - fdio_config != configs) {
      configs -= fdio_config
    }
  }
  output_name = "xhci-transfer-ring-unittest"
  sources = [
    "../xhci-transfer-ring.cc",
    "test-endpoint.cc",
    "test-xhci.cc",
    "xhci-transfer-ring-test.cc",
  ]
  deps = [ ":test-libs" ]
}

test("xhci-endpoint-unittest-bin") {
  if (is_fuchsia) {
    fdio_config = [ "//build/config/fuchsia:fdio_config" ]
    if (configs + fdio_config - fdio_config != configs) {
      configs -= fdio_config
    }
  }
  output_name = "xhci-endpoint-unittest"
  sources = [
    "../xhci-endpoint.cc",
    "test-xhci.cc",
    "xhci-endpoint-test.cc",
  ]
  deps = [
    ":test-libs",
    "//src/devices/testing/fake-bti",
  ]
}

fuchsia_unittest_package("xhci-unittest") {
  deps = [ ":xhci-unittest-bin" ]
  test_specs = {
    environments = basic_envs
    log_settings = {
      max_severity = "ERROR"
    }
  }
}

fuchsia_unittest_package("xhci-core-unittest") {
  deps = [ ":xhci-core-unittest-bin" ]
  test_specs = {
    environments = basic_envs
  }
}

fuchsia_unittest_package("xhci-event-ring-unittest") {
  deps = [ ":xhci-event-ring-unittest-bin" ]
  test_specs = {
    environments = basic_envs
  }
}

fuchsia_unittest_package("xhci-transfer-ring-unittest") {
  deps = [ ":xhci-transfer-ring-unittest-bin" ]
  test_specs = {
    environments = basic_envs
    log_settings = {
      max_severity = "ERROR"
    }
  }
}

fuchsia_unittest_package("xhci-endpoint-unittest") {
  deps = [ ":xhci-endpoint-unittest-bin" ]
  test_specs = {
    environments = basic_envs
    log_settings = {
      max_severity = "ERROR"
    }
  }
}

group("unittests") {
  testonly = true
  deps = [
    ":xhci-core-unittest",
    ":xhci-endpoint-unittest",
    ":xhci-event-ring-unittest",
    ":xhci-transfer-ring-unittest",
    ":xhci-unittest",
  ]
}

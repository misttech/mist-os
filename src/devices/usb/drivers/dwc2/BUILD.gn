# Copyright 2019 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/bind/bind.gni")
import("//build/components.gni")
import("//build/drivers.gni")
import("//build/test.gni")

driver_bind_rules("dwc2_bind") {
  rules = "meta/dwc2.bind"
  bind_output = "dwc2.bindbc"
  tests = "meta/tests.json"
  deps = [
    "//sdk/fidl/fuchsia.hardware.usb.phy:fuchsia.hardware.usb.phy_bindlib",
    "//src/devices/bind/fuchsia.designware.platform",
    "//src/devices/bind/fuchsia.devicetree",
    "//src/devices/bind/fuchsia.platform",
    "//src/devices/bind/fuchsia.usb.phy",
  ]
}

source_set("common") {
  sources = [
    "dwc2.cc",
    "dwc2.h",
    "usb_dwc_regs.h",
  ]
  configs += [
    "//build/config:all_source",
    "//build/config/fuchsia:enable_zircon_asserts",
  ]
  deps = [
    "//sdk/lib/driver/platform-device/cpp",
    "//src/devices/bind/fuchsia:fuchsia_cpp",
    "//src/devices/bind/fuchsia.designware.platform:fuchsia.designware.platform_cpp",
    "//src/devices/lib/driver",
    "//src/devices/lib/driver:driver_runtime",
    "//zircon/system/ulib/fbl",
    "//zircon/system/ulib/pretty",
    "//zircon/system/ulib/sync",
    "//zircon/system/ulib/zx",
  ]
  public_deps = [
    ":dwc2-config",
    "//sdk/banjo/fuchsia.hardware.usb:fuchsia.hardware.usb_banjo_cpp",
    "//sdk/banjo/fuchsia.hardware.usb.dci:fuchsia.hardware.usb.dci_banjo_cpp",
    "//sdk/banjo/fuchsia.hardware.usb.phy:fuchsia.hardware.usb.phy_banjo_cpp",
    "//sdk/banjo/fuchsia.hardware.usb.request:fuchsia.hardware.usb.request_banjo_cpp",
    "//sdk/fidl/fuchsia.hardware.usb.dci:fuchsia.hardware.usb.dci_cpp",
    "//sdk/lib/async-loop:async-loop-cpp",
    "//sdk/lib/component/outgoing/cpp",
    "//sdk/lib/fit",
    "//src/devices/lib/mmio",
    "//src/devices/usb/lib/usb",
    "//src/devices/usb/lib/usb:dwc2",
    "//src/devices/usb/lib/usb-endpoint:usb-endpoint-server",
    "//src/devices/usb/lib/usb-phy",
    "//src/lib/ddk",
    "//src/lib/ddktl",
    "//zircon/system/ulib/hwreg",
  ]
}

fuchsia_cc_driver("dwc2-driver") {
  output_name = "dwc2"
  deps = [ ":common" ]
}

fuchsia_component_manifest("dwc2-manifest") {
  component_name = "dwc2"
  manifest = "meta/dwc2.cml"
}

fuchsia_driver_component("dwc2") {
  info = "meta/dwc2-info.json"
  cm_label = ":dwc2-manifest"
  deps = [
    ":dwc2-driver",
    ":dwc2_bind",
  ]
}

fuchsia_structured_config_cpp_elf_lib("dwc2-config") {
  cm_label = ":dwc2-manifest"
}

fuchsia_structured_config_values("dwc2-values") {
  cm_label = ":dwc2-manifest"
  values = {
    enable_suspend = false
  }
}

fuchsia_driver_package("package") {
  package_name = "dwc2"
  export_to_bazel = true
  driver_components = [ ":dwc2" ]
  deps = [ ":dwc2-values" ]
}

test("dwc2-test-bin") {
  sources = [ "dwc2-test.cc" ]
  output_name = "dwc2-test"
  deps = [
    ":common",
    "//src/devices/testing/mock-ddk",
    "//zircon/system/ulib/zxtest",
  ]
}

fuchsia_unittest_package("dwc2-test") {
  deps = [ ":dwc2-test-bin" ]
  test_specs = {
    environments = basic_envs
  }
}

group("tests") {
  testonly = true
  deps = [
    ":dwc2-test",
    ":dwc2_bind_test",
  ]
}

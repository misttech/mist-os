# Copyright 2019 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/bind/bind.gni")
import("//build/components.gni")
import("//build/drivers.gni")
import("//build/test.gni")

driver_bind_rules("usb_peripheral_bind") {
  rules = "meta/usb_peripheral.bind"
  bind_output = "usb-peripheral.bindbc"
  tests = "meta/tests.json"
  deps = [
    "//sdk/fidl/fuchsia.hardware.usb.dci:fuchsia.hardware.usb.dci_bindlib",
    "//src/devices/bind/fuchsia",
    "//src/devices/bind/fuchsia.designware.platform",
    "//src/devices/bind/fuchsia.test.platform",
    "//src/devices/bind/fuchsia.usb",
  ]
}

source_set("common") {
  configs += [
    "//build/config:all_source",
    "//build/config/fuchsia:enable_zircon_asserts",
  ]
  sources = [
    "config-parser.cc",
    "config-parser.h",
    "usb-dci-interface-server.cc",
    "usb-dci-interface-server.h",
    "usb-function.cc",
    "usb-function.h",
    "usb-peripheral.cc",
    "usb-peripheral.h",
  ]
  deps = [
    "//sdk/banjo/fuchsia.hardware.usb.request:fuchsia.hardware.usb.request_banjo_cpp",
    "//sdk/fidl/fuchsia.hardware.usb.descriptor:fuchsia.hardware.usb.descriptor_cpp",
    "//sdk/lib/component/outgoing/cpp",
    "//sdk/lib/fidl",
    "//src/devices/bind/fuchsia:fuchsia_cpp",
    "//src/devices/bind/fuchsia.usb:fuchsia.usb_cpp",
    "//src/devices/lib/driver",
    "//src/devices/lib/driver:driver_runtime",
    "//src/devices/usb/lib/usb:cdc",
    "//src/devices/usb/lib/usb:peripheral",
    "//src/lib/ddk",
    "//src/lib/ddk:ddk-metadata-headers",
  ]
  public_deps = [
    ":usb-peripheral-config",
    "//sdk/banjo/fuchsia.hardware.usb:fuchsia.hardware.usb_banjo_cpp",
    "//sdk/banjo/fuchsia.hardware.usb.dci:fuchsia.hardware.usb.dci_banjo_cpp",
    "//sdk/banjo/fuchsia.hardware.usb.function:fuchsia.hardware.usb.function_banjo_cpp",
    "//sdk/fidl/fuchsia.hardware.usb.dci:fuchsia.hardware.usb.dci_cpp",
    "//sdk/fidl/fuchsia.hardware.usb.function:fuchsia.hardware.usb.function_cpp",
    "//sdk/fidl/fuchsia.hardware.usb.peripheral:fuchsia.hardware.usb.peripheral_cpp",
    "//src/devices/usb/lib/usb",
    "//src/devices/usb/lib/usb-monitor-util",
    "//src/lib/ddktl",
    "//zircon/system/ulib/fbl",
    "//zircon/system/ulib/sync:sync-cpp",
  ]
}

fuchsia_cc_driver("usb-peripheral-driver") {
  legacy = true
  output_name = "usb-peripheral"
  deps = [
    ":common",
    ":usb_peripheral_bind",
  ]
}

fuchsia_component_manifest("manifest") {
  component_name = "usb-peripheral"
  manifest = "meta/usb-peripheral.cml"
}

fuchsia_structured_config_cpp_elf_lib("usb-peripheral-config") {
  cm_label = ":manifest"
}

fuchsia_driver_component("driver") {
  info = "meta/usb-peripheral-info.json"
  cm_label = ":manifest"
  deps = [ ":usb-peripheral-driver" ]
}

group("usb-peripheral") {
  deps = [ ":driver" ]
}

fuchsia_driver_package("package") {
  package_name = "usb-peripheral"
  driver_components = [ ":driver" ]
}

test("usb-peripheral-unittest-bin") {
  sources = [ "usb-peripheral-test.cc" ]
  output_name = "usb-peripheral-unittest"
  deps = [
    ":common",
    "//sdk/fidl/fuchsia.hardware.usb.dci:fuchsia.hardware.usb.dci_cpp",
    "//sdk/fidl/fuchsia.hardware.usb.descriptor:fuchsia.hardware.usb.descriptor_cpp",
    "//sdk/lib/async_patterns/testing/cpp",
    "//sdk/lib/component/outgoing/cpp",
    "//sdk/lib/driver/testing/cpp",
    "//src/devices/testing/mock-ddk",
    "//src/devices/usb/lib/usb",
    "//zircon/system/ulib/zxtest",
  ]
}

fuchsia_unittest_package("usb-peripheral-unittest") {
  deps = [ ":usb-peripheral-unittest-bin" ]
  test_specs = {
    log_settings = {
      max_severity = "ERROR"
    }
  }
}

group("tests") {
  testonly = true
  deps = [
    ":usb-peripheral-unittest",
    ":usb_peripheral_bind_test",
  ]
}

# Copyright 2025 The Fuchsia Authors
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/bind/bind.gni")
import("//build/components/fuchsia_unittest_package.gni")
import("//build/devicetree/devicetree.gni")
import("//build/drivers.gni")
import("//build/test.gni")

group("tests") {
  testonly = true
  deps = [
    ":pmem_tests",
    ":virtio_pmem_bind_test",
  ]
}

driver_bind_rules("virtio_pmem_bind") {
  bind_output = "virtio_pmem.bindbc"
  rules = "meta/virtio_pmem.bind"
  tests = "tests/bind_tests.json"
  deps = [
    "//sdk/fidl/fuchsia.hardware.pci:fuchsia.hardware.pci_bindlib",
    "//src/devices/bind/fuchsia.acpi",
    "//src/devices/bind/fuchsia.pci",
  ]
}

source_set("pmem_lib") {
  sources = [
    "pmem.cc",
    "pmem.h",
    "virtio/pmem.h",
  ]
  public_deps = [
    "//sdk/lib/driver/component/cpp",
    "//src/devices/bin/driver_runtime",
    "//src/devices/bus/lib/virtio:virtio-dfv2",
    "//zircon/system/ulib/zx",
  ]
  deps = [ "//sdk/fidl/fuchsia.kernel:fuchsia.kernel_cpp" ]
}

fuchsia_cc_driver("driver") {
  output_name = "virtio_pmem"

  sources = [ "pmem_driver.cc" ]

  deps = [
    ":pmem_lib",
    "//sdk/lib/driver/component/cpp",
    "//sdk/lib/driver/runtime:driver_runtime_cpp",
  ]
}

fuchsia_driver_component("component") {
  component_name = "virtio_pmem"
  deps = [
    ":driver",
    ":virtio_pmem_bind",
  ]
  info = "meta/virtio_pmem-info.json"
  manifest = "meta/virtio_pmem.cml"
}

fuchsia_driver_package("package") {
  package_name = "virtio_pmem"
  driver_components = [ ":component" ]
  export_to_bazel = true
}

test("pmem_tests_bin") {
  output_name = "pmem_tests"

  sources = [ "pmem_tests.cc" ]

  deps = [
    ":pmem_lib",
    "//sdk/lib/async-loop:async-loop-cpp",
    "//sdk/lib/async-loop:async-loop-default",
    "//sdk/lib/driver/testing/cpp:scoped_global_logger",
    "//src/devices/bus/lib/virtio:virtio-dfv2",
    "//src/devices/testing/fake-bti",
    "//src/devices/testing/fake-resource",
    "//src/lib/fxl/test:gtest_main",
    "//src/lib/testing/predicates",
  ]
}

fuchsia_unittest_package("pmem_tests") {
  deps = [ ":pmem_tests_bin" ]
}

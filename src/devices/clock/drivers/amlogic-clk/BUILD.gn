# Copyright 2019 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

assert(target_cpu == "arm64", "The amlogic-clk driver is only valid on arm64.")

import("//build/bind/bind.gni")
import("//build/components.gni")
import("//build/drivers.gni")
import("//build/test.gni")

group("tests") {
  testonly = true
  deps = [ ":amlogic-clk-test" ]
}

driver_bind_rules("aml-clk-bind") {
  rules = "meta/aml-clk.bind"
  bind_output = "amlogic-clk.bindbc"

  tests = "meta/bind-tests.json"
  deps = [
    "//src/devices/bind/fuchsia.amlogic.platform",
    "//src/devices/bind/fuchsia.devicetree",
    "//src/devices/bind/fuchsia.platform",
  ]
}

source_set("amlogic-clk-driver-source") {
  output_name = "amlogic-clk"
  configs += [
    "//build/config:all_source",
    "//build/config/fuchsia:enable_zircon_asserts",
  ]
  sources = [
    "aml-a1-blocks.h",
    "aml-a5-blocks.h",
    "aml-axg-blocks.h",
    "aml-clk-blocks.h",
    "aml-clk-blocks.h",
    "aml-clk.cc",
    "aml-clk.h",
    "aml-fclk-rates.cc",
    "aml-fclk.h",
    "aml-g12a-blocks.h",
    "aml-g12b-blocks.h",
    "aml-gxl-blocks.h",
    "aml-sm1-blocks.h",
    "meson-cpu-clock.cc",
    "meson-cpu-clock.h",
    "meson-pll-clock.cc",
    "meson-pll-clock.h",
    "meson-rate-clock.h",
  ]
  public_deps = [
    "//sdk/fidl/fuchsia.hardware.clock:fuchsia.hardware.clock_cpp",
    "//sdk/fidl/fuchsia.hardware.clock.measure:fuchsia.hardware.clock.measure_cpp",
    "//sdk/fidl/fuchsia.hardware.clockimpl:fuchsia.hardware.clockimpl_cpp",
    "//sdk/lib/driver/compat/cpp",
    "//sdk/lib/driver/devfs/cpp",
    "//sdk/lib/driver/metadata/cpp",
    "//sdk/lib/driver/platform-device/cpp",
    "//src/devices/lib/amlogic",
    "//src/lib/ddk",
    "//zircon/system/ulib/fbl",
    "//zircon/system/ulib/hwreg",
    "//zircon/system/ulib/zircon-internal",
  ]
  deps = [
    "//src/devices/bind/fuchsia.clock:fuchsia.clock_cpp",
    "//src/devices/lib/driver:driver_runtime",
    "//src/devices/lib/mmio",
    "//zircon/system/ulib/sync",
    "//zircon/system/ulib/zx",
  ]
}

fuchsia_cc_driver("amlogic-clk-driver") {
  output_name = "amlogic-clk"
  deps = [
    ":aml-clk-bind",
    ":amlogic-clk-driver-source",
  ]
}

fuchsia_driver_component("amlogic-clk") {
  component_name = "amlogic-clk"
  deps = [ ":amlogic-clk-driver" ]
  info = "meta/amlogic-clk-info.json"
  manifest = "meta/amlogic-clk.cml"
}

fuchsia_driver_package("package") {
  package_name = "amlogic-clk"
  export_to_bazel = true
  driver_components = [ ":amlogic-clk" ]
}

test("amlogic-clk-test-bin") {
  output_name = "amlogic-clk-test"
  sources = [ "aml-clk-test.cc" ]
  deps = [
    ":amlogic-clk-driver-source",
    "//sdk/lib/driver/fake-platform-device/cpp",
    "//sdk/lib/driver/testing/cpp",
    "//src/devices/lib/mmio",
    "//src/devices/lib/mmio:test_helper",
    "//src/lib/fxl/test:gtest_main",
    "//src/lib/testing/predicates",
    "//zircon/system/ulib/mmio-ptr:mmio-ptr-fake",
  ]
}

fuchsia_unittest_package("amlogic-clk-test") {
  manifest = "meta/amlogic-clk-test.cml"
  deps = [
    ":aml-clk-bind_test",
    ":amlogic-clk-test-bin",
  ]
  test_specs = {
    log_settings = {
      max_severity = "ERROR"
    }
  }
}

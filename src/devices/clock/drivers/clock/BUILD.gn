# Copyright 2019 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/bind/bind.gni")
import("//build/components.gni")
import("//build/drivers.gni")
import("//build/test.gni")

driver_bind_rules("clock-bind") {
  rules = "meta/clock.bind"
  bind_output = "clock.bindbc"
  tests = "meta/bind-tests.json"
  deps = [
    "//sdk/fidl/fuchsia.hardware.clockimpl:fuchsia.hardware.clockimpl_bindlib",
    "//src/devices/bind/fuchsia.clock",
  ]
}

source_set("common") {
  configs += [
    "//build/config:all_source",
    "//build/config/fuchsia:enable_zircon_asserts",
  ]
  sources = [ "clock.cc" ]
  deps = [
    "//src/devices/bind/fuchsia:fuchsia_cpp",
    "//src/devices/bind/fuchsia.clock:fuchsia.clock_cpp",
    "//src/devices/lib/driver:driver_runtime",
    "//src/lib/ddk",
    "//src/lib/ddk:ddk-metadata-headers",
    "//zircon/system/ulib/fbl",
    "//zircon/system/ulib/zx",
  ]
  public_deps = [
    "//sdk/fidl/fuchsia.hardware.clock:fuchsia.hardware.clock_cpp",
    "//sdk/fidl/fuchsia.hardware.clockimpl:fuchsia.hardware.clockimpl_cpp",
    "//sdk/lib/component/outgoing/cpp",
    "//sdk/lib/driver/compat/cpp",
    "//src/lib/ddktl",
  ]
}

fuchsia_cc_driver("clock-driver") {
  output_name = "clock"
  deps = [ ":common" ]
}

fuchsia_driver_component("clock") {
  visibility = [
    ":*",
    "//src/devices/clock:drivers",
  ]
  component_name = "clock"
  deps = [
    ":clock-bind",
    ":clock-driver",
  ]
  info = "meta/clock-info.json"
  manifest = "meta/clock.cml"
}

fuchsia_driver_package("package") {
  package_name = "clock"
  driver_components = [ ":clock" ]
}

test("clock-test-bin") {
  sources = [ "clock-test.cc" ]
  deps = [
    ":common",
    "//sdk/fidl/fuchsia.hardware.clock:fuchsia.hardware.clock_cpp",
    "//sdk/lib/async_patterns/testing/cpp",
    "//sdk/lib/driver/testing/cpp",
    "//src/devices/testing/mock-ddk:mock-ddk",
    "//src/lib/ddk:ddk-metadata-headers",
    "//src/lib/fxl/test:gtest_main",
    "//src/lib/testing/predicates",
  ]
}

fuchsia_unittest_package("clock-test") {
  deps = [ ":clock-test-bin" ]
  test_specs = {
    environments = basic_envs
    log_settings = {
      max_severity = "ERROR"
    }
  }
}

group("tests") {
  testonly = true
  deps = [
    ":clock-bind_test",
    ":clock-test",
  ]
}

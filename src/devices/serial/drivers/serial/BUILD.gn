# Copyright 2019 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/bind/bind.gni")
import("//build/drivers.gni")

driver_bind_rules("serial_bind") {
  rules = "meta/serial.bind"
  bind_output = "serial.bindbc"
  tests = "tests/tests.json"
  deps = [ "//sdk/fidl/fuchsia.hardware.serialimpl:fuchsia.hardware.serialimpl_bindlib" ]
}

fuchsia_cc_driver("serial-driver") {
  output_name = "serial"
  configs += [
    "//build/config:all_source",
    "//build/config/fuchsia:enable_zircon_asserts",
  ]
  deps = [ ":common" ]
}

fuchsia_driver_component("serial") {
  visibility = [ ":*" ]
  info = "meta/serial-info.json"
  component_name = "serial"
  deps = [ ":serial-driver" ]
  manifest = "meta/serial.cml"
}

fuchsia_driver_package("package") {
  package_name = "serial"
  driver_components = [ ":serial" ]
}

source_set("common") {
  configs += [ "//build/config:all_source" ]
  sources = [ "serial.cc" ]
  public_deps = [
    ":serial_bind",
    "//sdk/fidl/fuchsia.hardware.serial:fuchsia.hardware.serial_cpp",
    "//sdk/fidl/fuchsia.hardware.serialimpl:fuchsia.hardware.serialimpl_cpp",
    "//sdk/lib/driver/component/cpp",
    "//sdk/lib/driver/node/cpp",
    "//sdk/lib/driver/runtime:driver_runtime_cpp",
    "//src/devices/bin/driver_runtime",
    "//src/devices/bind/fuchsia:fuchsia_cpp",
    "//src/devices/bind/fuchsia.serial:fuchsia.serial_cpp",
    "//zircon/system/ulib/fbl",
    "//zircon/system/ulib/zircon-internal",
    "//zircon/system/ulib/zx",
  ]
}

group("tests") {
  testonly = true
  deps = [
    ":serial_bind_test",
    "tests",
  ]
}

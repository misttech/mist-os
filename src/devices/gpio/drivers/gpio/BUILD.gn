# Copyright 2019 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/bind/bind.gni")
import("//build/components.gni")
import("//build/drivers.gni")
import("//build/test.gni")

driver_bind_rules("gpio_bind") {
  rules = "meta/gpio.bind"
  bind_output = "gpio.bindbc"
  tests = "meta/bind_tests.json"
  deps =
      [ "//sdk/fidl/fuchsia.hardware.pinimpl:fuchsia.hardware.pinimpl_bindlib" ]
}

fuchsia_cc_driver("gpio-driver") {
  output_name = "gpio"
  configs += [
    "//build/config:all_source",
    "//build/config/fuchsia:enable_zircon_asserts",
  ]
  sources = [ "gpio.cc" ]
  deps = [
    ":gpio_bind",
    "//sdk/fidl/fuchsia.hardware.gpio:fuchsia.hardware.gpio_cpp",
    "//sdk/fidl/fuchsia.hardware.pin:fuchsia.hardware.pin_cpp",
    "//sdk/fidl/fuchsia.hardware.pinimpl:fuchsia.hardware.pinimpl_cpp",
    "//sdk/fidl/fuchsia.scheduler:fuchsia.scheduler_cpp",
    "//sdk/lib/component/outgoing/cpp",
    "//sdk/lib/driver/compat/cpp",
    "//sdk/lib/driver/node/cpp",
    "//sdk/lib/driver/runtime:driver_runtime_cpp",
    "//src/devices/bind/fuchsia:fuchsia_cpp",
    "//src/devices/bind/fuchsia.gpio:fuchsia.gpio_cpp",
    "//src/lib/ddk:ddk-metadata-headers",
    "//src/lib/ddk:ddk_metadata",
    "//zircon/system/ulib/fbl",
    "//zircon/system/ulib/zx",
  ]
}

fuchsia_driver_component("gpio") {
  visibility = [ ":*" ]
  component_name = "gpio"
  deps = [ ":gpio-driver" ]
  info = "meta/gpio-info.json"
  manifest = "meta/gpio.cml"
}

fuchsia_driver_package("package") {
  package_name = "gpio"
  driver_components = [ ":gpio" ]
}

test("gpio-test-bin") {
  output_name = "gpio-test"
  sources = [
    "gpio-test.cc",
    "gpio.cc",
  ]
  deps = [
    ":gpio_bind",
    "//sdk/fidl/fuchsia.hardware.gpio:fuchsia.hardware.gpio_cpp",
    "//sdk/fidl/fuchsia.hardware.pin:fuchsia.hardware.pin_cpp",
    "//sdk/fidl/fuchsia.hardware.pinimpl:fuchsia.hardware.pinimpl_cpp",
    "//sdk/fidl/fuchsia.scheduler:fuchsia.scheduler_cpp",
    "//sdk/lib/async_patterns/testing/cpp",
    "//sdk/lib/driver/compat/cpp",
    "//sdk/lib/driver/node/cpp",
    "//sdk/lib/driver/outgoing/cpp",
    "//sdk/lib/driver/runtime:driver_runtime_cpp",
    "//sdk/lib/driver/testing/cpp",
    "//src/devices/bind/fuchsia:fuchsia_cpp",
    "//src/devices/bind/fuchsia.gpio:fuchsia.gpio_cpp",
    "//src/lib/ddk:ddk-metadata-headers",
    "//src/lib/ddk:ddk_metadata",
    "//src/lib/fxl/test:gtest_main",
    "//src/lib/testing/predicates",
    "//zircon/system/ulib/fbl",
    "//zircon/system/ulib/zx",
  ]
}

fuchsia_unittest_package("gpio-test") {
  deps = [ ":gpio-test-bin" ]
  test_specs = {
    environments = basic_envs
    log_settings = {
      max_severity = "ERROR"
    }
  }
}

group("tests") {
  testonly = true
  deps = [
    ":gpio-test",
    ":gpio_bind_test",
  ]
}

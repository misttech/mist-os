# Copyright 2021 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/bind/bind.gni")
import("//build/components.gni")
import("//build/drivers.gni")
import("//build/test.gni")

driver_bind_rules("tpm_bind") {
  rules = "meta/tpm.bind"
  tests = "meta/bind_tests.json"
  deps = [ "//src/devices/bind/fuchsia.tpm" ]
}

common_deps = [
  ":tpm_bind",
  "//sdk/banjo/fuchsia.hardware.tpmimpl:fuchsia.hardware.tpmimpl_banjo_cpp",
  "//sdk/fidl/fuchsia.hardware.tpmimpl:fuchsia.hardware.tpmimpl_cpp",
  "//sdk/fidl/fuchsia.tpm:fuchsia.tpm_cpp",
  "//sdk/lib/async-loop:async-loop-cpp",
  "//src/devices/lib/driver",
  "//src/lib/ddktl",
  "//zircon/system/ulib/hwreg",
  "//zircon/system/ulib/inspect",
  "//zircon/system/ulib/zx",
]

fuchsia_cc_driver("fuchsia_driver") {
  output_name = "tpm-driver"
  sources = [ "tpm.cc" ]
  deps = common_deps

  # TODO(https://fxbug.dev/42136089): delete the below and fix compiler warnings
  configs += [ "//build/config:Wno-conversion" ]
}

fuchsia_driver_component("tpm-driver") {
  info = "meta/tpm-driver-info.json"
  deps = [ ":fuchsia_driver" ]
  manifest = "meta/tpm-driver.cml"
}

fuchsia_driver_package("tpm-pkg") {
  package_name = "tpm"
  driver_components = [ ":tpm-driver" ]
}

test("tpm-driver-test-bin") {
  output_name = "tpm-driver-test"
  sources = [
    "tpm-test.cc",
    "tpm.cc",
  ]
  deps = common_deps + [
           "//src/devices/testing/mock-ddk",
           "//zircon/system/ulib/zxtest",
         ]

  # TODO(https://fxbug.dev/357662201): This test contains a new-delete-type-mismatch
  # error caught by ASan that should be fixed. This particular bug seems to hide
  # another ASan bug that should also be investigated and fixed. Rather than fixing
  # them now, let's temporarily disable this particular ASan check to unblock
  # the clang roll.
  deps += [ "//build/config/sanitizers:suppress-asan-new-delete-type-mismatch.DO-NOT-USE-THIS" ]

  # TODO(https://fxbug.dev/42136089): delete the below and fix compiler warnings
  configs += [ "//build/config:Wno-conversion" ]
}

fuchsia_unittest_package("tpm-driver-test") {
  deps = [ ":tpm-driver-test-bin" ]
}

group("tpm") {
  deps = [ ":tpm-pkg" ]
}

group("tests") {
  testonly = true
  deps = [
    ":tpm-driver-test",
    ":tpm_bind_test",
  ]
}

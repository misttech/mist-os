# Copyright 2019 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/bind/bind.gni")
import("//build/components.gni")
import("//build/drivers.gni")
import("//build/test.gni")

driver_bind_rules("nvme_bind") {
  rules = "meta/nvme.bind"
  tests = "meta/bind_tests.json"
  deps = [
    "//sdk/fidl/fuchsia.hardware.pci:fuchsia.hardware.pci_bindlib",
    "//src/devices/bind/fuchsia.acpi",
    "//src/devices/bind/fuchsia.pci",
    "//src/devices/bind/fuchsia.pci.massstorage",
  ]
}

group("tests") {
  testonly = true
  deps = [
    ":nvme-tests",
    ":nvme_bind_test",
  ]
}

common_deps = [
  ":nvme_bind",
  "//sdk/fidl/fuchsia.hardware.block.driver:fuchsia.hardware.block.driver_banjo_cpp",
  "//sdk/lib/driver/compat/cpp",
  "//sdk/lib/driver/runtime:driver_runtime_cpp",
  "//sdk/lib/inspect/component/cpp",
  "//src/devices/bin/driver_runtime",
  "//src/devices/block/lib/common",
  "//src/devices/lib/dma-buffer",
  "//src/devices/lib/mmio",
  "//src/devices/pci/lib/device-protocol-pci",
  "//src/devices/pci/lib/pci",
  "//zircon/system/ulib/fbl",
  "//zircon/system/ulib/fzl",
  "//zircon/system/ulib/hwreg",
  "//zircon/system/ulib/inspect",
  "//zircon/system/ulib/sync",
  "//zircon/system/ulib/zircon-internal",
]

fuchsia_cc_driver("nvme-driver") {
  output_name = "nvme"
  configs += [ "//build/config:all_source" ]
  sources = [
    "export.cc",
    "namespace.cc",
    "nvme.cc",
    "queue-pair.cc",
    "queue.cc",
  ]
  deps = common_deps
}

fuchsia_driver_component("nvme") {
  component_name = "nvme"
  deps = [ ":nvme-driver" ]
  info = "meta/nvme-info.json"
  manifest = "meta/nvme.cml"
}

fuchsia_driver_package("package") {
  package_name = "nvme"
  driver_components = [ ":nvme" ]
}

test("nvme-queue-tests-bin") {
  sources = [
    "queue-test.cc",
    "queue.cc",
  ]
  deps = [
    ":nvme_bind",
    "//src/devices/lib/dma-buffer",
    "//src/devices/testing/fake-bti",
    "//src/lib/fxl/test:gtest_main",
    "//src/lib/testing/predicates",
    "//zircon/system/ulib/fbl",
    "//zircon/system/ulib/hwreg",
  ]
}

fuchsia_unittest_component("nvme-queue-tests") {
  deps = [ ":nvme-queue-tests-bin" ]
}

test("nvme-queue-pair-tests-bin") {
  configs += [ "//build/config/fuchsia:enable_zircon_asserts" ]
  sources = [
    "queue-pair-test.cc",
    "queue-pair.cc",
    "queue.cc",
  ]
  deps = [
    ":nvme_bind",
    "//sdk/lib/driver/logging/cpp",
    "//sdk/lib/driver/testing/cpp:scoped_global_logger",
    "//src/devices/lib/dma-buffer",
    "//src/devices/lib/mmio",
    "//src/devices/lib/mmio:test_helper",
    "//src/devices/testing/fake-bti",
    "//src/lib/fxl/test:gtest_main",
    "//src/lib/testing/predicates",
    "//zircon/system/ulib/fbl",
    "//zircon/system/ulib/hwreg",
    "//zircon/system/ulib/mmio-ptr",
    "//zircon/system/ulib/sync",
  ]
}

fuchsia_unittest_component("nvme-queue-pair-tests") {
  deps = [ ":nvme-queue-pair-tests-bin" ]
}

test("nvme-unit-tests-bin") {
  configs += [ "//build/config:all_source" ]
  sources = [
    "fake/fake-admin-commands.cc",
    "fake/fake-controller.cc",
    "fake/fake-namespace.cc",
    "fake/fake-registers.cc",
    "namespace.cc",
    "nvme-test.cc",
    "nvme.cc",
    "queue-pair.cc",
    "queue.cc",
  ]
  deps = common_deps + [
           "//sdk/lib/driver/testing/cpp",
           "//sdk/lib/driver/testing/cpp:minimal_compat_environment",
           "//sdk/lib/inspect/testing/cpp",
           "//src/devices/testing/fake-bti",
           "//src/lib/fxl/test:gtest_main",
           "//src/lib/testing/predicates",
         ]
}

fuchsia_unittest_component("nvme-unit-tests") {
  deps = [ ":nvme-unit-tests-bin" ]
}

fuchsia_test_package("nvme-tests") {
  test_components = [
    ":nvme-queue-pair-tests",
    ":nvme-queue-tests",
    ":nvme-unit-tests",
  ]
}

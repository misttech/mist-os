# Copyright 2019 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/bazel/export_fuchsia_package_to_bazel.gni")
import("//build/bind/bind.gni")
import("//build/components.gni")
import("//build/drivers.gni")
import("//build/test.gni")

source_set("dma-descriptor-builder") {
  public = [ "dma-descriptor-builder.h" ]
  public_deps = [
    "//sdk/banjo/fuchsia.hardware.sdmmc:fuchsia.hardware.sdmmc_banjo_cpp",
    "//sdk/lib/driver/logging/cpp",
    "//src/devices/block/lib/sdmmc",
    "//src/lib/vmo_store",
    "//zircon/system/ulib/fbl",
    "//zircon/system/ulib/fzl",
    "//zircon/system/ulib/hwreg",
    "//zircon/system/ulib/zx",
  ]
}

driver_bind_rules("sdhci-bind") {
  rules = "meta/sdhci.bind"
  tests = "meta/sdhci-bind-test.json"
  deps = [ "//src/devices/bind/fuchsia.sdmmc" ]
}

source_set("sdhci-driver-source") {
  configs += [
    "//build/config:all_source",
    "//build/config/fuchsia:enable_zircon_asserts",
  ]
  sources = [
    "sdhci-reg.h",
    "sdhci.cc",
    "sdhci.h",
  ]
  deps = [
    ":sdhci-bind",
    "//sdk/fidl/fuchsia.hardware.block:fuchsia.hardware.block_cpp",
    "//sdk/fidl/fuchsia.hardware.block.driver:fuchsia.hardware.block.driver_banjo_cpp",
    "//sdk/fidl/fuchsia.hardware.sdmmc:fuchsia.hardware.sdmmc_bindlib_cpp",
    "//sdk/lib/driver/runtime:driver_runtime_cpp",
    "//src/devices/bin/driver_runtime",
    "//zircon/system/ulib/fbl",
  ]
  public_deps = [
    ":dma-descriptor-builder",
    "//sdk/banjo/fuchsia.hardware.sdmmc:fuchsia.hardware.sdmmc_banjo_cpp",
    "//sdk/fidl/fuchsia.hardware.sdhci:fuchsia.hardware.sdhci_cpp",
    "//sdk/fidl/fuchsia.hardware.sdmmc:fuchsia.hardware.sdmmc_cpp",
    "//sdk/lib/driver/compat/cpp",
    "//sdk/lib/driver/metadata/cpp",
    "//src/devices/block/lib/sdmmc",
    "//src/devices/lib/dma-buffer",
    "//src/devices/lib/mmio",
    "//src/lib/vmo_store",
    "//zircon/system/ulib/fzl",
    "//zircon/system/ulib/sync",
    "//zircon/system/ulib/zircon-internal",
    "//zircon/system/ulib/zx",
  ]
}

fuchsia_cc_driver("sdhci-driver") {
  output_name = "sdhci"
  sources = [ "export.cc" ]
  deps = [ ":sdhci-driver-source" ]
}

fuchsia_driver_component("sdhci") {
  component_name = "sdhci"
  deps = [
    ":sdhci-bind",
    ":sdhci-driver",
  ]
  info = "meta/sdhci-info.json"
  manifest = "meta/sdhci.cml"
}

fuchsia_driver_package("package") {
  package_name = "sdhci"
  driver_components = [ ":sdhci" ]
}

test("sdhci-test-bin") {
  output_name = "sdhci-test"
  sources = [ "sdhci-test.cc" ]
  deps = [
    ":sdhci-driver-source",
    "//sdk/lib/async-default",
    "//sdk/lib/driver/fake-bti/cpp",
    "//sdk/lib/driver/testing/cpp",
    "//src/devices/lib/mmio",
    "//src/devices/lib/mmio:test_helper",
    "//src/lib/fxl/test:gtest_main",
    "//src/lib/testing/predicates",
    "//zircon/system/ulib/mmio-ptr:mmio-ptr-fake",
    "//zircon/system/ulib/sync",
  ]
}

fuchsia_unittest_package("sdhci-test") {
  deps = [ ":sdhci-test-bin" ]
  test_specs = {
    environments = basic_envs
    log_settings = {
      max_severity = "ERROR"
    }
  }
}

group("tests") {
  testonly = true
  deps = [
    ":sdhci-bind_test",
    ":sdhci-test",
  ]
}

export_fuchsia_package_to_bazel("package_for_bazel") {
  package = ":package"
}

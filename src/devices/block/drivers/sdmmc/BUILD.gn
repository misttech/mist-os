# Copyright 2019 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/bind/bind.gni")
import("//build/components.gni")
import("//build/drivers.gni")
import("//build/test.gni")
import("//tools/power_config/build/config.gni")

driver_bind_rules("sdmmc-bind") {
  rules = "meta/sdmmc.bind"
  bind_output = "sdmmc.bindbc"
  tests = "meta/sdmmc-bind-test.json"
  deps = [
    "//sdk/fidl/fuchsia.hardware.sdmmc:fuchsia.hardware.sdmmc_bindlib",
    "//src/devices/bind/fuchsia.sdmmc",
  ]
}

source_set("sdmmc-driver-source") {
  configs += [
    "//build/config:all_source",
    "//build/config/fuchsia:enable_zircon_asserts",
  ]
  sources = [
    "mmc.cc",
    "sd.cc",
    "sdio-controller-device.cc",
    "sdio-controller-device.h",
    "sdio-function-device.cc",
    "sdio-function-device.h",
    "sdmmc-block-device.cc",
    "sdmmc-block-device.h",
    "sdmmc-device.cc",
    "sdmmc-device.h",
    "sdmmc-partition-device.cc",
    "sdmmc-partition-device.h",
    "sdmmc-root-device.cc",
    "sdmmc-root-device.h",
    "sdmmc-rpmb-device.cc",
    "sdmmc-rpmb-device.h",
  ]
  deps = [
    "//sdk/fidl/fuchsia.hardware.block.volume:fuchsia.hardware.block.volume_cpp",
    "//sdk/fidl/fuchsia.hardware.power:fuchsia.hardware.power_cpp",
    "//sdk/fidl/fuchsia.io:fuchsia.io_cpp",
    "//sdk/fidl/fuchsia.power.broker:fuchsia.power.broker_cpp",
    "//sdk/fidl/fuchsia.power.system:fuchsia.power.system_cpp",
    "//sdk/lib/driver/component/cpp:cpp",
    "//sdk/lib/driver/devfs/cpp",
    "//sdk/lib/driver/metadata/cpp",
    "//sdk/lib/driver/runtime:driver_runtime",
    "//sdk/lib/driver/runtime:driver_runtime_cpp",
    "//sdk/lib/inspect/component/cpp",
    "//src/devices/bin/driver_runtime",
    "//src/devices/bind/fuchsia:fuchsia_cpp",
    "//src/devices/block/lib/common",
    "//tools/power_config/lib/cpp",
    "//zircon/system/ulib/inspect",
    "//zircon/system/ulib/pretty",
    "//zircon/system/ulib/sync",
    "//zircon/system/ulib/sync:sync-cpp",
    "//zircon/system/ulib/trace",
    "//zircon/system/ulib/zx",
    "//zircon/third_party/ulib/safemath",
  ]
  public_deps = [
    ":sdmmc-config",
    "//sdk/banjo/fuchsia.hardware.block.partition:fuchsia.hardware.block.partition_banjo_cpp",
    "//sdk/banjo/fuchsia.hardware.sdio:fuchsia.hardware.sdio_banjo_cpp",
    "//sdk/banjo/fuchsia.hardware.sdmmc:fuchsia.hardware.sdmmc_banjo_cpp",
    "//sdk/fidl/fuchsia.hardware.block.driver:fuchsia.hardware.block.driver_banjo_cpp",
    "//sdk/fidl/fuchsia.hardware.rpmb:fuchsia.hardware.rpmb_cpp",
    "//sdk/fidl/fuchsia.hardware.sdio:fuchsia.hardware.sdio_cpp",
    "//sdk/fidl/fuchsia.hardware.sdmmc:fuchsia.hardware.sdmmc_cpp",
    "//sdk/lib/async-loop:async-loop-cpp",
    "//sdk/lib/async-loop:async-loop-default",
    "//sdk/lib/driver/compat/cpp",
    "//sdk/lib/driver/component/cpp",
    "//sdk/lib/driver/power/cpp",
    "//src/devices/bind/fuchsia.sdio:fuchsia.sdio_cpp",
    "//src/devices/block/lib/sdio",
    "//src/devices/block/lib/sdmmc",
    "//src/devices/lib/dev-operation",
    "//src/storage/lib/block_server:block_server_cc",
    "//zircon/system/ulib/fbl",
    "//zircon/system/ulib/fzl",
    "//zircon/system/ulib/zircon-internal",
  ]
}

fuchsia_cc_driver("sdmmc-driver") {
  output_name = "sdmmc"
  sources = [ "driver-export.cc" ]
  deps = [ ":sdmmc-driver-source" ]
}

fuchsia_component_manifest("sdmmc-manifest") {
  component_name = "sdmmc"
  manifest = "meta/sdmmc.cml"
}

fuchsia_driver_component("sdmmc") {
  visibility = [
    ":*",
    "//src/devices/block:drivers",
  ]
  cm_label = ":sdmmc-manifest"
  deps = [
    ":sdmmc-bind",
    ":sdmmc-driver",
  ]
  info = "meta/sdmmc-info.json"
}

fuchsia_structured_config_cpp_elf_lib("sdmmc-config") {
  cm_label = ":sdmmc-manifest"
}

fuchsia_structured_config_values("sc-values") {
  cm_label = ":sdmmc-manifest"
  values = {
    enable_suspend = false
  }
}

power_config("power-config") {
  output_name = "power_config.fidl"
  value_file = "power_config.json5"
}

fuchsia_driver_package("package") {
  package_name = "sdmmc"
  driver_components = [ ":sdmmc" ]
  deps = [
    ":power-config",
    ":sc-values",
  ]
}

source_set("fake-sdmmc-device") {
  sources = [
    "fake-sdmmc-device.cc",
    "fake-sdmmc-device.h",
  ]
  deps = [ "//src/devices/block/lib/sdio" ]
  public_deps = [
    "//sdk/banjo/fuchsia.hardware.sdio:fuchsia.hardware.sdio_banjo_cpp",
    "//sdk/banjo/fuchsia.hardware.sdmmc:fuchsia.hardware.sdmmc_banjo_cpp",
    "//sdk/fidl/fuchsia.hardware.sdmmc:fuchsia.hardware.sdmmc_cpp",
    "//sdk/lib/driver/testing/cpp",
    "//src/lib/vmo_store",
  ]
}

test("sdmmc-block-test-bin") {
  configs += [ "//build/config:all_source" ]
  output_name = "sdmmc-block-test"
  sources = [ "sdmmc-block-device-test.cc" ]
  deps = [
    ":fake-sdmmc-device",
    ":sdmmc-driver-source",
    "//sdk/fidl/fuchsia.hardware.block:fuchsia.hardware.block_cpp",
    "//sdk/lib/async_patterns/testing/cpp",
    "//sdk/lib/driver/metadata/cpp",
    "//sdk/lib/driver/power/cpp/testing",
    "//sdk/lib/driver/testing/cpp:cpp",
    "//sdk/lib/inspect/testing/cpp:zxtest",
    "//src/storage/lib/block_client/cpp",
    "//zircon/system/ulib/fdio-caller",
    "//zircon/system/ulib/zxtest",
  ]
}

fuchsia_unittest_package("sdmmc-block-test") {
  deps = [
    ":power-config",
    ":sdmmc-block-test-bin",
  ]
  test_specs = {
    environments = basic_envs
    log_settings = {
      max_severity = "ERROR"
    }
  }
}

test("sdio-controller-test-bin") {
  output_name = "sdio-controller-test"
  sources = [ "sdio-controller-device-test.cc" ]
  deps = [
    ":fake-sdmmc-device",
    ":sdmmc-driver-source",
    "//sdk/fidl/fuchsia.hardware.block:fuchsia.hardware.block_cpp",
    "//sdk/lib/driver/metadata/cpp",
    "//sdk/lib/driver/testing/cpp",
    "//src/lib/fxl/test:gtest_main",
    "//src/lib/testing/predicates",
  ]
}

fuchsia_unittest_package("sdio-controller-test") {
  deps = [ ":sdio-controller-test-bin" ]
  test_specs = {
    environments = basic_envs
    log_settings = {
      max_severity = "ERROR"
    }
  }
}

group("tests") {
  testonly = true
  deps = [
    ":sdio-controller-test",
    ":sdmmc-bind_test",
    ":sdmmc-block-test",
  ]
}

# Copyright 2024 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/bind/bind.gni")
import("//build/components.gni")
import("//build/drivers.gni")

group("tests") {
  testonly = true
  public_deps = [
    ":aml-suspend-bind",
    ":aml-suspend-bind-with-fake-syscall",
    "tests:aml-suspend-test-bin",
    "tests:aml-suspend-test-package",
  ]
}

group("drivers") {
  testonly = true
  deps = [
    ":aml-suspend",
    ":aml-suspend-with-fake-syscall",
  ]
}

driver_bind_rules("aml-suspend-bind") {
  rules = "meta/aml-suspend.bind"
  bind_output = "aml-suspend.bindbc"
  tests = "meta/aml-suspend-bind-test.json"
  deps = [
    "//src/devices/bind/fuchsia.amlogic.platform",
    "//src/devices/bind/fuchsia.devicetree",
    "//src/devices/bind/fuchsia.platform",
  ]
}

_DEPS = [
  "//sdk/fidl/fuchsia.io:fuchsia.io_cpp",
  "//sdk/fidl/fuchsia.driver.compat:fuchsia.driver.compat_cpp",
  "//sdk/fidl/fuchsia.hardware.platform.bus:fuchsia.hardware.platform.bus_cpp",
  "//sdk/fidl/fuchsia.hardware.platform.device:fuchsia.hardware.platform.device_cpp",
  "//sdk/fidl/fuchsia.hardware.suspend:fuchsia.hardware.suspend_cpp",
  "//sdk/fidl/fuchsia.kernel:fuchsia.kernel_cpp",
  "//sdk/fidl/fuchsia.power.observability:fuchsia.power.observability_cpp",
  "//sdk/lib/driver/compat/cpp",
  "//sdk/lib/driver/component/cpp:cpp",
  "//sdk/lib/driver/logging/cpp:cpp",
  "//sdk/lib/driver/runtime:driver_runtime_cpp",
  "//sdk/lib/inspect/component/cpp",
  "//sdk/lib/inspect/contrib/cpp:bounded_list_node",
  "//src/devices/bin/driver_runtime",
  "//src/devices/bus/lib/device-protocol-pdev",
  "//src/devices/lib/driver:driver_runtime",
  "//src/devices/lib/mmio",
  "//zircon/system/ulib/fbl",
  "//zircon/system/ulib/hwreg",
  "//zircon/system/ulib/inspect",
  "//zircon/system/ulib/zx",
]

_SOURCES = [
  "aml-suspend.cc",
  "driver-registration.cc",
]

fuchsia_cc_driver("aml-suspend-driver") {
  output_name = "aml-suspend"
  sources = _SOURCES
  deps = _DEPS
  deps += [ ":aml-suspend-bind" ]
}

fuchsia_driver_component("aml-suspend") {
  component_name = "aml-suspend"
  deps = [ ":aml-suspend-driver" ]
  info = "meta/aml-suspend-info.json"
  manifest = "meta/aml-suspend.cml"
}

fuchsia_driver_package("package") {
  package_name = "aml-suspend"
  driver_components = [ ":aml-suspend" ]
}

# Test fixtures

driver_bind_rules("aml-suspend-bind-with-fake-syscall") {
  testonly = true
  rules = "meta/aml-suspend-with-fake-syscall.bind"
  bind_output = "aml-suspend-with-fake-syscall.bindbc"
  tests = "meta/aml-suspend-bind-test-with-fake-syscall.json"
  deps = [ "//src/devices/bind/fuchsia.test" ]
}

fuchsia_cc_driver("aml-suspend-driver-with-fake-syscall") {
  testonly = true
  output_name = "aml-suspend-with-fake-syscall"
  sources = _SOURCES
  sources -= [ "driver-registration.cc" ]
  sources += [ "tests/aml-suspend-for-tests.cc" ]

  deps = _DEPS
  deps += [
    ":aml-suspend-bind-with-fake-syscall",

    # Overrides the default vDSO call to zx_system_suspend_enter.
    "//src/devices/testing/syscall-intercept:lib",
  ]
}

fuchsia_driver_component("aml-suspend-with-fake-syscall") {
  testonly = true
  deps = [ ":aml-suspend-driver-with-fake-syscall" ]
  info = "meta/aml-suspend-info.json"
  manifest = "meta/aml-suspend-with-fake-syscall.cml"
}

fuchsia_driver_package("package-with-fake-syscall") {
  testonly = true
  package_name = "aml-suspend-with-fake-syscall"
  driver_components = [ ":aml-suspend-with-fake-syscall" ]
}

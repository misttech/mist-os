# Copyright 2024 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/components.gni")
import("//build/drivers.gni")
import("//build/rust/rustc_test.gni")

driver_bind_rules("bind") {
  rules = "meta/failer.bind"
  deps = [ "//src/devices/bind/fuchsia.test" ]
  bind_output = "failer.bindbc"
}

driver_bind_rules("replacement-bind") {
  rules = "meta/failer-replacement.bind"
  deps = [ "//src/devices/bind/fuchsia.test" ]
  bind_output = "failer-replacement.bindbc"
}

fuchsia_cc_driver("driver") {
  testonly = true
  output_name = "fail-to-start-driver"
  sources = [ "driver.cc" ]
  deps = [
    "//sdk/lib/driver/component/cpp",
    "//src/devices/lib/driver:driver_runtime",
  ]
}

fuchsia_cc_driver("replacement-driver") {
  testonly = true
  output_name = "fail-to-start-replacement-driver"
  sources = [ "replacement-driver.cc" ]
  deps = [
    "//sdk/lib/driver/component/cpp",
    "//src/devices/lib/driver:driver_runtime",
  ]
}

fuchsia_driver_component("component") {
  testonly = true
  manifest = "meta/fail-to-start.cml"
  component_name = "fail-to-start"
  deps = [
    ":bind",
    ":driver",
  ]
  info = "meta/info.json"
}

fuchsia_driver_component("replacement-component") {
  testonly = true
  manifest = "meta/fail-to-start-replacement.cml"
  component_name = "fail-to-start-replacement"
  deps = [
    ":replacement-bind",
    ":replacement-driver",
  ]
  info = "meta/info.json"
}

group("tests") {
  testonly = true
  deps = [ ":replacefaileddriver_test" ]
}

group("drivers") {
  testonly = true
  deps = [
    ":component",
    ":replacement-component",
  ]
}

rustc_test("bin") {
  edition = "2021"
  testonly = true
  output_name = "replacefaileddriver_bin"
  source_root = "test.rs"
  sources = [ "test.rs" ]
  deps = [
    "//sdk/fidl/fuchsia.driver.development:fuchsia.driver.development_rust",
    "//sdk/fidl/fuchsia.driver.registrar:fuchsia.driver.registrar_rust",
    "//sdk/fidl/fuchsia.driver.test:fuchsia.driver.test_rust",
    "//sdk/lib/driver_test_realm/realm_builder/rust",
    "//src/lib/fidl/rust/fidl",
    "//src/lib/fuchsia-async",
    "//src/lib/fuchsia-component-test",
    "//third_party/rust_crates:anyhow",
  ]
}

fuchsia_unittest_package("replacefaileddriver_test") {
  test_specs = {
    log_settings = {
      max_severity = "ERROR"
    }
  }
  deps = [
    ":bin",
    ":component",
    ":replacement-component",
    "//src/devices/misc/drivers/test-parent",
  ]
}

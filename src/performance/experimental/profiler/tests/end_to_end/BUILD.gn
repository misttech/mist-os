# Copyright 2024 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.
import("//build/python/python_mobly_test.gni")

if (is_host) {
  python_mobly_test("cpu_profiler") {
    main_source = "cpu_profiler_e2e_test.py"
    libraries = [
      # Honeydew provides device interaction APIs.
      "//src/testing/end_to_end/honeydew",

      # Base class provides common Fuchsia testing setup and teardown logic.
      "//src/testing/end_to_end/mobly_base_tests:fuchsia_base_test",
    ]
    deps = [
      "//src/developer/ffx/tools/profiler:ffx_profiler_host_tool",
      "//src/developer/ffx/tools/profiler:ffx_profiler_test_data",
    ]
    environments = [
      # Only enable for AEMU-x64 as host-tests aren't supported on QEMU-arm64.
      aemu_env,
      vim3_env,
      nuc11_env,
    ]
  }
}

# Adding to a mobly test dep adds it to the host dependencies, but we want our packages to profile built for the target.
group("package_deps") {
  testonly = true
  deps = [
    "//src/performance/experimental/profiler/tests:gtest_target",
    "//src/performance/experimental/profiler/tests/demo_target",
  ]
}

group("e2e_tests") {
  testonly = true
  deps = [
    ":cpu_profiler($host_toolchain)",
    ":package_deps($target_toolchain)",
  ]
}

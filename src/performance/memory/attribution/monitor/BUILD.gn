# Copyright 2024 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/components.gni")
import("//build/components/fuchsia_unittest_package.gni")
import("//build/packages/sdk_fuchsia_package.gni")
import("//build/rust/rustc_binary.gni")
import("//build/rust/rustc_library.gni")
import("//build/rust/rustc_staticlib.gni")
import("//build/test.gni")

rustc_binary("bin") {
  output_name = "memory-monitor2"
  with_unit_tests = true

  edition = "2021"
  sources = [
    "src/attribution_client.rs",
    "src/common.rs",
    "src/main.rs",
    "src/resources.rs",
    "src/snapshot.rs",
  ]

  deps = [
    ":inspect_nodes",
    ":traces",
    "//sdk/fidl/fuchsia.component:fuchsia.component_rust",
    "//sdk/fidl/fuchsia.kernel:fuchsia.kernel_rust",
    "//sdk/fidl/fuchsia.memory.attribution:fuchsia.memory.attribution_rust",
    "//sdk/fidl/fuchsia.memory.attribution.plugin:fuchsia.memory.attribution.plugin_rust",
    "//sdk/rust/zx",
    "//src/lib/fidl/rust/fidl",
    "//src/lib/fuchsia",
    "//src/lib/fuchsia-async",
    "//src/lib/fuchsia-component",
    "//src/lib/fuchsia-sync",
    "//src/lib/mapped-vmo",
    "//src/lib/trace/rust:trace",
    "//third_party/rust_crates:anyhow",
    "//third_party/rust_crates:futures",
    "//third_party/rust_crates:tracing",
  ]

  test_deps = [
    "//sdk/rust/zx",
    "//src/lib/fidl/rust/fidl",
    "//src/lib/fuchsia-async",
    "//src/performance/memory/attribution/server",
    "//third_party/rust_crates:futures",
  ]
}

rustc_library("traces") {
  edition = "2021"
  with_unit_tests = true
  source_root = "src/traces/lib.rs"
  sources = [
    "src/traces/kernel.rs",
    "src/traces/lib.rs",
    "src/traces/watcher.rs",
  ]
  deps = [
    "//sdk/fidl/fuchsia.kernel:fuchsia.kernel_rust",
    "//src/lib/fuchsia-async",
    "//src/lib/trace/rust:trace",
    "//src/lib/trace/rust:trace-observer",
    "//src/lib/trace/rust:trace-provider",
    "//third_party/rust_crates:anyhow",
    "//third_party/rust_crates:futures",
    "//third_party/rust_crates:tokio",
    "//third_party/rust_crates:tracing",
  ]
}

rustc_library("inspect_nodes") {
  edition = "2021"
  with_unit_tests = true
  source_root = "src/inspect_nodes/lib.rs"
  sources = [ "src/inspect_nodes/lib.rs" ]
  deps = [
    "//sdk/fidl/fuchsia.kernel:fuchsia.kernel_rust",
    "//src/lib/diagnostics/inspect/runtime/rust",
    "//src/lib/diagnostics/inspect/rust",
    "//third_party/rust_crates:anyhow",
    "//third_party/rust_crates:futures",
    "//third_party/rust_crates:tracing",
  ]
}

fuchsia_package_with_single_component("monitor") {
  package_name = "memory_monitor2"
  manifest = "meta/memory_monitor2.cml"
  deps = [ ":bin" ]
}

fuchsia_unittest_package("memory-monitor2-tests") {
  package_name = "memory-monitor2-tests"
  deps = [ ":bin_test" ]
}

rustc_staticlib("memory-monitor2-trace-rust-cpp-lib") {
  name = "rust_cpp_test_lib"
  version = "0.1.0"
  edition = "2021"
  testonly = true

  deps = [
    ":traces",
    "//sdk/fidl/fuchsia.kernel:fuchsia.kernel_rust",
    "//src/lib/fidl/rust/fidl",
    "//src/lib/fuchsia-async",
    "//third_party/rust_crates:tokio",
    "//third_party/rust_crates:tracing-subscriber",
  ]

  public = [ "tests/traces/lib.h" ]

  source_root = "tests/traces/lib.rs"
  sources = [ "tests/traces/lib.rs" ]
}

test("memory-monitor2-trace-test") {
  sources = [ "tests/traces/test_runner.cc" ]
  deps = [
    ":memory-monitor2-trace-rust-cpp-lib",
    "//sdk/lib/async",
    "//sdk/lib/async:async-cpp",
    "//sdk/lib/async-default",
    "//sdk/lib/async-loop",
    "//sdk/lib/async-loop:async-loop-cpp",
    "//sdk/lib/async-loop:async-loop-default",
    "//sdk/lib/fdio",
    "//testing/gtest:gtest_main",
    "//third_party/googletest:gtest",
    "//zircon/system/ulib/fbl",
    "//zircon/system/ulib/trace",
    "//zircon/system/ulib/trace-engine",
    "//zircon/system/ulib/trace-engine:trace-engine-headers-for-reader",
    "//zircon/system/ulib/trace-reader",
    "//zircon/system/ulib/trace-test-utils",
    "//zircon/system/ulib/zx",
  ]
}

fuchsia_unittest_component("memory-monitor2-trace-test-component") {
  deps = [ ":memory-monitor2-trace-test" ]
}

fuchsia_test_package("memory-monitor2-trace-test-pkg") {
  test_components = [ ":memory-monitor2-trace-test-component" ]
  test_specs = {
    environments = [ emu_env ]
  }
}

group("tests") {
  testonly = true
  deps = [
    ":memory-monitor2-tests",
    ":memory-monitor2-trace-test-pkg",
  ]
}

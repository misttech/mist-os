# Copyright 2024 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/bind/bind.gni")
import("//build/components.gni")
import("//build/drivers.gni")
import("//build/test.gni")

driver_bind_rules("bind") {
  rules = "meta/child-driver.bind"
  bind_output = "child-driver.bindbc"
  deps = [ "//sdk/fidl/fuchsia.hardware.i2c:fuchsia.hardware.i2c_bindlib" ]
}

fuchsia_rust_driver("driver") {
  edition = "2021"

  output_name = "zircon_transport_rust_child"
  sources = [ "src/lib.rs" ]
  deps = [
    "//sdk/fidl/fuchsia.driver.framework:fuchsia.driver.framework_rust",
    "//sdk/fidl/fuchsia.hardware.i2c:fuchsia.hardware.i2c_rust",
    "//sdk/fidl/fuchsia.io:fuchsia.io_rust",
    "//sdk/lib/driver/component/rust",
    "//sdk/lib/driver/runtime/rust",
    "//sdk/rust/zx",
    "//src/devices/bind/fuchsia.test:fuchsia.test_rust",
    "//src/lib/fidl/rust/fidl",
    "//src/lib/fuchsia-async",
    "//src/lib/fuchsia-component",
    "//src/sys/lib/cm_types",
    "//third_party/rust_crates:futures",
    "//third_party/rust_crates:log",
  ]
}

fuchsia_driver_component("component") {
  component_name = "zircon_transport_child"
  manifest = "meta/child-driver.cml"
  deps = [
    ":bind",
    ":driver",
  ]
  info = "meta/component-info.json"
}

test("test") {
  output_name = "zircon-rust-driver-child-test"
  sources = [ "child-driver-test.cc" ]
  deps = [
    ":driver-test-staticlib",
    "//sdk/fidl/fuchsia.hardware.i2c:fuchsia.hardware.i2c_cpp",
    "//sdk/lib/driver/testing/cpp",
    "//src/devices/bind/fuchsia.test:fuchsia.test_cpp",
    "//src/lib/fxl/test:gtest_main",
  ]
}

fuchsia_unittest_package("hermetic-test-pkg") {
  package_name = "zircon-rust-driver-child-test"
  deps = [ ":test" ]
}

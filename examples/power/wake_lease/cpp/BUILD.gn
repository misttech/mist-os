# Copyright 2024 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/components.gni")

group("cpp") {
  deps = [ ":wake_lease_cpp" ]
}

static_library("wake_lease_cpp") {
  sources = [ "wake_lease.cc" ]
  public = [ "wake_lease.h" ]
  public_deps = [
    "//sdk/fidl/fuchsia.power.system:fuchsia.power.system_cpp",
    "//sdk/lib/fit",
  ]
}

executable("wake_lease_cpp_integration_test") {
  testonly = true
  sources = [ "wake_lease_integration_test.cc" ]
  deps = [
    ":wake_lease_cpp",
    "//sdk/fidl/fuchsia.power.broker:fuchsia.power.broker_cpp",
    "//sdk/fidl/fuchsia.power.system:fuchsia.power.system_cpp",
    "//sdk/lib/component/incoming/cpp",
    "//sdk/lib/driver/power/cpp/testing",
    "//sdk/lib/fit",
    "//src/lib/fxl/test:gtest_main",
    "//src/lib/testing/loop_fixture:gtest",
  ]
}

executable("wake_lease_cpp_unittest") {
  testonly = true
  sources = [ "wake_lease_unittest.cc" ]
  deps = [
    ":wake_lease_cpp",
    "//sdk/lib/driver/power/cpp/testing",
    "//sdk/lib/fit",
    "//src/lib/fxl/test:gtest_main",
    "//src/lib/testing/loop_fixture:gtest",
  ]
}

fuchsia_test_component("wake-lease-cpp-integration-test") {
  manifest = "meta/wake_lease_cpp_integration_test.cml"
  deps = [ ":wake_lease_cpp_integration_test" ]
}

fuchsia_unittest_component("wake-lease-cpp-unittest") {
  deps = [ ":wake_lease_cpp_unittest" ]
}

fuchsia_test_package("tests") {
  package_name = "wake-lease-example-cpp-tests"
  test_components = [
    ":wake-lease-cpp-integration-test",
    ":wake-lease-cpp-unittest",
  ]
  deps = [
    # Other components that we are injecting into integration tests.
    "//src/power/broker:component",
  ]
  subpackages = [
    "//src/power/system-activity-governor:package",
    "//src/power/system-activity-governor:config-no-suspender",
  ]
}

# Copyright 2024 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

source_set("runtime") {
  public = [ "runtime-dynamic-linker.h" ]
  public_deps = [
    ":diagnostics",
    ":error",
    "//sdk/lib/fit",
    "//src/lib/elfldltl",
    "//zircon/system/ulib/fbl",
  ]
  if (is_fuchsia || is_mistos) {
    public_deps += [ "//zircon/system/ulib/zx" ]
  }

  sources = [
    "linking-session.h",
    "runtime-dynamic-linker.cc",
    "runtime-module.cc",
    "runtime-module.h",
  ]
  deps = [ ":concat-view" ]
  if (is_fuchsia || is_mistos) {
    sources += [ "unmap-zircon.cc" ]
  } else {
    sources += [ "unmap-posix.cc" ]
  }
}

source_set("error") {
  public = [ "error.h" ]
  public_deps = [ "//sdk/lib/ld:headers" ]
  sources = [ "error.cc" ]
}

source_set("diagnostics") {
  public = [ "diagnostics.h" ]
  public_deps = [ ":error" ]
  sources = [ "diagnostics.cc" ]
}

source_set("stateful-error") {
  public = [ "stateful-error.h" ]
  public_deps = [
    ":error",
    "//sdk/lib/fit",
  ]
  sources = [ "stateful-error.cc" ]
}

source_set("concat-view") {
  public = [ "concat-view.h" ]
}

group("tests") {
  testonly = true
  deps = [ "test:tests" ]
}

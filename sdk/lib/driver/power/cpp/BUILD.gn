# Copyright 2024 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/components.gni")
import("//build/cpp/sdk_source_set.gni")
import("//build/test.gni")

group("tests") {
  testonly = true

  deps = [ ":power-test-pkg" ]
}

sdk_source_set("cpp") {
  category = "partner"
  stable = false
  sdk_name = "driver_power_cpp"
  include_base = "//sdk"

  sources = [
    "element-description-builder.cc",
    "element-description-builder.h",
    "power-support.cc",
    "power-support.h",
    "types.cc",
    "types.h",
    "wake-lease.cc",
    "wake-lease.h",
  ]

  public_deps = [
    "//sdk/fidl/fuchsia.hardware.power:fuchsia.hardware.power_cpp",
    "//sdk/fidl/fuchsia.power.broker:fuchsia.power.broker_cpp",
    "//sdk/fidl/fuchsia.power.system:fuchsia.power.system_cpp",
    "//sdk/lib/async:async-cpp",
    "//sdk/lib/driver/incoming/cpp",
    "//sdk/lib/driver/logging/cpp",
    "//sdk/lib/fidl",
    "//zircon/system/ulib/inspect",
    "//zircon/system/ulib/zx",
  ]

  deps = [
    "//sdk/lib/driver/component/cpp",
    "//sdk/lib/driver/logging/cpp",
  ]
}

test("power-test") {
  sources = [
    "element-description-builder-tests.cc",
    "power-support-tests.cc",
    "types-test.cc",
  ]
  deps = [
    ":cpp",
    "testing",
    "//sdk/fidl/fuchsia.component.runner:fuchsia.component.runner_cpp",
    "//sdk/fidl/fuchsia.hardware.power:fuchsia.hardware.power_cpp",
    "//sdk/fidl/fuchsia.power.broker:fuchsia.power.broker_cpp",
    "//sdk/fidl/fuchsia.power.system:fuchsia.power.system_cpp",
    "//sdk/lib/async-loop-testing/cpp",
    "//sdk/lib/driver/component/cpp",
    "//sdk/lib/fidl",
    "//sdk/lib/fit-promise",
    "//sdk/lib/inspect/testing/cpp",
    "//sdk/lib/sys/component/cpp/testing:cpp",
    "//sdk/lib/sys/service/cpp",
    "//src/lib/fxl/test:gtest_main",
    "//src/lib/testing/loop_fixture",
    "//src/storage/lib/vfs/cpp",
    "//third_party/googletest:gtest",
    "//zircon/system/ulib/fbl",
  ]
}

fuchsia_unittest_package("power-test-pkg") {
  package_name = "power-tests"

  deps = [
    ":power-test",
    "//src/power/broker:component",
  ]
}

# Copyright 2022 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/components.gni")
import("//build/fidl/fidl.gni")
import("//build/test.gni")
import("//build/testing/cc_test_executable.gni")

group("tests") {
  testonly = true
  deps = [
    ":component_cpp_incoming_tests",
    ":component_cpp_outgoing_directory_tests",
    ":component_cpp_service_server_tests",
  ]
}

fidl("fidl.service.test") {
  testonly = true
  visibility = [
    ":*",

    # TODO(https://fxbug.dev/42064416): Migrate all the remaining libs in //sdk/lib/sys/component/cpp
    # and its tests to //sdk/lib/component and remove this visibility target.
    "//sdk/lib/sys/component/cpp/tests:*",
  ]
  sources = [ "test.test.fidl" ]
  excluded_checks = [
    "disallowed-library-name-component",
    "string-bounds-not-specified",
  ]
  public_deps = [
    "//sdk/fidl/fuchsia.io",
    "//sdk/fidl/fuchsia.unknown",
  ]
  enable_hlcpp = true
}

test("incoming_test_bin") {
  sources = [ "incoming_test.cc" ]
  deps = [
    ":fidl.service.test_cpp_testing",
    "//sdk/lib/async-loop:async-loop-cpp",
    "//sdk/lib/async-loop:async-loop-default",
    "//sdk/lib/component/incoming/cpp",
    "//sdk/lib/component/outgoing/cpp",
    "//src/lib/fxl/test:gtest_main",
    "//src/lib/testing/loop_fixture",
    "//src/lib/testing/predicates",
  ]
}

fuchsia_unittest_component("incoming_test") {
  deps = [ ":incoming_test_bin" ]
}

cc_test_executable("incoming_integration_test_bin") {
  testonly = true

  sources = [ "incoming_integration_test.cc" ]

  deps = [
    ":fidl.service.test_hlcpp",
    "//sdk/lib/component/incoming/cpp",
    "//sdk/lib/component/tests:fidl.service.test_cpp",
    "//sdk/lib/fidl/hlcpp",
    "//sdk/lib/sys/component/cpp/testing:cpp",
    "//sdk/lib/sys/cpp",
    "//sdk/lib/syslog/cpp",
    "//src/lib/fxl/test:gtest_main",
    "//src/lib/testing/loop_fixture",
  ]
}

fuchsia_component("incoming_integration_test") {
  testonly = true
  deps = [ ":incoming_integration_test_bin" ]
  manifest = "meta/incoming_integration_test.cml"
}

cc_test_executable("service_watcher_unittests") {
  testonly = true

  sources = [ "service_watcher_test.cc" ]

  deps = [
    "//examples/fidl/fuchsia.examples:fuchsia.examples_cpp",
    "//sdk/lib/async-testing",
    "//sdk/lib/component/incoming/cpp",
    "//src/lib/fxl/test:gtest_main",
    "//third_party/googletest:gmock",
    "//zircon/system/ulib/fbl",
  ]
}

fuchsia_component("service_watcher_tests") {
  testonly = true
  deps = [ ":service_watcher_unittests" ]
  manifest = "meta/service_watcher_unittests.cml"
}

executable("echo_service_client_bin") {
  testonly = true

  sources = [ "echo_service_client.cc" ]

  deps = [
    ":fidl.service.test_cpp",
    "//sdk/lib/component/incoming/cpp",
    "//sdk/lib/syslog/cpp",
  ]
}

fuchsia_component("echo_service_client") {
  testonly = true
  deps = [ ":echo_service_client_bin" ]
  manifest = "meta/echo_service_client.cml"
}

executable("echo_client_bin") {
  testonly = true

  sources = [ "echo_client.cc" ]

  deps = [
    ":fidl.service.test_cpp",
    "//sdk/lib/component/incoming/cpp",
    "//sdk/lib/syslog/cpp",
  ]
}

fuchsia_component("echo_client") {
  testonly = true
  deps = [ ":echo_client_bin" ]
  manifest = "meta/echo_client.cml"
}

fuchsia_test_package("component_cpp_incoming_tests") {
  test_components = [
    ":incoming_test",
    ":incoming_integration_test",
    ":service_watcher_tests",
  ]

  deps = [
    ":echo_client",
    ":echo_service_client",
  ]
}

executable("echo_service_server_bin") {
  testonly = true

  sources = [ "echo_service_server.cc" ]

  deps = [
    "//examples/fidl/fuchsia.examples:fuchsia.examples_cpp",
    "//sdk/lib/async-loop:async-loop-cpp",
    "//sdk/lib/async-loop:async-loop-default",
    "//sdk/lib/component/outgoing/cpp",
    "//sdk/lib/fidl",
    "//sdk/lib/syslog/cpp",
  ]
}

fuchsia_component("echo_service_server") {
  testonly = true
  deps = [ ":echo_service_server_bin" ]
  manifest = "meta/echo_service_server.cml"
}

cc_test_executable("cpp_outgoing_directory_test_bin") {
  testonly = true

  sources = [ "outgoing_directory_test.cc" ]

  deps = [
    "//examples/fidl/fuchsia.examples:fuchsia.examples_cpp",
    "//sdk/fidl/fuchsia.io:fuchsia.io_cpp",
    "//sdk/lib/component/incoming/cpp",
    "//sdk/lib/component/outgoing/cpp",
    "//sdk/lib/fidl/cpp",
    "//src/lib/fxl/test:gtest_main",
    "//src/lib/testing/loop_fixture",
    "//src/lib/testing/predicates",
    "//src/storage/lib/vfs/cpp",
    "//third_party/googletest:gmock",
    "//zircon/system/ulib/sync:sync-cpp",
  ]
}

cc_test_executable("cpp_outgoing_directory_integration_test_bin") {
  testonly = true

  sources = [ "outgoing_directory_integration_test.cc" ]

  deps = [
    "//examples/fidl/fuchsia.examples:fuchsia.examples_hlcpp",
    "//sdk/fidl/fuchsia.component:fuchsia.component_hlcpp",
    "//sdk/fidl/fuchsia.logger:fuchsia.logger_hlcpp",
    "//sdk/lib/fidl/hlcpp",
    "//sdk/lib/sys/component/cpp/testing:cpp",
    "//sdk/lib/sys/cpp",
    "//src/lib/fxl/test:gtest_main",
    "//src/lib/testing/loop_fixture",
    "//src/storage/lib/vfs/cpp",
  ]
}

fuchsia_component("outgoing_directory_test") {
  testonly = true
  deps = [ ":cpp_outgoing_directory_test_bin" ]
  manifest = "meta/outgoing_directory_test.cml"
}

fuchsia_component("outgoing_directory_integration_test") {
  testonly = true
  deps = [ ":cpp_outgoing_directory_integration_test_bin" ]
  manifest = "meta/outgoing_directory_integration_test.cml"
}

fuchsia_test_package("component_cpp_outgoing_directory_tests") {
  test_components = [
    ":outgoing_directory_test",
    ":outgoing_directory_integration_test",
  ]
  deps = [ ":echo_service_server" ]
}

test("service_server_test") {
  sources = [ "server_test.cc" ]
  deps = [
    "//sdk/lib/async-loop:async-loop-cpp",
    "//sdk/lib/async-loop:async-loop-default",
    "//sdk/lib/component/incoming/cpp",
    "//sdk/lib/component/outgoing/cpp",
    "//sdk/lib/component/tests:fidl.service.test_cpp",
    "//sdk/lib/fdio",
    "//src/storage/lib/vfs/cpp",
    "//zircon/system/ulib/zxtest",
  ]
}

fuchsia_unittest_package("component_cpp_service_server_tests") {
  deps = [ ":service_server_test" ]
}

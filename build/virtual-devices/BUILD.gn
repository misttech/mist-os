# Copyright 2024 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.
import("//build/sdk/virtual_device.gni")

# Virtual device manifest should only be generated for emu compatible boards.
if (board_is_emu) {
  # Append '-emu' to the end of the device_name in order to make it unique for
  # cases where the board is compatible with a physical and virtual device.
  device_name = "${board_name}-emu"
  if (virtual_device_name_prefix != "") {
    device_name = "${virtual_device_name_prefix}-${device_name}"
  }

  # Add the recommended configuration first. This is the default
  # used unless a specific device is requested.
  virtual_device_specification("virtual_device_specification_recommended") {
    testonly = true
    name = "${device_name}-recommended"
    description = "Recommended configuration for running this product"
    output = "$root_build_dir/virtual_device_recommended.json"
    cpu = {
      arch = target_cpu
      count = 4
    }
  }

  # Add the Minimum configuration
  virtual_device_specification("virtual_device_specification_min") {
    testonly = true
    name = "${device_name}-min"
    description = "Minimum configuration for running this product"
    output = "$root_build_dir/virtual_device_min.json"
    memory = {
      quantity = 2048
      units = "megabytes"
    }
    cpu = {
      arch = target_cpu

      # The 1cpu environment depends on this device spec having a cpu count of 1.
      # LINT.IfChange
      count = 1

      # LINT.ThenChange(//build/testing/environments.gni)
    }
  }

  # Add a Large configuration
  virtual_device_specification("virtual_device_specification_large") {
    testonly = true
    name = "${device_name}-large"
    description =
        "Larger configuration for running this product with extra memory"
    output = "$root_build_dir/virtual_device_large.json"
    memory = {
      # The max amount of memory supported on arm64 in infra is 8GB. If a larger
      # memory size is needed for a specific use case, either use the xlarge
      # configuration or create a new device spec.
      quantity = 8192
      units = "megabytes"
    }
    cpu = {
      arch = target_cpu
      count = 4
    }
  }

  # Add an X-Large configuration
  virtual_device_specification("virtual_device_specification_xlarge") {
    testonly = true
    name = "${device_name}-xlarge"
    description = "Larger configuration for running this product with extra storage and memory"
    output = "$root_build_dir/virtual_device_xlarge.json"
    storage = {
      quantity = 10
      units = "gigabytes"
    }
    memory = {
      quantity = 28
      units = "gigabytes"
    }
    cpu = {
      arch = target_cpu
      count = 8
    }
  }
}

# Copyright 2021 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

# This file should only be imported by build files that need to know the current
# set of API levels or to update that set. This includes IDK, SDK, and FIDL.
# For the target API level of the current build, see
# //build/config/fuchsia/target_api_level.gni.

_script = "get_platform_version.py"
version_history_path = "//sdk/version_history.json"
_get_platform_version_args = [
  "-S",  # See https://fxbug.dev/42141201.
  rebase_path(_script, root_build_dir),
  "--version-history-path",
  rebase_path(version_history_path, root_build_dir),
]
_file_dependencies = [
  _script,  # See https://fxbug.dev/42141201#comment6.
  version_history_path,
]

platform_version = exec_script(python_exe_src,
                               _get_platform_version_args,
                               "json",
                               _file_dependencies)

declare_args() {
  # Overrides the set of API levels for which this build will provide build-time
  # support in the IDK/SDK. The default (`false`) set is all `supported` and
  # `in development` non-special API levels in //sdk/version_history.json.
  # Other valid values are a list containing a subset of the default set.If
  # empty, only targets for which the IDK contains artifacts built at "PLATFORM"
  # will be built.
  #
  # This is useful for reducing the overall build time of any build that
  # includes the IDK/SDK in exchange for reduced coverage of API level support.
  # For example, `fx build //sdk:final_fuchsia_idk`.
  #
  # Note: The in-tree Bazel SDK ignores this variable and the variable it
  # overrides unless `bazel_fuchsia_sdk_all_api_levels` is true.
  #
  # To override the set of target CPUs, see `override_idk_target_cpus`.
  override_idk_buildable_api_levels = false
}

if (override_idk_buildable_api_levels != false) {
  platform_version.idk_buildable_api_levels = []
  platform_version.idk_buildable_api_levels = override_idk_buildable_api_levels
}

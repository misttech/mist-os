# Copyright 2024 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/config/fuchsia/platform_version.gni")
import("//build/config/fuchsia/target_api_level.gni")

# `current_build_target_api_level` may be a string or integer. If it is not one
# of the special named API levels defined in
# https://fuchsia.dev/fuchsia-src/contribute/governance/rfcs/0246_api_levels_are_32_bits#special_api_levels,
# it must be a numbered API level.
if (current_build_target_api_level == "NEXT") {
  lesser_or_equal_fuchsia_api_levels =
      platform_version.all_numbered_api_levels + [ "NEXT" ]
  greater_fuchsia_api_levels = [
    "HEAD",
    "PLATFORM",
  ]
} else if (current_build_target_api_level == "HEAD") {
  lesser_or_equal_fuchsia_api_levels =
      platform_version.all_numbered_api_levels + [
        "NEXT",
        "HEAD",
      ]
  greater_fuchsia_api_levels = [ "PLATFORM" ]
} else if (current_build_target_api_level == "PLATFORM") {
  lesser_or_equal_fuchsia_api_levels =
      platform_version.all_numbered_api_levels + [
        "NEXT",
        "HEAD",
        "PLATFORM",
      ]
  greater_fuchsia_api_levels = []
} else {
  lesser_or_equal_fuchsia_api_levels = []
  greater_fuchsia_api_levels = []
  _target_level_found = false

  foreach(level, platform_version.all_numbered_api_levels) {
    if (level <= current_build_target_api_level) {
      lesser_or_equal_fuchsia_api_levels += [ level ]
      if (level == current_build_target_api_level) {
        _target_level_found = true
      }
    } else {
      greater_fuchsia_api_levels += [ level ]
    }
  }

  assert(
      _target_level_found,
      "${current_build_target_api_level} is not a known numbered API level (${platform_version.all_numbered_api_levels}).")

  # All numbered API levels must be below the reserved range of named API levels per
  # https://fuchsia.dev/fuchsia-src/contribute/governance/rfcs/0246_api_levels_are_32_bits#design.
  greater_fuchsia_api_levels += [
    "NEXT",
    "HEAD",
    "PLATFORM",
  ]
}
